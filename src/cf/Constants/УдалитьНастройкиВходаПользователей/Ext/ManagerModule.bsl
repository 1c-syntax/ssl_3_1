///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик обновления БСП 3.1.6.78
//
// Переносит настройки из удаляемой константы УдалитьНастройкиВходаПользователей
//
Процедура ПеренестиНастройки() Экспорт
	
	Блокировка = Новый БлокировкаДанных();
	Блокировка.Добавить("Константа.УдалитьНастройкиВходаПользователей");
	Блокировка.Добавить("Константа.НастройкиВходаПользователей");
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка.Заблокировать();
		
		УстановитьПривилегированныйРежим(Истина);
		
		СохраненныеНастройки = Константы.УдалитьНастройкиВходаПользователей.Получить().Получить();
		Если СохраненныеНастройки <> Неопределено Тогда
			
			Константы.НастройкиВходаПользователей.Установить(Новый ХранилищеЗначения(СохраненныеНастройки));
			Константы.УдалитьНастройкиВходаПользователей.Установить(Новый ХранилищеЗначения(Неопределено));
			
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			Метаданные.Константы.НастройкиВходаПользователей,
			,
			НСтр("ru = 'Настройки входа пользователей успешно перенесены'"));
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Ошибка переноса настроек входа пользователя:
				 |%1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Константы.НастройкиВходаПользователей,
			,
			ТекстОшибки);
		
		ВызватьИсключение НСтр("ru = 'Ошибка переноса настроек входа пользователя. Подробности в журнале регистрации.'");
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
