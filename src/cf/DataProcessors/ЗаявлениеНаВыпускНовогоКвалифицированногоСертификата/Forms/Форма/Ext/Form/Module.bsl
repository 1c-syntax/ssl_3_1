///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
Перем Криптография, РаботаСДвоичнымиДанными, РеквизитыПроверкиАдреса;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьОрганизации = Не Метаданные.ОпределяемыеТипы.Организация.Тип.СодержитТип(Тип("Строка"));
	Элементы.Организация.Видимость = ЕстьОрганизации;
	Элементы.НаименованиеСокращенное.Видимость = Не ЕстьОрганизации;
	Элементы.НаименованиеСокращенное.КнопкаВыбора = ЕстьОрганизации;
	
	Элементы.ВладелецСертификатаСтрокой.Видимость = Ложь;
	
	ЭтоПодчиненныйУзелРИБ = ОбщегоНазначения.ЭтоПодчиненныйУзелРИБ();
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь();
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		ИнтернетПоддержкаПользователейПодключена = Истина;
		ПрограммаОблачногоСервиса = ПрограммаОблачногоСервиса();
	КонецЕсли;
	
	ЗаполнитьСписокПрограмм();
	ЗаполнитьЗаголовкиРеквизитов();
	
	Если ЗначениеЗаполнено(Параметры.СертификатСсылка) Тогда
		ЗагрузитьЗаявление();
		ПриИзмененииОрганизацииНаСервере(Истина);
		Модифицированность = Ложь;
	ИначеЕсли ЗначениеЗаполнено(Параметры.СертификатОснование) Тогда
		СкопироватьЗаявление();
		ПриИзмененииОрганизацииНаСервере(Истина);
		Модифицированность = Истина;
	Иначе
		
		ДокументыПартнерЭтоИП = Ложь;
		
		Объект.Добавил = Пользователи.ТекущийПользователь();
		Объект.Пользователь = Объект.Добавил;
		Если ЗначениеЗаполнено(Параметры.Организация) Тогда
			Организация = Параметры.Организация;
			Элементы.Организация.ТолькоПросмотр = Истина;
		КонецЕсли;
		
		ПриИзмененииОрганизацииНаСервере(Ложь);
		
		Объект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.НеПодготовлено;
		Модифицированность = Ложь;
		
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	РеквизитыПроверкиАдреса = Новый Структура;
	РеквизитыПроверкиАдреса.Вставить("ЮридическийАдрес", Ложь);
	РеквизитыПроверкиАдреса.Вставить("ФактическийАдрес", Ложь);
	
	Если Объект.СостояниеЗаявления = ПредопределенноеЗначение("Перечисление.СостоянияЗаявленияНаВыпускСертификата.НеПодготовлено") Тогда
		ПодключитьОбработчикОжидания("ПриИзмененияСоставаИлиНастроекПрограмм", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// При изменении состава или настроек программ.
	Если ЗначениеЗаполнено(ВариантАлгоритмов)
		И (ВРег(ИмяСобытия) = ВРег("Запись_ПрограммыЭлектроннойПодписиИШифрования")
		Или ВРег(ИмяСобытия) = ВРег("Запись_ПутиКПрограммамЭлектроннойПодписиИШифрованияНаСерверахLinux")) Тогда
		
		ПодключитьОбработчикОжидания("ПриИзмененияСоставаИлиНастроекПрограмм", 0.1, Истина);
	КонецЕсли;
	
	Если ВРег(ИмяСобытия) = ВРег("ИнтернетПоддержкаПодключена") Тогда
		ИнтернетПоддержкаПользователейПодключена = Истина;
	ИначеЕсли ВРег(ИмяСобытия) = ВРег("ИнтернетПоддержкаОтключена") Тогда
		ИнтернетПоддержкаПользователейПодключена = Ложь;
	КонецЕсли;
	
	Если Не СтрНачинаетсяС(ИмяСобытия, "Запись_") Тогда
		Возврат;
	КонецЕсли;
	
	Если Источник = Организация Тогда
		ПодключитьОбработчикОжидания("ОбработчикОжиданияРеквизитыОрганизацииПриИзменении", 0.1, Истина);
	ИначеЕсли Источник = Сотрудник Тогда
		ПодключитьОбработчикОжидания("ОбработчикОжиданияРеквизитыВладельцаПриИзменении", 0.1, Истина);
	ИначеЕсли Источник = ДокументыПартнерСсылка Тогда
		ПодключитьОбработчикОжидания("ОбработчикОжиданияРеквизитыПартнераПриИзменении", 0.1, Истина);
	ИначеЕсли Источник = ДокументыРуководительСсылка Тогда
		ПодключитьОбработчикОжидания("ОбработчикОжиданияРеквизитыРуководителяПриИзменении", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	Если Модифицированность Тогда
		
		Отказ = Истина;
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗакрытиеПослеОтветаНаВопрос", ЭтотОбъект),
			НСтр("ru = 'Данные были изменены. Сохранить изменения?'"), РежимДиалогаВопрос.ДаНетОтмена);
		
	Иначе
		
		ВозвращаемоеЗначение = Новый Структура;
		ВозвращаемоеЗначение.Вставить("Ссылка", Объект.Ссылка);
		ВозвращаемоеЗначение.Вставить("Добавлен", ЗаявлениеИсполнено());
		Закрыть(ВозвращаемоеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РеквизитПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.СостояниеЗаявления) Тогда
		Модифицированность = Истина;
		ДокументыПечатались = Ложь;
	КонецЕсли;
	
	ЭтотОбъект[Элемент.Имя] = СокрЛП(ЭтотОбъект[Элемент.Имя]);
	
КонецПроцедуры

#Область Организация

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПриИзмененииОрганизацииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	ОчиститьРеквизитыОрганизации(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеСокращенноеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка, "ОрганизацияТип");
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеСокращенноеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение <> Неопределено Тогда
		
		Организация = ВыбранноеЗначение;
		ПодключитьОбработчикОжидания("ОбработчикОжиданияРеквизитыОрганизацииПриИзменении", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВладелецСертификата

&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	ПриИзмененииВладельцаСертификатаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникОчистка(Элемент, СтандартнаяОбработка)
	
	Если СотрудникТип <> Неопределено
		И СотрудникТип.Количество() = 1
		И Сотрудник = Неопределено Тогда
		
		ОписаниеТипов = Новый ОписаниеТипов(СотрудникТип.ВыгрузитьЗначения());
		Сотрудник = ОписаниеТипов.ПривестиЗначение(Неопределено);
	КонецЕсли;
	
	ОчиститьРеквизитыВладельца();
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецСертификатаСтрокойНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка, "СотрудникТип");
	
КонецПроцедуры

&НаКлиенте
Процедура ВладелецСертификатаСтрокойОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение <> Неопределено Тогда
		
		Сотрудник = ВыбранноеЗначение;
		ПодключитьОбработчикОжидания("ОбработчикОжиданияРеквизитыВладельцаПриИзменении", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПодписантЗаявления

&НаКлиенте
Процедура ДокументыРуководительПриИзменении(Элемент)
	
	ДокументыРуководительСсылка = Неопределено;
	Элементы.ДокументыРуководитель.КнопкаОткрытия = Неопределено;
	
	ДокументыРуководитель = СокрЛП(ДокументыРуководитель);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыРуководительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыРуководительОчистка(Элемент, СтандартнаяОбработка)
	
	ДокументыРуководительСсылка = Неопределено;
	Элементы.ДокументыРуководитель.КнопкаОткрытия = Неопределено;
	
	ОчиститьРеквизит("ДокументыРуководительДолжность");
	ОчиститьРеквизит("ДокументыРуководительОснование");
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыРуководительОткрытие(Элемент, СтандартнаяОбработка)
	
	ЗначениеОткрытие(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыРуководительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ЗначениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	ПодключитьОбработчикОжидания("ОбработчикОжиданияРеквизитыРуководителяПриИзменении", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбслуживающаяОрганизация

&НаКлиенте
Процедура ДокументыПартнерПриИзменении(Элемент)
	
	ДокументыПартнерСсылка = Неопределено;
	Элементы.ДокументыПартнер.КнопкаОткрытия = Неопределено;
	
	ДокументыПартнер = СокрЛП(ДокументыПартнер);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыПартнерНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыПартнерОчистка(Элемент, СтандартнаяОбработка)
	
	ДокументыПартнерСсылка = Неопределено;
	Элементы.ДокументыПартнер.КнопкаОткрытия = Неопределено;
	
	Для Каждого Реквизит Из РеквизитыОбслуживающейОрганизации() Цикл
		ОчиститьРеквизит(Реквизит.Ключ);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыПартнерОткрытие(Элемент, СтандартнаяОбработка)
	
	ЗначениеОткрытие(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыПартнерОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ЗначениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	ПодключитьОбработчикОжидания("ОбработчикОжиданияРеквизитыПартнераПриИзменении", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыПартнерИННПриИзменении(Элемент)
	
	ДокументыПартнерЭтоИП = СтрДлина(ТолькоЦифры(ДокументыПартнерИНН)) > 10;
	Если ДокументыПартнерЭтоИП Тогда
		ОчиститьРеквизит("ДокументыПартнерКПП");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.СостояниеЗаявления) Тогда
		Модифицированность = Истина;
		ДокументыПечатались = Ложь;
	КонецЕсли;
	
	Элементы.ДокументыПартнерКПП.Доступность = Не ДокументыПартнерЭтоИП;
	
КонецПроцедуры

#КонецОбласти

#Область Программа

&НаКлиенте
Процедура ПрограммаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЗаполнитьДанныеВыбораПрограммы(ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрограммаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Строка") Тогда
		
		Представление = ПоставляемоеПредставлениеПрограммы(СписокПрограмм, ВыбранноеЗначение);
		Если Не ЭтоПолноправныйПользователь Тогда
			
			ПоказатьПредупреждение(, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Программа %1 еще не добавлена в список используемых программ.
					|Обратитесь к администратору.'"),
					Представление));
			
		ИначеЕсли ЭтоПодчиненныйУзелРИБ Тогда
			
			ПоказатьПредупреждение(, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Программа %1 еще не добавлена в список используемых программ.
					|Выполните добавление в главном узле информационной базы.'"),
					Представление));
					
		Иначе
			
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить("Добавить",    НСтр("ru = 'Добавить'"));
			Кнопки.Добавить("НеДобавлять", НСтр("ru = 'Не добавлять'"));
			ПоказатьВопрос(Новый ОписаниеОповещения("ПрограммаОбработкаВыбораПродолжение", ЭтотОбъект, ВыбранноеЗначение),
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Программа %1 еще не добавлена в список используемых программ.
					|Добавить?'"),
					Представление),
				Кнопки,, "НеДобавлять");
				
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если Объект.Программа <> ВыбранноеЗначение Тогда
		Модифицированность = Истина;
		ДокументыПечатались = Ложь;
	КонецЕсли;
	
	Объект.Программа = ВыбранноеЗначение;
	ПодключитьОбработчикОжидания("ПослеВыбораПрограммыОбработчикОжидания", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрограммаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ЗаполнитьДанныеВыбораПрограммы(ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрограммаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ЗаполнитьДанныеВыбораПрограммы(ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрограммаРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭлектроннаяПодписьСлужебныйКлиент.ОткрытьИнструкциюПоРаботеСПрограммами();
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ИнструкцияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылка <> "КомплектДокументов" Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоИндивидуальныйПредприниматель Тогда
		ТекстПредупреждения =
			НСтр("ru = 'Комплект документов:
			|1. Заявление на выпуск сертификата (подготовленное на предыдущем шаге).
			|2. Копия свидетельства о постановке на учет в налоговом органе (ИНН).
			|3. Копия свидетельства о государственной регистрации индивидуального предпринимателя (ОГРН) или лист записи ЕГРИП (Форма Р60009).
			|4. Копия документа, удостоверяющего личность владельца сертификата.
			|5. Копия страхового свидетельства обязательного пенсионного страхования (СНИЛС) владельца сертификата или уведомления о регистрации в системе персонифицированного учета (Форма АДИ-РЕГ).'");
	Иначе
		ТекстПредупреждения =
			НСтр("ru = 'Комплект документов:
			|1. Заявление на выпуск сертификата (подготовленное на предыдущем шаге).
			|2. Копия* свидетельства о постановке на учет в налоговом органе (ИНН).
			|3. Копия* свидетельства о государственной регистрации юридического лица (ОГРН) или лист записи ЕГРЮЛ (Форма № Р50007).
			|4. Копия* документа, удостоверяющего личность представителя организации, указанного для выпуска сертификата.
			|5. Копия* страхового свидетельства обязательного пенсионного страхования (СНИЛС) представителя организации,
			|   указанного для выпуска сертификата.
			|6. Копия* документа, подтверждающего полномочия руководителя, подписавшего заявление (протокол собрания
			|   учредителей, решение собственника, устав) или актуальная выписка из ЕГРЮЛ, заверенная налоговым органом.
			|
			|* Копии документов заверяются подписью руководителя и печатью организации.'");
	КонецЕсли;
	
	ПоказатьПредупреждение(, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияУсловияСоглашенияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылка = "УсловияСоглашения" Тогда
		ОткрытьФорму("Обработка.ЗаявлениеНаВыпускНовогоКвалифицированногоСертификата.Форма.ФормаСоглашения", , ЭтотОбъект);
	ИначеЕсли НавигационнаяСсылка = "УдостоверяющийЦентр" Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("http://ca.1c.ru/");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияТекстЗаявленияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ДокументыПечатались
		И Не Модифицированность Тогда
		
		ПечатьЗаявленияПослеСозданияКлючаИЗапросаНаСертификат(Истина, Неопределено);
	ИначеЕсли ПроверитьЗаявление() Тогда
		
		Доступность = Ложь;
		Если Объект.Программа = ПрограммаОблачногоСервиса Тогда
			СоздатьЗапросВОблачныйСервис();
		Иначе
			ЭлектроннаяПодписьСлужебныйКлиент.СоздатьМенеджерКриптографии(Новый ОписаниеОповещения(
				"ПечатьЗаявленияПослеСозданияМенеджераКриптографии", ЭтотОбъект),
				"", Неопределено, , Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОписаниеНажатие(Элемент)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://portal.1c.ru/applications/31");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УказатьСведенияОбОрганизации(Команда)
	
	СведенияОбОрганизации = РеквизитыОрганизации();
	СведенияОбОрганизации.Вставить("ТелефонXML");
	СведенияОбОрганизации.Вставить("ФактическийАдресXML");
	СведенияОбОрганизации.Вставить("ЮридическийАдресXML");
	ЗаполнитьЗначенияСвойств(СведенияОбОрганизации, ЭтотОбъект);
	
	ПараметрыВвода = Новый Структура("СведенияОбОрганизации, ТолькоПросмотр",
		СведенияОбОрганизации, Элементы.ГруппаОрганизация.ТолькоПросмотр);
	
	ЗавершениеВводаСведений = Новый ОписаниеОповещения("ВводСведенийОбОрганизации", ЭтотОбъект);
	ОткрытьФорму("Обработка.ЗаявлениеНаВыпускНовогоКвалифицированногоСертификата.Форма.СведенияОбОрганизации",
		ПараметрыВвода, ЭтотОбъект, , , , ЗавершениеВводаСведений);
	
КонецПроцедуры

&НаКлиенте
Процедура УказатьСведенияОВладельце(Команда)
	
	СведенияОВладельце = РеквизитыВладельцаСертификата();
	СведенияОВладельце.Вставить("ЭтоИндивидуальныйПредприниматель");
	СведенияОВладельце.Вставить("ГражданствоОКСМКодАльфа3");
	СведенияОВладельце.Вставить("ГражданствоПредставление");
	ЗаполнитьЗначенияСвойств(СведенияОВладельце, ЭтотОбъект);
	
	ПараметрыВвода = Новый Структура("СведенияОВладельце, ТолькоПросмотр",
		СведенияОВладельце, Элементы.ГруппаВладелецСертификата.ТолькоПросмотр);
		
	ЗавершениеВводаСведений = Новый ОписаниеОповещения("ВводСведенийОВладельце", ЭтотОбъект);
	ОткрытьФорму("Обработка.ЗаявлениеНаВыпускНовогоКвалифицированногоСертификата.Форма.СведенияОВладельцеСертификата",
		ПараметрыВвода, ЭтотОбъект, , , , ЗавершениеВводаСведений);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗаявление(Команда)
	
	Если Не ДокументыПечатались Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Документы еще не печатались.
			|Заявление не будет принято пока не будут получены печатные документы.'"));
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДокументыПартнерИНН)
		И (ДокументыПартнерЭтоИП Или ЗначениеЗаполнено(ДокументыПартнерКПП)) Тогда
		
		ОтправитьЗаявлениеПродолжение("Отправить", Неопределено);
		Возврат;
	КонецЕсли;
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить("Отправить",   НСтр("ru = 'Отправить'"));
	Кнопки.Добавить("НеОтправить", НСтр("ru = 'Не отправлять'"));
	
	Текст = "";
	Если ДокументыПартнерЭтоИП Тогда
		
		Если Не ЗначениеЗаполнено(ДокументыПартнерИНН) Тогда
			Текст = Текст + Символы.ПС  + Символы.ПС
				+ НСтр("ru = 'Не указан ИНН обслуживающей организации.
				|Заявление может обрабатываться дольше обычного.'");
		КонецЕсли;
		
	ИначеЕсли Не ЗначениеЗаполнено(ДокументыПартнерИНН)
		Или Не ЗначениеЗаполнено(ДокументыПартнерКПП) Тогда
		
		Текст = Текст + Символы.ПС  + Символы.ПС
			+ НСтр("ru = 'Не указан ИНН или КПП обслуживающей организации.
			|Заявление может обрабатываться дольше обычного.'");
		
	КонецЕсли;
	
	ОповещениеОтправитьЗаявление = Новый ОписаниеОповещения("ОтправитьЗаявлениеПродолжение", ЭтотОбъект);
	ПоказатьВопрос(ОповещениеОтправитьЗаявление, СокрЛП(Текст), Кнопки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСостояниеОбработкиЗаявления(Команда)
	
	РезультатОбновления = ОбновитьСостояниеОбработкиЗаявленияНаСервере();
	Если РезультатОбновления = Истина Тогда
		
		Если Объект.Программа = ПрограммаОблачногоСервиса Тогда
			УстановитьСертификатОблачногоСервиса();
		Иначе
			УстановитьСертификат(Новый ОписаниеОповещения("ПослеУстановкиСертификата", ЭтотОбъект));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЗапросНаСертификат(Команда)
	
	ЭлектроннаяПодписьСлужебныйКлиент.СохранитьЗапросНаСертификат(Неопределено,
		АдресЗапросаНаСертификат, ИмяФайлаБезРасширения());
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьКорневойСертификат(Команда)
	
	ЭлектроннаяПодписьСлужебныйКлиент.СохранитьСертификат(Неопределено,
		АдресКорневогоСертификата, НСтр("ru = 'Корневой сертификат ООО НПЦ 1С'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьСертификат(Команда)
	
	ЭлектроннаяПодписьСлужебныйКлиент.СохранитьСертификат(Новый ОписаниеОповещения(
		"ВыгрузитьСертификатЗавершение", ЭтотОбъект), АдресСертификата, ИмяФайлаБезРасширения());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДействияСЗаявлением

&НаКлиенте
Процедура ЗакрытиеПослеОтветаНаВопрос(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		РазблокироватьОбъект();
		Модифицированность = Ложь;
		Закрыть();
	ИначеЕсли Ответ = КодВозвратаДиалога.Да Тогда
		ЗаписатьИРазблокироватьОбъект();
		ПослеЗаписи();
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи()
	
	Если Не ОповеститьОбИзмененииЗаявления Тогда
		Возврат;
	КонецЕсли;
	
	ОповеститьОбИзменении(Объект.Ссылка);
	
	Контекст = Новый Структура;
	Если ЭтоНовый Тогда
		Контекст.Вставить("ЭтоНовый");
	КонецЕсли;
	
	ОповеститьОбИзмененииЗаявления = Ложь;
	Оповестить("Запись_СертификатыКлючейЭлектроннойПодписиИШифрования", Контекст, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Функция ЗаявлениеИсполнено()
	
	Возврат Объект.СостояниеЗаявления = ПредопределенноеЗначение("Перечисление.СостоянияЗаявленияНаВыпускСертификата.Исполнено");
	
КонецФункции

&НаСервере
Процедура ЗагрузитьЗаявление()
	
	ЗаблокироватьДанныеДляРедактирования(Параметры.СертификатСсылка, , УникальныйИдентификатор);
	
	ТекущийОбъект = Параметры.СертификатСсылка.ПолучитьОбъект();
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	
	ПоследнееСостояниеЗаявления = Объект.СостояниеЗаявления;
	
	Содержание = ТекущийОбъект.СодержаниеЗаявления.Получить();
	Если ТипЗнч(Содержание) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Содержание);
	КонецЕсли;
	
	Если Содержание.Свойство("ЗапросНаСертификат")
		И ТипЗнч(Содержание.ЗапросНаСертификат) = Тип("ДвоичныеДанные") Тогда
		
		АдресЗапросаНаСертификат = ПоместитьВоВременноеХранилище(Содержание.ЗапросНаСертификат, УникальныйИдентификатор);
	КонецЕсли;
	
	Если Содержание.Свойство("Сертификат")
		И ТипЗнч(Содержание.Сертификат) = Тип("ДвоичныеДанные") Тогда
		
		АдресСертификата = ПоместитьВоВременноеХранилище(Содержание.Сертификат, УникальныйИдентификатор);
	КонецЕсли;

	Если Содержание.Свойство("КорневойСертификат")
		И ТипЗнч(Содержание.КорневойСертификат) = Тип("ДвоичныеДанные") Тогда
		
		АдресКорневогоСертификата = ПоместитьВоВременноеХранилище(Содержание.КорневойСертификат, УникальныйИдентификатор);
	КонецЕсли;
	
	Если ТекущийОбъект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.Отклонено Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = НСтр("ru = 'Заявление, по которому не удалось получить сертификат'");
	ИначеЕсли ТекущийОбъект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.Исполнено Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = НСтр("ru = 'Заявление, по которому был получен сертификат'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СкопироватьЗаявление()
	
	ДокументыПартнерЭтоИП = Ложь;
	
	Объект.Добавил = Пользователи.ТекущийПользователь();
	Объект.Пользователь = Объект.Добавил;
	Объект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.НеПодготовлено;
	
	СертификатОбъект = Параметры.СертификатОснование.ПолучитьОбъект();
	Содержание = СертификатОбъект.СодержаниеЗаявления.Получить();
	Если ТипЗнч(Содержание) = Тип("Структура") Тогда
		
		КопируемыеСвойства = "";
		Для Каждого Реквизит Из РеквизитыОрганизации() Цикл
			
			КопируемыеСвойства = КопируемыеСвойства
				+ ?(ПустаяСтрока(КопируемыеСвойства), "", ", ")
				+ Реквизит.Ключ;
			
		КонецЦикла;
		
		КопируемыеСвойства = КопируемыеСвойства
			+ ", Организация, ЮридическийАдресXML, ФактическийАдресXML, ТелефонXML";
		
		Для Каждого Реквизит Из РеквизитыВладельцаСертификата() Цикл
			КопируемыеСвойства = КопируемыеСвойства + ", " + Реквизит.Ключ;
		КонецЦикла;
		
		КопируемыеСвойства = КопируемыеСвойства
			+ ", Сотрудник, ГражданствоПредставление, ГражданствоОКСМКодАльфа3, ВладелецСертификатаСтрокой";
		
		Для Каждого Реквизит Из РеквизитыПодписанта() Цикл
			КопируемыеСвойства = КопируемыеСвойства + ", " + Реквизит.Ключ;
		КонецЦикла;
		
		КопируемыеСвойства = КопируемыеСвойства + ", ДокументыРуководительСсылка";
		
		Для Каждого Реквизит Из РеквизитыОбслуживающейОрганизации() Цикл
			КопируемыеСвойства = КопируемыеСвойства + ", " + Реквизит.Ключ;
		КонецЦикла;
		
		КопируемыеСвойства = КопируемыеСвойства
			+ ", ДокументыПартнерСсылка, ДокументыПартнерЭтоИП";
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Содержание, КопируемыеСвойства);
		
	Иначе
		
		Организация = СертификатОбъект.Организация;
		ЗаполнитьРеквизитыОрганизации();
		
		РеквизитыВладельца = РеквизитыВладельцаСертификата();
		ЗаполнитьЗначенияСвойств(РеквизитыВладельца, СертификатОбъект, "Фамилия, Имя, Отчество, Должность");
		УстановитьРеквизитыВладельца(РеквизитыВладельца, Истина);
		
		Элементы.ВладелецСертификатаСтрокой.СписокВыбора.Добавить(ВладелецСертификатаСтрокой);
		
	КонецЕсли;
	
	Объект.Программа = СертификатОбъект.Программа;
	ДозаполнитьПоПредыдущемуЗаявлению();
	
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаявление()
	
	Возврат ПроверитьЗаполнениеОсновныхРеквизитов()
		И ПроверитьЗаполнениеДополнительныхРеквизитов();
	
КонецФункции

&НаСервере
Процедура ЗаписатьЗаявление()
	
	ТекущийОбъект = РеквизитФормыВЗначение("Объект");
	ЭтоНовоеСостояние = ПоследнееСостояниеЗаявления <> ТекущийОбъект.СостояниеЗаявления;
	
	Содержание = ТекущийОбъект.СодержаниеЗаявления.Получить();
	Если ТипЗнч(Содержание) <> Тип("Структура") Тогда
		Содержание = Новый Структура;
	КонецЕсли;
	
	Имя = СокрЛП(Имя);
	Фамилия = СокрЛП(Фамилия);
	Отчество = СокрЛП(Отчество);
	
	ТекущийОбъект.Наименование = СокрП(Фамилия + " " + Имя + " " + Отчество)
		+ " (" + НСтр("ru = 'Заявление на новый сертификат'") + ")";
		
	РеквизитыОрганизации = РеквизитыОрганизации();
	РеквизитыОрганизации.Вставить("Организация");
	РеквизитыОрганизации.Вставить("ЮридическийАдресXML");
	РеквизитыОрганизации.Вставить("ФактическийАдресXML");
	РеквизитыОрганизации.Вставить("ТелефонXML");
	
	Для Каждого Реквизит Из РеквизитыОрганизации Цикл
		ОбновитьЗначение(Содержание, Реквизит.Ключ);
	КонецЦикла;
	
	РеквизитыВладельца = РеквизитыВладельцаСертификата();
	РеквизитыВладельца.Вставить("Сотрудник");
	РеквизитыВладельца.Вставить("ГражданствоПредставление");
	РеквизитыВладельца.Вставить("ГражданствоОКСМКодАльфа3");
	РеквизитыВладельца.Вставить("ВладелецСертификатаСтрокой");
	
	Для Каждого Реквизит Из РеквизитыВладельца Цикл
		ОбновитьЗначение(Содержание, Реквизит.Ключ);
	КонецЦикла;
	
	РеквизитыПодписанта = РеквизитыПодписанта();
	РеквизитыПодписанта.Вставить("ДокументыРуководительСсылка");
	
	Для Каждого Реквизит Из РеквизитыПодписанта Цикл
		ОбновитьЗначение(Содержание, Реквизит.Ключ);
	КонецЦикла;
	
	РеквизитыПартнера = РеквизитыОбслуживающейОрганизации();
	РеквизитыПартнера.Вставить("ДокументыПартнерСсылка");
	РеквизитыПартнера.Вставить("ДокументыПартнерЭтоИП");
	
	Для Каждого Реквизит Из РеквизитыПартнера Цикл
		ОбновитьЗначение(Содержание, Реквизит.Ключ);
	КонецЦикла;
	
	ОбновитьЗначение(Содержание, "Программа", Объект.Программа);
	ОбновитьЗначение(Содержание, "ИдентификаторЗаявленияВОблачныйСервис");
	
	Если Объект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.Подготовлено Тогда
		
		ОбновитьЗначение(Содержание, "ВариантАлгоритмов");
		ОбновитьЗначение(Содержание, "ДокументыПечатались");
		ОбновитьЗначение(Содержание, "ПредставлениеПрограммы");
		ОбновитьЗначение(Содержание, "КонтейнерКлючаИмя");
		ОбновитьЗначение(Содержание, "КонтейнерКлючаПуть");
		ОбновитьЗначение(Содержание, "ЗапросНаСертификат", ПолучитьИзВременногоХранилища(АдресЗапросаНаСертификат));
		ОбновитьЗначение(Содержание, "ОткрытаяЧастьКлючаЭП");
		ОбновитьЗначение(Содержание, "ИдентификаторКлючаСубъекта");
		ОбновитьЗначение(Содержание, "ДокументыПоляСертификата");
		ОбновитьЗначение(Содержание, "ДатаЗаявления");
		
	ИначеЕсли Объект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.Отправлено Тогда
		
		ОбновитьЗначение(Содержание, "ИдентификаторДокументооборота");
		ОбновитьЗначение(Содержание, "ДатаОтправки");
		ОбновитьЗначение(Содержание, "ДатаОбновленияСостояния");
		ОбновитьЗначение(Содержание, "СостояниеОбработкиЗаявления");
		
	ИначеЕсли Объект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.ИсполненоСертификатНеУстановлен Тогда
		
		ОбновитьЗначение(Содержание, "ОтпечатокКорневогоСертификата");
		ОбновитьЗначение(Содержание, "Сертификат",         ПолучитьИзВременногоХранилища(АдресСертификата));
		ОбновитьЗначение(Содержание, "КорневойСертификат", ПолучитьИзВременногоХранилища(АдресКорневогоСертификата));
		ОбновитьЗначение(Содержание, "ИдентификаторАбонента");
		ОбновитьЗначение(Содержание, "ДатаОбновленияСостояния");
		ОбновитьЗначение(Содержание, "СостояниеОбработкиЗаявления");
		ОбновитьЗначение(Содержание, "ОшибкаУстановкиСертификата");
		ОбновитьЗначение(Содержание, "ДатаУстановкиСертификата");
		Если ТекущийОбъект.ДанныеСертификата.Получить() <> Содержание.Сертификат Тогда
			ТекущийОбъект.ДанныеСертификата = Новый ХранилищеЗначения(Содержание.Сертификат);
		КонецЕсли;
		
	ИначеЕсли Объект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.Отклонено Тогда
		
		ОбновитьЗначение(Содержание, "ДатаОбновленияСостояния");
		ОбновитьЗначение(Содержание, "СостояниеОбработкиЗаявления");
		НовоеНаименование = СокрП(Фамилия + " " + Имя + " " + Отчество) + " (" + НСтр("ru = 'Удалить'") + ")";
		Если ТекущийОбъект.Наименование <> НовоеНаименование Тогда
			ТекущийОбъект.Наименование = НовоеНаименование;
		КонецЕсли;
		
	ИначеЕсли Объект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.Исполнено Тогда
		ОбновитьЗначение(Содержание, "ОшибкаУстановкиСертификата");
		ОбновитьЗначение(Содержание, "ДатаУстановкиСертификата");
	КонецЕсли;
	
	ЭтоНовый = ТекущийОбъект.ЭтоНовый();
	ТекущийОбъект.Организация = Организация;
	ТекущийОбъект.Имя = Имя;
	ТекущийОбъект.Фамилия = Фамилия;
	ТекущийОбъект.Отчество = Отчество;
	ТекущийОбъект.СодержаниеЗаявления = Новый ХранилищеЗначения(Содержание);
	ТекущийОбъект.Записать();
	
	Модифицированность = Ложь;
	ЗначениеВРеквизитФормы(ТекущийОбъект, "Объект");
	ОповеститьОбИзмененииЗаявления = Истина;
	
	ПоследнееСостояниеЗаявления = Объект.СостояниеЗаявления;
	
	Если ЭтоНовый Тогда
		ЗаблокироватьДанныеДляРедактирования(ТекущийОбъект.Ссылка, , УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИРазблокироватьОбъект()
	
	ЗаписатьЗаявление();
	РазблокироватьОбъект();
	
КонецПроцедуры

&НаСервере
Процедура РазблокироватьОбъект()
	
	РазблокироватьДанныеДляРедактирования(Объект.Ссылка, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Функция ОбновитьСостояниеОбработкиЗаявленияНаСервере()
	
	Результат = ПолучитьСертификат();
	Если Результат = Истина Тогда
		Объект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.ИсполненоСертификатНеУстановлен;
	ИначеЕсли Результат = Ложь Тогда
		Объект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.Отклонено;
	КонецЕсли;
	
	ЗаписатьЗаявление();
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	ЗаявлениеОтправлено = Объект.СостояниеЗаявления <> Перечисления.СостоянияЗаявленияНаВыпускСертификата.НеПодготовлено
		И Объект.СостояниеЗаявления <> Перечисления.СостоянияЗаявленияНаВыпускСертификата.Подготовлено;
	
	Элементы.ГруппаПрограмма.ТолькоПросмотр = ЗаявлениеОтправлено;
	Элементы.ГруппаОрганизация.ТолькоПросмотр = ЗаявлениеОтправлено;
	Элементы.ГруппаОбработкаЗаявления.Видимость = ЗаявлениеОтправлено;
	Элементы.ГруппаПодписантЗаявления.ТолькоПросмотр = ЗаявлениеОтправлено;
	Элементы.ГруппаВладелецСертификата.ТолькоПросмотр = ЗаявлениеОтправлено;
	Элементы.ГруппаОбслуживающаяОрганизация.ТолькоПросмотр = ЗаявлениеОтправлено;
	
	Элементы.ОтправитьЗаявление.Доступность = Не ЗаявлениеОтправлено;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеРеквизитов

&НаКлиенте
Процедура ОчиститьРеквизит(ИмяРеквизита)
	
	ЭтотОбъект[ИмяРеквизита] = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Значение = ЭтотОбъект[Элемент.Имя + "Ссылка"];
	
	Если ЗначениеЗаполнено(Значение) Тогда
		ПоказатьЗначение(, Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка, ИмяРеквизитаТип = "")
	
	СтандартнаяОбработка = Ложь;
	
	ИмяРеквизита = Элемент.Имя;
	Если ПустаяСтрока(ИмяРеквизитаТип) Тогда
		ИмяРеквизитаТип = ИмяРеквизита + "Тип";
		ИмяРеквизитаЗначения = Элемент.Имя
			+ ?(ТипЗнч(ЭтотОбъект[Элемент.Имя]) = Тип("Строка"), "Ссылка", "");
	Иначе
		ИмяРеквизитаЗначения = Лев(ИмяРеквизитаТип, СтрДлина(ИмяРеквизитаТип) - 3);
	КонецЕсли;
	
	Контекст = Новый Структура;
	Контекст.Вставить("Элемент", Элемент);
	Контекст.Вставить("ИмяРеквизита", ИмяРеквизита);
	Контекст.Вставить("ИмяРеквизитаТип", ИмяРеквизитаТип);
	Контекст.Вставить("ИмяРеквизитаЗначения", ИмяРеквизитаЗначения);
	
	Если ТипЗнч(ЭтотОбъект[ИмяРеквизитаТип]) <> Тип("СписокЗначений") Тогда
		Возврат;
	ИначеЕсли ЭтотОбъект[ИмяРеквизитаТип].Количество() = 1 Тогда
		ЗначениеНачалоВыбораПослеВыбораТипа(ЭтотОбъект[ИмяРеквизитаТип][0], Контекст);
	Иначе
		
		СписокВыбораТипов = Новый СписокЗначений;
		СписокВыбораТипов.ЗагрузитьЗначения(ЭтотОбъект[ИмяРеквизитаТип].ВыгрузитьЗначения());
		СписокВыбораТипов.ПоказатьВыборЭлемента(
			Новый ОписаниеОповещения("ЗначениеНачалоВыбораПослеВыбораТипа", ЭтотОбъект, Контекст),
			НСтр("ru = 'Выбор типа данных'"),
			ЭтотОбъект[ИмяРеквизитаТип].НайтиПоЗначению(ТипЗнч(ЭтотОбъект[ИмяРеквизитаЗначения])));
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеНачалоВыбораПослеВыбораТипа(ЭлементСписка, Контекст) Экспорт
	
	Если ЭлементСписка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элемент = Контекст.Элемент;
	ИмяРеквизита = Контекст.ИмяРеквизита;
	ИмяРеквизитаТип = Контекст.ИмяРеквизитаТип;
	ИмяРеквизитаЗначения = Контекст.ИмяРеквизитаЗначения;
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(ЭлементСписка.Значение);
	
	ОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
	НачальноеЗначение = ОписаниеТипов.ПривестиЗначение(ЭтотОбъект[ИмяРеквизитаЗначения]);
	
	Если ИмяРеквизита = ИмяРеквизитаЗначения Тогда
		ЭтотОбъект[ИмяРеквизитаЗначения] = НачальноеЗначение;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ТекущаяСтрока", НачальноеЗначение);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Ложь);
	
	ИмяФормыВыбора = ЭтотОбъект[ИмяРеквизитаТип].НайтиПоЗначению(ТипЗнч(НачальноеЗначение)).Представление
		+ ".ФормаВыбора";
	
	ОткрытьФорму(ИмяФормыВыбора, ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭтотОбъект[Элемент.Имя + "Ссылка"] = ВыбранноеЗначение;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРеквизит(Реквизиты, ИмяРеквизита, ТолькоЦифры = Ложь, ЭтоАдрес = Неопределено)
	
	Если Реквизиты[ИмяРеквизита] = Неопределено Тогда
		Возврат; // Заполнение реквизита не внедрено.
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Реквизиты[ИмяРеквизита]) Тогда
		
		ЭтотОбъект[ИмяРеквизита] = Неопределено;
		Если ЭтоАдрес <> Неопределено Тогда
			ЭтотОбъект[ИмяРеквизита + "XML"] = "";
		КонецЕсли;
		
	Иначе
		
		Если ЭтоАдрес <> Неопределено Тогда
			
			ЭтотОбъект[ИмяРеквизита + "XML"] = Реквизиты[ИмяРеквизита];
			Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
				
				МодульУправлениеКонтактнойИнформацией = ОбщегоНазначения.ОбщийМодуль("УправлениеКонтактнойИнформацией");
				ЭтотОбъект[ИмяРеквизита] = МодульУправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(
					Реквизиты[ИмяРеквизита]);
				
			КонецЕсли;
			
		ИначеЕсли ТолькоЦифры Тогда
			ЭтотОбъект[ИмяРеквизита] = ТолькоЦифры(Реквизиты[ИмяРеквизита]);
		ИначеЕсли ТипЗнч(Реквизиты[ИмяРеквизита]) = Тип("Строка") Тогда
			ЭтотОбъект[ИмяРеквизита] = СокрЛП(Реквизиты[ИмяРеквизита]);
		Иначе
			ЭтотОбъект[ИмяРеквизита] = Реквизиты[ИмяРеквизита];
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТипыЗначения(РеквизитТип, Элемент, ОписаниеТипов);
	
	Если ТипЗнч(ОписаниеТипов) <> Тип("ОписаниеТипов") Тогда
		РеквизитТип = Неопределено;
	Иначе
		
		СоставТипов = Новый СписокЗначений;
		Для Каждого Тип Из ОписаниеТипов.Типы() Цикл
			Если ОбщегоНазначения.ЭтоСсылка(Тип) Тогда
				СоставТипов.Добавить(Тип, Строка(Тип));
			КонецЕсли;
		КонецЦикла;
		
		СоставТипов.СортироватьПоПредставлению();
		
		Для Каждого ЭлементСписка Из СоставТипов Цикл
			ЭлементСписка.Представление = Метаданные.НайтиПоТипу(ЭлементСписка.Значение).ПолноеИмя();
		КонецЦикла;
		
		Если СоставТипов.Количество() = 0 Тогда
			РеквизитТип = Неопределено;
		Иначе
			
			РеквизитТип = СоставТипов;
			Если Элемент.ОтображениеКнопкиВыбора = ОтображениеКнопкиВыбора.ОтображатьВВыпадающемСписке Тогда
				Элемент.ВыбиратьТип = Ложь;
			Иначе
				Элемент.ВыбиратьТип = РеквизитТип.Количество() > 1;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗначение(Содержание, ИмяСвойства, Значение = null)
	
	Если Значение = Null Тогда
		Значение = ЭтотОбъект[ИмяСвойства];
	КонецЕсли;
	
	Если Содержание.Свойство(ИмяСвойства)
		И Содержание[ИмяСвойства] = Значение Тогда
		
		Возврат;
	КонецЕсли;
	
	Содержание.Вставить(ИмяСвойства, Значение);
	
КонецПроцедуры

#КонецОбласти

#Область ИзменениеОсновныхРеквизитов

&НаКлиенте
Процедура ВводСведенийОбОрганизации(Сведения, ДополнительныеПараметры) Экспорт
	
	Если Сведения <> Неопределено Тогда
		
		Модифицированность = Истина;
		ДокументыПечатались = Ложь;
		УстановитьРеквизитыОрганизации(Сведения, Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВводСведенийОВладельце(Сведения, ДополнительныеПараметры) Экспорт
	
	Если Сведения <> Неопределено Тогда
		
		Модифицированность = Истина;
		ДокументыПечатались = Ложь;
		
		ВведеноВручную = Не ЗначениеЗаполнено(Сотрудник)
			Или Фамилия <> Сведения.Фамилия
			Или Имя <> Сведения.Имя;
			
		УстановитьРеквизитыВладельца(Сведения, ВведеноВручную);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияРеквизитыОрганизацииПриИзменении()
	
	ПриИзмененииОрганизацииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияРеквизитыВладельцаПриИзменении()
	
	ПриИзмененииВладельцаСертификатаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОрганизацииНаСервере(Загрузка = Ложь)
	
	Если Не Загрузка Тогда
		
		Модифицированность = ЗначениеЗаполнено(Объект.СостояниеЗаявления);
		ДокументыПечатались = Ложь;
		
		ЗаполнитьРеквизитыОрганизации();
		
	КонецЕсли;
	
	Если СотрудникТип <> Неопределено
		И СотрудникТип.Количество() = 1
		И Сотрудник = Неопределено Тогда
		
		ОписаниеТипов = Новый ОписаниеТипов(СотрудникТип.ВыгрузитьЗначения());
		Сотрудник = ОписаниеТипов.ПривестиЗначение(Неопределено);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
		
		МодульРаботаСАдресами = ОбщегоНазначения.ОбщийМодуль("РаботаСАдресами");
		
		ДополнительныеПараметры = Новый Структура("НаименованиеВключаетСокращение", Истина);
		ФактическийАдресСтруктура =
			МодульРаботаСАдресами.СведенияОбАдресе(ФактическийАдресXML, ДополнительныеПараметры);
		
		ЮридическийАдресСтруктура =
			МодульРаботаСАдресами.СведенияОбАдресе(ЮридическийАдресXML, ДополнительныеПараметры);
		
	КонецЕсли;
	
	ПриИзмененииРуководителяНаСервере(Загрузка);
	ПриИзмененииПартнераНаСервере(Загрузка);
	ПриИзмененииВладельцаСертификатаНаСервере(Загрузка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыОрганизации()
	
	Реквизиты = РеквизитыОрганизации();
	Реквизиты.Вставить("Организация", ?(ЕстьОрганизации, Организация, Неопределено));
	Реквизиты.ЭтоИндивидуальныйПредприниматель = ЭтоИндивидуальныйПредприниматель;
	
	ЭлектроннаяПодписьПереопределяемый.ПриЗаполненииРеквизитовОрганизацииВЗаявленииНаСертификат(Реквизиты);// АПК:222 Вызов устаревшей для совместимости.
	ЗаявлениеНаСертификатПереопределяемый.ПриЗаполненииРеквизитовОрганизацииВЗаявленииНаСертификат(Реквизиты);
	
	Если ЕстьОрганизации Тогда
		Организация = Реквизиты.Организация;
	КонецЕсли;
	
	УстановитьРеквизитыОрганизации(Реквизиты);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииВладельцаСертификатаНаСервере(Загрузка = Ложь)
	
	Реквизиты = РеквизитыВладельцаСертификата();
	Реквизиты.Вставить("Директор");
	Реквизиты.Вставить("ГлавныйБухгалтер");
	Реквизиты.Вставить("Пользователь");
	Реквизиты.Вставить("Организация", ?(ЕстьОрганизации, Организация, Неопределено));
	Реквизиты.Вставить("Сотрудник", ?(СотрудникТип <> Неопределено, Сотрудник, Неопределено));
	Реквизиты.Вставить("ТипВладельца");
	
	ЭлектроннаяПодписьПереопределяемый.ПриЗаполненииРеквизитовВладельцаВЗаявленииНаСертификат(Реквизиты);// АПК:222 Вызов устаревшей для совместимости.
	ЗаявлениеНаСертификатПереопределяемый.ПриЗаполненииРеквизитовВладельцаВЗаявленииНаСертификат(Реквизиты);
	
	ЗаполнитьТипыЗначения(СотрудникТип, Элементы.Сотрудник, Реквизиты.ТипВладельца);
	
	Элементы.Сотрудник.СписокВыбора.Очистить();
	Если Не ЭтоИндивидуальныйПредприниматель Тогда
		
		Если ЗначениеЗаполнено(Реквизиты.Директор) Тогда
			Элементы.Сотрудник.СписокВыбора.Добавить(Реквизиты.Директор, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Директор: %1'"), Реквизиты.Директор));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Реквизиты.ГлавныйБухгалтер) Тогда
			Элементы.Сотрудник.СписокВыбора.Добавить(Реквизиты.ГлавныйБухгалтер, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Главный бухгалтер: %1'"), Реквизиты.ГлавныйБухгалтер));
		КонецЕсли;
		
	КонецЕсли;
	
	Сотрудник = ?(Загрузка, Сотрудник, Реквизиты.Сотрудник);
	Если ЭтоИндивидуальныйПредприниматель Тогда
		Элементы.Сотрудник.ТолькоПросмотр = Истина;
		Элементы.Сотрудник.КнопкаВыпадающегоСписка = Ложь;
	Иначе
		Элементы.Сотрудник.ТолькоПросмотр = Ложь;
		Элементы.Сотрудник.КнопкаВыпадающегоСписка = Истина;
	КонецЕсли;
	
	Если Загрузка Тогда
		
		Если Не ЗначениеЗаполнено(Сотрудник)
			И Не ПустаяСтрока(ВладелецСертификатаСтрокой) Тогда
			
			Элементы.Сотрудник.Видимость = Ложь;
			Элементы.ВладелецСертификатаСтрокой.Видимость = Истина;
		КонецЕсли;
		
		ПриИзмененииВидаДокументаНаСервере();
		
	Иначе
		
		Модифицированность = ЗначениеЗаполнено(Объект.СостояниеЗаявления);
		ДокументыПечатались = Ложь;
		
		УстановитьРеквизитыВладельца(Реквизиты);
		Если ЗначениеЗаполнено(Объект.СостояниеЗаявления) Тогда
			ДозаполнитьПоПредыдущемуЗаявлению();
			ПроверитьЗаполнениеОсновныхРеквизитов();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииВидаДокументаНаСервере()
	
	Если ДокументВид = "" Тогда
		ДокументВид = "21";
		ДокументКодПодразделения = "";
	ИначеЕсли ДокументВид = 91 Тогда
		ДокументВид = "";
	КонецЕсли;
	
	Если ДокументВид <> "21" Тогда
		ДокументКодПодразделения = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРеквизитыОрганизации(Реквизиты, ОчищатьПодчиненныеРеквизиты = Истина)
	
	ОчиститьРеквизитыОрганизации(ОчищатьПодчиненныеРеквизиты);
	
	ВведеноВручную = Не ЗначениеЗаполнено(Организация)
		И ЗначениеЗаполнено(Объект.СостояниеЗаявления);
	
	Элементы.Организация.Видимость = Не ВведеноВручную;
	Элементы.НаименованиеСокращенное.Видимость = ВведеноВручную;
	
	ЭтоИндивидуальныйПредприниматель = Реквизиты.ЭтоИндивидуальныйПредприниматель;
	
	УстановитьРеквизит(Реквизиты, "НаименованиеСокращенное");
	УстановитьРеквизит(Реквизиты, "НаименованиеПолное");
	УстановитьРеквизит(Реквизиты, "ИНН", Истина);
	УстановитьРеквизит(Реквизиты, "ОГРН", Истина);
	УстановитьРеквизит(Реквизиты, "РасчетныйСчет", Истина);
	УстановитьРеквизит(Реквизиты, "БИК", Истина);
	УстановитьРеквизит(Реквизиты, "КорреспондентскийСчет", Истина);
	УстановитьРеквизит(Реквизиты, "Телефон", , Ложь);
	УстановитьРеквизит(Реквизиты, "ЮридическийАдрес", , Истина);
	УстановитьРеквизит(Реквизиты, "ФактическийАдрес", , Истина);
	
	Если Не ЭтоИндивидуальныйПредприниматель Тогда
		УстановитьРеквизит(Реквизиты, "КПП", Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРеквизитыВладельца(Реквизиты, ВведеноВручную = Ложь)
	
	ОчиститьРеквизитыВладельца();
	
	Сотрудник = ?(ВведеноВручную, Неопределено, Сотрудник);
	Элементы.ВладелецСертификатаСтрокой.Видимость = ВведеноВручную;
	Элементы.Сотрудник.Видимость = Не ВведеноВручную;
	Элементы.ВладелецСертификатаСтрокой.КнопкаВыпадающегоСписка = Не ЭтоИндивидуальныйПредприниматель;
	
	Если ВведеноВручную Тогда
		Элементы.ВладелецСертификатаСтрокой.СписокВыбора.ЗагрузитьЗначения(Элементы.Сотрудник.СписокВыбора.ВыгрузитьЗначения());
	КонецЕсли;
	
	УстановитьРеквизит(Реквизиты, "Фамилия");
	УстановитьРеквизит(Реквизиты, "Имя");
	УстановитьРеквизит(Реквизиты, "Отчество");
	УстановитьРеквизит(Реквизиты, "ДатаРождения");
	УстановитьРеквизит(Реквизиты, "Пол");
	УстановитьРеквизит(Реквизиты, "МестоРождения");
	УстановитьРеквизит(Реквизиты, "Гражданство");
	УстановитьРеквизит(Реквизиты, "СтраховойНомерПФР", Истина);
	
	ЭлементыФИО = Новый Массив;
	ЭлементыФИО.Добавить(Фамилия);
	ЭлементыФИО.Добавить(Имя);
	ЭлементыФИО.Добавить(Отчество);
	ВладелецСертификатаСтрокой = СтрСоединить(Новый ФиксированныйМассив(ЭлементыФИО), " ");
	
	Если Пол <> "Мужской"
		И Пол <> "Женский" Тогда
		
		Пол = "";
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект,
		Обработки.ЗаявлениеНаВыпускНовогоКвалифицированногоСертификата.ГражданствоСвойства(Гражданство));
	
	Если Не ЗначениеЗаполнено(ГражданствоОКСМКодАльфа3) Тогда
		Гражданство = Неопределено;
		ГражданствоПредставление = "";
		ГражданствоОКСМКодАльфа3 = "";
	КонецЕсли;
	
	Если ЭтоИндивидуальныйПредприниматель Тогда
		Должность = "";
		Подразделение = "";
	Иначе
		УстановитьРеквизит(Реквизиты, "Должность");
		УстановитьРеквизит(Реквизиты, "Подразделение");
	КонецЕсли;

	УстановитьРеквизит(Реквизиты, "ДокументВид", Истина);
	УстановитьРеквизит(Реквизиты, "ДокументНомер", Истина);
	УстановитьРеквизит(Реквизиты, "ДокументКемВыдан");
	УстановитьРеквизит(Реквизиты, "ДокументКодПодразделения", Истина);
	УстановитьРеквизит(Реквизиты, "ДокументДатаВыдачи");
	УстановитьРеквизит(Реквизиты, "ЭлектроннаяПочта", , Ложь);
	
	ПриИзмененииВидаДокументаНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьРеквизитыОрганизации(ОчищатьПодчиненные)
	
	Реквизиты = РеквизитыОрганизации();
	Реквизиты.Вставить("ТелефонXML");
	Реквизиты.Вставить("ЮридическийАдресXML");
	Реквизиты.Вставить("ФактическийАдресXML");
	Если ОчищатьПодчиненные Тогда
		Реквизиты.Вставить("Сотрудник");
	КонецЕсли;
	
	Для Каждого РеквизитОрганизации Из Реквизиты Цикл
		
		Если РеквизитОрганизации.Ключ = "ЭтоИндивидуальныйПредприниматель" Тогда
			Продолжить;
		КонецЕсли;
		
		ЭтотОбъект[РеквизитОрганизации.Ключ] = Неопределено;
		
	КонецЦикла;
	
	Если ОчищатьПодчиненные Тогда
		ОчиститьРеквизитыВладельца();
		ОчиститьРеквизитыПодписанта();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьРеквизитыВладельца()
	
	Реквизиты = РеквизитыВладельцаСертификата();
	Реквизиты.Вставить("ВладелецСертификатаСтрокой");
	Реквизиты.Вставить("ГражданствоПредставление");
	Реквизиты.Вставить("ГражданствоОКСМКодАльфа3");
	Реквизиты.Вставить("ЭлектроннаяПочтаXML");
	
	Для Каждого РеквизитВладельца Из Реквизиты Цикл
		ЭтотОбъект[РеквизитВладельца.Ключ] = Неопределено;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаполнениеОсновныхРеквизитов()
	
	ПроверяемыеРеквизиты = Новый Массив;
	РеквизитыОрганизации = РеквизитыОрганизации(Истина);
	РеквизитыОрганизации.Телефон = "";
	РеквизитыОрганизации.ЮридическийАдрес = "";
	РеквизитыОрганизации.ФактическийАдрес = "";
	Если ЭтоИндивидуальныйПредприниматель Тогда
		РеквизитыОрганизации.КПП = "";
	КонецЕсли;
	
	ДобавитьРеквизитыДляПроверки(ПроверяемыеРеквизиты, РеквизитыОрганизации,
		"Организация", НСтр("ru = 'У организации не заполнено поле ""%1""'"));
	
	ДобавитьРеквизитыДляПроверки(ПроверяемыеРеквизиты, Новый Структура("ЮридическийАдрес", НСтр("ru = 'Юридический адрес'")),
		"Организация", НСтр("ru = 'Юридический адрес: %1'"), Ложь);
	ДобавитьРеквизитыДляПроверки(ПроверяемыеРеквизиты, Новый Структура("ФактическийАдрес", НСтр("ru = 'Фактический адрес'")),
		"Организация", НСтр("ru = 'Фактический адрес: %1'"), Ложь);
	ДобавитьРеквизитыДляПроверки(ПроверяемыеРеквизиты, Новый Структура("Телефон", НСтр("ru = 'Телефон'")),
		"Организация", НСтр("ru = 'Телефон: %1'"), Ложь);
	
	РеквизитыВладельца = РеквизитыВладельцаСертификата(Истина);
	Если ЭтоИндивидуальныйПредприниматель Тогда
		РеквизитыВладельца.Должность = "";
	КонецЕсли;
	
	Если ДокументВид <> "21" Тогда
		РеквизитыВладельца.ДокументНомер = "";
		РеквизитыВладельца.ДокументКодПодразделения = "";
	КонецЕсли;
	
	ДобавитьРеквизитыДляПроверки(ПроверяемыеРеквизиты, РеквизитыВладельца,
		"Сотрудник", НСтр("ru = 'У владельца сертификата не заполнено поле ""%1""'"));
	
	Возврат Не Обработки.ЗаявлениеНаВыпускНовогоКвалифицированногоСертификата.ПроверитьЗаполнениеРеквизитов(
		ПроверяемыеРеквизиты, ЭтоИндивидуальныйПредприниматель);
	
КонецФункции

&НаСервере
Процедура ДобавитьРеквизитыДляПроверки(ПроверяемыеРеквизиты,
	РеквизитыДляПроверки,
	ПолеСообщения,
	ШаблонСообщения,
	ПодставлятьПолеВШаблон = Истина)
	
	Для Каждого Реквизит Из РеквизитыДляПроверки Цикл
		
		ИмяРеквизита = Реквизит.Ключ;
		Если ПустаяСтрока(Реквизит.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыРеквизитаДляПроверки = Новый Структура;
		ПараметрыРеквизитаДляПроверки.Вставить("Имя", ИмяРеквизита);
		Если ИмяРеквизита = "Телефон"
			Или ИмяРеквизита = "ФактическийАдрес"
			Или ИмяРеквизита = "ЮридическийАдрес" Тогда
			ПараметрыРеквизитаДляПроверки.Вставить("Значение", ЭтотОбъект[ИмяРеквизита + "XML"]);
		Иначе
			ПараметрыРеквизитаДляПроверки.Вставить("Значение", ЭтотОбъект[ИмяРеквизита]);
		КонецЕсли;
		ПараметрыРеквизитаДляПроверки.Вставить("ПолеСообщения", ПолеСообщения);
		ПараметрыРеквизитаДляПроверки.Вставить("ТекстНезаполненного", ?(Не ПодставлятьПолеВШаблон, ШаблонСообщения,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, Реквизит.Значение)));
		
		ПроверяемыеРеквизиты.Добавить(ПараметрыРеквизитаДляПроверки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыОрганизации(ДляПроверки = Ложь)
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ЭтоИндивидуальныйПредприниматель");
	Реквизиты.Вставить("НаименованиеСокращенное",
		?(ДляПроверки, НСтр("ru = 'Наименование (сокращенное)'"), ""));
	Реквизиты.Вставить("НаименованиеПолное",
		?(ДляПроверки, НСтр("ru = 'Наименование (полное)'"), ""));
	Реквизиты.Вставить("ИНН",
		?(ДляПроверки, НСтр("ru = 'ИНН'"), ""));
	Реквизиты.Вставить("КПП",
		?(ДляПроверки, НСтр("ru = 'КПП'"), ""));
	Реквизиты.Вставить("ОГРН",
		?(ДляПроверки, НСтр("ru = 'ОГРН'"), ""));
	Реквизиты.Вставить("РасчетныйСчет",
		?(ДляПроверки, НСтр("ru = 'Расчетный счет'"), ""));
	Реквизиты.Вставить("БИК",
		?(ДляПроверки, НСтр("ru = 'БИК'"), ""));
	Реквизиты.Вставить("КорреспондентскийСчет",
		?(ДляПроверки, НСтр("ru = 'Корреспондентский счет'"), ""));
	Реквизиты.Вставить("Телефон",
		?(ДляПроверки, НСтр("ru = 'Телефон'"), ""));
	Реквизиты.Вставить("ЮридическийАдрес",
		?(ДляПроверки, НСтр("ru = 'Юридический адрес'"), ""));
	Реквизиты.Вставить("ФактическийАдрес",
		?(ДляПроверки, НСтр("ru = 'Фактический адрес'"), ""));
	
	Возврат Реквизиты;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыВладельцаСертификата(ДляПроверки = Ложь)
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Фамилия",
		?(ДляПроверки, НСтр("ru = 'Фамилия'"), ""));
	Реквизиты.Вставить("Имя",
		?(ДляПроверки, НСтр("ru = 'Имя'"), ""));
	Реквизиты.Вставить("Отчество");
	Реквизиты.Вставить("ДатаРождения",
		?(ДляПроверки, НСтр("ru = 'Дата рождения'"), ""));
	Реквизиты.Вставить("Пол",
		?(ДляПроверки, НСтр("ru = 'Пол'"), ""));
	Реквизиты.Вставить("МестоРождения",
		?(ДляПроверки, НСтр("ru = 'Место рождения'"), ""));
	Реквизиты.Вставить("Гражданство",
		?(ДляПроверки, НСтр("ru = 'Гражданство'"), ""));
	Реквизиты.Вставить("СтраховойНомерПФР",
		?(ДляПроверки, НСтр("ru = 'СНИЛС'"), ""));
	Реквизиты.Вставить("Должность",
		?(ДляПроверки, НСтр("ru = 'Должность'"), ""));
	Реквизиты.Вставить("Подразделение");
	Реквизиты.Вставить("ДокументВид",
		?(ДляПроверки, НСтр("ru = 'Вид документа, удостоверяющего личность'"), ""));
	Реквизиты.Вставить("ДокументНомер",
		?(ДляПроверки, НСтр("ru = 'Номер документа, удостоверяющего личность'"), ""));
	Реквизиты.Вставить("ДокументКемВыдан",
		?(ДляПроверки, НСтр("ru = 'Кем выдан документ, удостоверяющий личность'"), ""));
	Реквизиты.Вставить("ДокументКодПодразделения",
		?(ДляПроверки, НСтр("ru = 'Код подразделения, выдавшего документ, удостоверяющий личность'"), ""));
	Реквизиты.Вставить("ДокументДатаВыдачи",
		?(ДляПроверки, НСтр("ru = 'Дата выдачи документа, удостоверяющего личность'"), ""));
	Реквизиты.Вставить("ЭлектроннаяПочта",
		?(ДляПроверки, НСтр("ru = 'Электронная почта'"), ""));
	
	Возврат Реквизиты;
	
КонецФункции

#КонецОбласти

#Область ИзменениеДополнительныхРеквизитов

&НаКлиенте
Процедура ОбработчикОжиданияРеквизитыРуководителяПриИзменении()
	
	ПриИзмененииРуководителяНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОжиданияРеквизитыПартнераПриИзменении()
	
	ПриИзмененииПартнераНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииРуководителяНаСервере(Загрузка = Ложь)
	
	Если Не Загрузка Тогда
		Модифицированность = ЗначениеЗаполнено(Объект.СостояниеЗаявления);
		ДокументыПечатались = Ложь;
	КонецЕсли;
	
	ЭтоЗаявленияОтЮрЛица = Не ЭтоИндивидуальныйПредприниматель;
	Элементы.ГруппаПодписантЗаявления.Доступность = ЭтоЗаявленияОтЮрЛица;
	Элементы.ДокументыРуководитель.АвтоОтметкаНезаполненного = ЭтоЗаявленияОтЮрЛица;
	Элементы.ДокументыРуководительОснование.АвтоОтметкаНезаполненного = ЭтоЗаявленияОтЮрЛица;
	Элементы.ДокументыРуководительДолжность.АвтоОтметкаНезаполненного = ЭтоЗаявленияОтЮрЛица;
	
	Если ЭтоЗаявленияОтЮрЛица Тогда
		
		Реквизиты = Новый Структура;
		Реквизиты.Вставить("Должность");
		Реквизиты.Вставить("Основание");
		Реквизиты.Вставить("Организация", ?(ЕстьОрганизации, Организация, Неопределено));
		Реквизиты.Вставить("Представление");
		Реквизиты.Вставить("ТипРуководителя");
		Реквизиты.Вставить("Руководитель",
			?(ДокументыРуководительТип <> Неопределено, ДокументыРуководительСсылка, Неопределено));
		
		ЭлектроннаяПодписьПереопределяемый.ПриЗаполненииРеквизитовРуководителяВЗаявленииНаСертификат(Реквизиты);// АПК:222 Вызов устаревшей для совместимости.
		ЗаявлениеНаСертификатПереопределяемый.ПриЗаполненииРеквизитовРуководителяВЗаявленииНаСертификат(Реквизиты);
		
		ЗаполнитьТипыЗначения(ДокументыРуководительТип, Элементы.ДокументыРуководитель, Реквизиты.ТипРуководителя);
		Элементы.ДокументыРуководитель.КнопкаВыбора = ДокументыРуководительТип <> Неопределено;
		
		Если Загрузка Тогда
			Элементы.ДокументыРуководитель.КнопкаОткрытия = ЗначениеЗаполнено(ДокументыРуководительСсылка);
			Возврат;
		КонецЕсли;
		
		ОчиститьРеквизитыПодписанта();
		
		ДокументыРуководительСсылка = ?(ДокументыРуководительТип <> Неопределено, Реквизиты.Руководитель, Неопределено);
		ДокументыРуководитель = СокрЛП(?(Реквизиты.Представление <> Неопределено, Реквизиты.Представление, Реквизиты.Руководитель));
		Элементы.ДокументыРуководитель.КнопкаОткрытия = ЗначениеЗаполнено(ДокументыРуководительСсылка);
		
		ДокументыРуководительДолжность = Реквизиты.Должность;
		ДокументыРуководительОснование = Реквизиты.Основание;
		
	Иначе
		ОчиститьРеквизитыПодписанта();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПартнераНаСервере(Загрузка = Ложь)
	
	Реквизиты = Новый Структура();
	Реквизиты.Вставить("ИНН");
	Реквизиты.Вставить("КПП");
	Реквизиты.Вставить("Партнер", ?(ДокументыПартнерТип <> Неопределено, ДокументыПартнерСсылка, Неопределено));
	Реквизиты.Вставить("ТипПартнера");
	Реквизиты.Вставить("Организация", ?(ЕстьОрганизации, Организация, Неопределено));
	Реквизиты.Вставить("Представление");
	Реквизиты.Вставить("ЭтоИндивидуальныйПредприниматель", ДокументыПартнерЭтоИП);
	
	ЭлектроннаяПодписьПереопределяемый.ПриЗаполненииРеквизитовПартнераВЗаявленииНаСертификат(Реквизиты);// АПК:222 Вызов устаревшей для совместимости.
	ЗаявлениеНаСертификатПереопределяемый.ПриЗаполненииРеквизитовПартнераВЗаявленииНаСертификат(Реквизиты);
	
	ЗаполнитьТипыЗначения(ДокументыПартнерТип, Элементы.ДокументыПартнер, Реквизиты.ТипПартнера);
	Элементы.ДокументыПартнер.КнопкаВыбора = ДокументыПартнерТип <> Неопределено;
	
	Если Не Загрузка Тогда
		
		Модифицированность = ЗначениеЗаполнено(Объект.СостояниеЗаявления);
		ДокументыПечатались = Ложь;
		
		ОчиститьРеквизитыПартнера();
		
		ДокументыПартнер = СокрЛП(?(Реквизиты.Представление <> Неопределено, Реквизиты.Представление, Реквизиты.Партнер));
		ДокументыПартнерСсылка = ?(ДокументыПартнерТип <> Неопределено, Реквизиты.Партнер, Неопределено);
		
		ДокументыПартнерЭтоИП = Реквизиты.ЭтоИндивидуальныйПредприниматель;
		РеквизитыФормы = РеквизитыОбслуживающейОрганизации();
		РеквизитыФормы.ДокументыПартнерИНН = Реквизиты.ИНН;
		РеквизитыФормы.ДокументыПартнерКПП = Реквизиты.КПП;
		
		УстановитьРеквизит(РеквизитыФормы, "ДокументыПартнерИНН", Истина);
		
		Если ДокументыПартнерЭтоИП Тогда
			ДокументыПартнерКПП = Неопределено;
		Иначе
			УстановитьРеквизит(РеквизитыФормы, "ДокументыПартнерКПП", Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ДокументыПартнер.КнопкаОткрытия = ЗначениеЗаполнено(ДокументыПартнерСсылка);
	Элементы.ДокументыПартнерКПП.Доступность = Не ДокументыПартнерЭтоИП;
	
КонецПроцедуры

&НаСервере
Процедура ДозаполнитьПоПредыдущемуЗаявлению()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СертификатыКлючейЭлектроннойПодписиИШифрования.СодержаниеЗаявления КАК СодержаниеЗаявления
	|ИЗ
	|	Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК СертификатыКлючейЭлектроннойПодписиИШифрования
	|ГДЕ
	|	СертификатыКлючейЭлектроннойПодписиИШифрования.Фамилия = &Фамилия
	|	И СертификатыКлючейЭлектроннойПодписиИШифрования.Имя = &Имя
	|	И СертификатыКлючейЭлектроннойПодписиИШифрования.Отчество = &Отчество
	|	И СертификатыКлючейЭлектроннойПодписиИШифрования.Организация = &Организация
	|	И СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка <> &ЭтаСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	СертификатыКлючейЭлектроннойПодписиИШифрования.ДействителенДо УБЫВ";
	Запрос.УстановитьПараметр("ЭтаСсылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Имя", Имя);
	Запрос.УстановитьПараметр("Фамилия", Фамилия);
	Запрос.УстановитьПараметр("Отчество", Отчество);
	Запрос.УстановитьПараметр("Организация", Организация);
	СведенияОЗаявлении = Запрос.Выполнить().Выбрать();
	
	РеквизитыЗаявления = Неопределено;
	Пока СведенияОЗаявлении.Следующий() Цикл
		
		РеквизитыЗаявления = СведенияОЗаявлении.СодержаниеЗаявления.Получить();
		Если ТипЗнч(РеквизитыЗаявления) <> Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;
		
		Прервать;
		
	КонецЦикла;
	
	Если РеквизитыЗаявления <> Неопределено Тогда
		
		ДозаполнитьРеквизитыИзСведений(РеквизитыЗаявления, РеквизитыОрганизации());
		ДозаполнитьРеквизитыИзСведений(РеквизитыЗаявления, РеквизитыВладельцаСертификата());
		
		РеквизитыПодписанта = РеквизитыПодписанта();
		РеквизитыПодписанта.Вставить("ДокументыРуководительСсылка");
		ДозаполнитьРеквизитыИзСведений(РеквизитыЗаявления, РеквизитыПодписанта);
		Элементы.ДокументыРуководитель.КнопкаОткрытия = ЗначениеЗаполнено(ДокументыРуководительСсылка);
		
		РеквизитыПартнера = РеквизитыОбслуживающейОрганизации();
		РеквизитыПартнера.Вставить("ДокументыПартнерСсылка");
		ДозаполнитьРеквизитыИзСведений(РеквизитыЗаявления, РеквизитыПартнера);
		Элементы.ДокументыПартнер.КнопкаОткрытия = ЗначениеЗаполнено(ДокументыПартнерСсылка);
		Элементы.ДокументыПартнерКПП.Доступность = Не ДокументыПартнерЭтоИП;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДозаполнитьРеквизитыИзСведений(Сведения, Реквизиты)
	
	Для Каждого Реквизит Из Реквизиты Цикл
		
		Если Не ЗначениеЗаполнено(ЭтотОбъект[Реквизит.Ключ]) Тогда
			ЭтотОбъект[Реквизит.Ключ] = Сведения[Реквизит.Ключ];
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьРеквизитыПодписанта()
	
	РеквизитыПодписанта = РеквизитыПодписанта();
	РеквизитыПодписанта.Вставить("ДокументыРуководительСсылка");
	
	Для Каждого Реквизит Из РеквизитыПодписанта Цикл
		ЭтотОбъект[Реквизит.Ключ] = Неопределено;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьРеквизитыПартнера()
	
	Реквизиты = РеквизитыОбслуживающейОрганизации();
	Реквизиты.Вставить("ДокументыПартнерСсылка");
	
	Для Каждого Реквизит Из Реквизиты Цикл
		ЭтотОбъект[Реквизит.Ключ] = Неопределено;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаполнениеДополнительныхРеквизитов()
	
	ПроверяемыеРеквизиты = Новый Массив;
	Если Не ЭтоИндивидуальныйПредприниматель Тогда
		
		Для Каждого Реквизит Из РеквизитыПодписанта(Истина) Цикл
			ПроверяемыеРеквизиты.Добавить(ПараметрыРеквизитаДляПроверки(Реквизит, ЭтотОбъект[Реквизит.Ключ]));
		КонецЦикла;
		
	КонецЕсли;
	
	РеквизитыПартнера = РеквизитыОбслуживающейОрганизации(Истина);
	Если ДокументыПартнерЭтоИП
		Или ПустаяСтрока(ДокументыПартнерКПП) Тогда
		РеквизитыПартнера.ДокументыПартнерКПП = "";
	КонецЕсли;
	
	Если ПустаяСтрока(ДокументыПартнерИНН) Тогда
		РеквизитыПартнера.ДокументыПартнерИНН = "";
	КонецЕсли;
	
	Для Каждого Реквизит Из РеквизитыПартнера Цикл
		
		Если ЗначениеЗаполнено(Реквизит.Значение) Тогда
			ПроверяемыеРеквизиты.Добавить(ПараметрыРеквизитаДляПроверки(Реквизит, ЭтотОбъект[Реквизит.Ключ]));
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Не Обработки.ЗаявлениеНаВыпускНовогоКвалифицированногоСертификата.ПроверитьЗаполнениеРеквизитов(
		ПроверяемыеРеквизиты, ДокументыПартнерЭтоИП);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыРеквизитаДляПроверки(Реквизит, Значение)
	
	ИмяРеквизита = Реквизит.Ключ;
	ПараметрыРеквизитаДляПроверки = Новый Структура;
	ПараметрыРеквизитаДляПроверки.Вставить("Имя", ИмяРеквизита);
	ПараметрыРеквизитаДляПроверки.Вставить("Значение", Значение);
	ПараметрыРеквизитаДляПроверки.Вставить("ПолеСообщения", ИмяРеквизита);
	ПараметрыРеквизитаДляПроверки.Вставить("ТекстНезаполненного",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Поле ""%1"" не заполнено'"), Реквизит.Значение));
		
	Возврат ПараметрыРеквизитаДляПроверки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыПодписанта(ДляПроверки = Ложь)
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ДокументыРуководитель",
		?(ДляПроверки, НСтр("ru = 'Подписант заявления'"), ""));
	Реквизиты.Вставить("ДокументыРуководительДолжность",
		?(ДляПроверки, НСтр("ru = 'Должность'"), ""));
	Реквизиты.Вставить("ДокументыРуководительОснование",
		?(ДляПроверки, НСтр("ru = 'Основание'"), ""));
	
	Возврат Реквизиты;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыОбслуживающейОрганизации(ДляПроверки = Ложь)
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ДокументыПартнер",
		?(ДляПроверки, НСтр("ru = 'Обслуживающая организация'"), ""));
	Реквизиты.Вставить("ДокументыПартнерИНН",
		?(ДляПроверки, НСтр("ru = 'ИНН'"), ""));
	Реквизиты.Вставить("ДокументыПартнерКПП",
		?(ДляПроверки, НСтр("ru = 'КПП'"), ""));
	
	Возврат Реквизиты;
	
КонецФункции

#КонецОбласти

#Область Печать

&НаКлиенте
Функция ОписаниеЗапросаНаКвалифицированныйСертификат(Поля)
	
	СписокПараметров = Новый СписокЗначений;
	Поля = Новый Структура;
	
	Адрес = ЮридическийАдресСтруктура;
	СимволРазделения = СимволРазделенияЧастейИмениИлиЧастейФамилииИлиЧастейОтчестваВСертификате();
	
	// Поле CN - общее имя (организация или индивидуальный предприниматель).
	Если ЭтоИндивидуальныйПредприниматель Тогда
		ПолеОбщееИмя = СокрП(СтрЗаменить(Фамилия, " ", СимволРазделения)
			+ " " + СтрЗаменить(Имя, " ", СимволРазделения)
			+ " " + СтрЗаменить(Отчество, " ", СимволРазделения));
	Иначе
		ПолеОбщееИмя = НаименованиеСокращенное;
	КонецЕсли;
	Поля.Вставить("CN", Лев(ПолеОбщееИмя, 64));
	СписокПараметров.Добавить("2.5.4.3", Поля.CN);
	
	// Поле SN - фамилия должностного лица или индивидуального предпринимателя.
	ПолеФамилия = СтрЗаменить(Фамилия, " ", СимволРазделения);
	Поля.Вставить("SN", Лев(ПолеФамилия, 64));
	СписокПараметров.Добавить("2.5.4.4", Поля.SN);
	
	// Поле G - имя и отчество должностного лица или индивидуального предпринимателя.
	ПолеОтчество = СокрП(СтрЗаменить(Имя, " ", СимволРазделения)
		+ " " + СтрЗаменить(Отчество, " ", СимволРазделения));
	
	Поля.Вставить("G", Лев(ПолеОтчество, 64));
	СписокПараметров.Добавить("2.5.4.42", Поля.G);
	
	// Поле C - страна.
	Поля.Вставить("C", "RU");
	СписокПараметров.Добавить("2.5.4.6", Поля.C);
	
	// Следующие поля S, L, Street - адрес местонахождения организации или
	// регистрации индивидуального предпринимателя.
	
	// Поле S (ST) - регион (<код региона> <имя региона>) - наименование субъекта РФ для СКПЭП,
	// например, "77 г. Москва".
	ПолеРегион = Адрес.КодРегиона + " " + ИмяРегионаРФПоРекомендациямДляСКПЭП(Адрес.КодРегиона);
	Поля.Вставить("S", Лев(ПолеРегион, 128));
	СписокПараметров.Добавить("2.5.4.8", Поля.S);
	
	// Поле L - населенный пункт:
	// "<округ> и/или <район> и/или <город> и/или <внутригородской район> и/или <населенный пункт>".
	// Например, "<район>, <город>, <населенный пункт>":
	// "Москва г, Зеленоград г, Крюково п", "Москва г, Зеленоград г, Малино п".
	ПолеНаселенныйПункт = НаселенныйПунктПолностью(Адрес);
	Поля.Вставить("L", Лев(ПолеНаселенныйПункт, 128));
	СписокПараметров.Добавить("2.5.4.7", Поля.L);
	
	// Поле Street - улица:
	// "<улица> и/или <дополнительная территория> и/или <элемент дополнительной территории>, дом, офис".
	ПолеУлица = ПредставлениеЧастиАдреса(Адрес,
		"Улица,
		|ДополнительнаяТерритория,
		|ЭлементДополнительнойТерритории,
		|Здание, Корпуса, Помещения");
	Поля.Вставить("Street", Лев(ПолеУлица, 128));
	СписокПараметров.Добавить("2.5.4.9", Поля.Street);
	
	Если ЭтоИндивидуальныйПредприниматель Тогда
		// Поле OGRNIP - ОГРНИП индивидуального предпринимателя 15 символов.
		Поля.Вставить("OGRNIP", ОГРН);
		СписокПараметров.Добавить("1.2.643.100.5", Поля.OGRNIP);
	Иначе
		// Поле O - организация.
		Поля.Вставить("O", Лев(НаименованиеСокращенное, 64));
		СписокПараметров.Добавить("2.5.4.10", Поля.O);
		
		// Поле OU - обособленное подразделение должностного лица.
		Если ЗначениеЗаполнено(Подразделение) Тогда
			Поля.Вставить("OU", Лев(Подразделение, 64));
			СписокПараметров.Добавить("2.5.4.11", Поля.OU);
		КонецЕсли;
		
		// Поле T - должность должностного лица.
		Поля.Вставить("T", Лев(Должность, 64));
		СписокПараметров.Добавить("2.5.4.12", Поля.T);
		
		// Поле OGRN - ОГРН юридического лица 13 символов.
		Поля.Вставить("OGRN", ОГРН);
		СписокПараметров.Добавить("1.2.643.100.1", Поля.OGRN);
	КонецЕсли;
	
	// Поле SNILS - СНИЛС должностного лица или индивидуального предпринимателя.
	Поля.Вставить("SNILS", СтраховойНомерПФР);
	СписокПараметров.Добавить("1.2.643.100.3", Поля.SNILS);
	
	// Поле INN - ИНН организации или индивидуального предпринимателя.
	Поля.Вставить("INN", Прав("00" + ИНН, 12));
	СписокПараметров.Добавить("1.2.643.3.131.1.1", Поля.INN);
	
	// Поле E - электронная почта должностного лица организации или индивидуального предпринимателя.
	Поля.Вставить("E", Лев(ЭлектроннаяПочта, 128));
	СписокПараметров.Добавить("1.2.840.113549.1.9.1", Поля.E);
	
	// Подготовка строки запроса.
	
	Свойства = "";
	Для Каждого Параметр Из СписокПараметров Цикл 
		Свойства = Свойства + ",<" + Параметр.Значение + "=" + СокрЛП(Параметр.Представление) + ">";
	КонецЦикла;
	Свойства = Сред(Свойства, 2);
	
	// Использование ключа - Проверка подлинности клиента, Защищенная электронная почта.
	ИспользованиеКлюча = "1.3.6.1.5.5.7.3.2,1.3.6.1.5.5.7.3.4"; 
	
	// Для УЦ КриптоПро 2.0 требуется указать шаблон сертификата.
	// Без встроенной лицензии шаблон - "1.2.643.2.2.46.0.8", со встроенной лицензией
	// шаблон - "1.2.643.2.2.50.1.9.8791462.1882306.9191997.3049782.15111.41178".
	ШаблонСертификата = "1.2.643.2.2.46.0.8";
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"pRequestInfo:{
		|CertAttrs:{%1}
		|CertEnhKeyUsage:{%2}
		|CertPolicies:{<1.2.643.100.113.1=>}
		|dwKeyUsage:{240}
		|SignTool:{%3}
		|CertTemplate:{%4}
		|}",
		Свойства,
		ИспользованиеКлюча,
		ПредставлениеПрограммы,
		ШаблонСертификата);
	
КонецФункции

&НаКлиенте
Процедура НапечататьДокументы()
	
	Документ = ПодготовленныйДокумент();
	Если Документ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторПечатнойФормы = "ЗаявлениеНаВыпускСертификат";
	НазваниеПечатнойФормы = НСтр("ru = 'Заявление на выпуск сертификата'");
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Печать") Тогда
		Документ.Показать(НазваниеПечатнойФормы);
		ДокументыПечатались = Истина;
		Возврат;
	КонецЕсли;
	
	МодульУправлениеПечатьюКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеПечатьюКлиент");
	КоллекцияПечатныхФорм = МодульУправлениеПечатьюКлиент.НоваяКоллекцияПечатныхФорм(ИдентификаторПечатнойФормы);
	
	ПечатнаяФорма = МодульУправлениеПечатьюКлиент.ОписаниеПечатнойФормы(КоллекцияПечатныхФорм, ИдентификаторПечатнойФормы);
	ПечатнаяФорма.СинонимМакета = НазваниеПечатнойФормы;
	ПечатнаяФорма.ТабличныйДокумент = Документ;
	ПечатнаяФорма.ИмяФайлаПечатнойФормы = НазваниеПечатнойФормы;
	
	ОбластиОбъектов = Новый СписокЗначений;
	МодульУправлениеПечатьюКлиент.ПечатьДокументов(КоллекцияПечатныхФорм, ОбластиОбъектов);
	
	ДокументыПечатались = Истина;
	
КонецПроцедуры

&НаСервере
Функция ПодготовленныйДокумент()
	
	Макет = Обработки.ЗаявлениеНаВыпускНовогоКвалифицированногоСертификата.ПолучитьМакет(
		?(ЭтоИндивидуальныйПредприниматель, "ИндивидуальныйПредприниматель", "ЮридическоеЛицо"));
	
	Документ = Новый ТабличныйДокумент;
	ЗаполнитьЗначенияСвойств(Макет.Параметры, ЭтотОбъект);
	ЗаполнитьЗначенияСвойств(Макет.Параметры, ДокументыПоляСертификата);
	
	Макет.Параметры.Телефон = Телефон;
	
	ДокументВидПредставление = ПредставлениеВидаДокумента(ДокументВид);
	ДокументВидПредставление = НРег(Лев(ДокументВидПредставление, 1)) + Сред(ДокументВидПредставление, 2);
	
	ДокументНомерПредставление =
		?(ДокументВид = "21", ПредставлениеРеквизитаНомерПаспортаРФ(ДокументНомер), ДокументНомер);
	
	ДокументКодПодразделенияПредставление = ПредставлениеРеквизитаДокументКодПодразделения(ДокументКодПодразделения);
	ДокументДатаВыдачиПредставление = ПредставлениеРеквизитаДокументДатаВыдачи(ДокументДатаВыдачи);
	
	Макет.Параметры.УдостоверениеЛичности = ДокументВидПредставление + " " + ДокументНомерПредставление + " "
		+ НСтр("ru = 'от'") + " " + ДокументДатаВыдачиПредставление + " "
		+ НСтр("ru = 'выданный'") + " " + ДокументКемВыдан
		+ ?(ЗначениеЗаполнено(ДокументКодПодразделенияПредставление), ", " + НСтр("ru = 'код подразделения'")
			+ " " + ДокументКодПодразделенияПредставление + "", "");
	
	Если ЭтоИндивидуальныйПредприниматель Тогда
		Документ.Вывести(Макет);
	Иначе
		
		Шапка = Макет.ПолучитьОбласть("Шапка");
		Документ.Вывести(Шапка);
		
		Если Должность <> ДокументыРуководительДолжность
			Или Фамилия + " " + Имя + " " + Отчество <> ДокументыРуководитель Тогда
			
			СогласиеНаОбработкуПД = Макет.ПолучитьОбласть("СогласиеНаОбработкуПД");
			Документ.Вывести(СогласиеНаОбработкуПД);
			
		КонецЕсли;
		
		Подвал = Макет.ПолучитьОбласть("Подвал");
		Документ.Вывести(Подвал);
		
	КонецЕсли;
	
	Возврат Документ;
	
КонецФункции

&НаКлиенте
Процедура ПечатьЗаявленияПослеСозданияМенеджераКриптографии(Результат, Контекст) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("МенеджерКриптографии") Тогда
		
		Объект.Программа = Неопределено;
		ЭлектроннаяПодписьСлужебныйКлиент.ПоказатьОшибкуОбращенияКПрограмме(
			НСтр("ru = 'Создание ключа электронной подписи'"), "", Результат,
			Новый Структура, Новый Структура("ПоказатьИнструкцию", Истина));
		
		Возврат;
		
	КонецЕсли;
	
	ПредставлениеПрограммы = ПоставляемоеПредставлениеПрограммы(СписокПрограмм, Объект.Программа);
	
	СоздатьКлючИЗапросНаСертификат(
		Новый ОписаниеОповещения("ПечатьЗаявленияПослеСозданияКлючаИЗапросаНаСертификат", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЗаявленияПослеСозданияКлючаИЗапросаНаСертификат(Результат, Контекст) Экспорт
	
	Доступность = Истина;
	Если Результат <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	НапечататьДокументы();
	
	Если Модифицированность Тогда
		Объект.СостояниеЗаявления = ПредопределенноеЗначение("Перечисление.СостоянияЗаявленияНаВыпускСертификата.Подготовлено");
		ЗаписатьЗаявление();
		ПослеЗаписи();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОтправкаЗаявления

&НаКлиенте
Процедура ОтправитьЗаявлениеПродолжение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ <> "Отправить" Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Невозможно подключиться к порталу интернет-поддержки по причине:
			|Библиотека интернет поддержки пользователей не внедрена в конфигурацию.'"));
		Возврат;
	КонецЕсли;
	
	МодульИнтернетПоддержкаПользователейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"ИнтернетПоддержкаПользователейКлиент");
	
	Если Не ИнтернетПоддержкаПользователейПодключена Тогда
		
		Если МодульИнтернетПоддержкаПользователейКлиент.ДоступноПодключениеИнтернетПоддержки() Тогда
			
			ОповещениеОПодключении = Новый ОписаниеОповещения("ОтправитьЗаявлениеЗавершение", ЭтотОбъект);
			МодульИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
				ОповещениеОПодключении,ЭтотОбъект);
			
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Для отправки заявления необходимо подключить Интернет-поддержку пользователей.'"));
			Возврат;
		КонецЕсли;
		
	Иначе
		ОтправитьЗаявлениеЗавершение(Новый Структура, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗаявлениеЗавершение(Результат, Контекст) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		ИнтернетПоддержкаПользователейПодключена = Ложь;
		Возврат;
	КонецЕсли;
	
	ОписаниеОшибки = "";
	ИнтернетПоддержкаПользователейПодключена = Истина;
	Если ОтправитьЗаявлениеНаСервере(ОписаниеОшибки) Тогда
		ПослеЗаписи();
	Иначе
		
		Если ИнтернетПоддержкаПользователейПодключена Тогда
			ОбработкаПродолжения = Неопределено;
		Иначе
			ОбработкаПродолжения = Новый ОписаниеОповещения(
				"ОтправитьЗаявлениеПослеПредупреждения", ЭтотОбъект);
		КонецЕсли;
		
		ПоказатьПредупреждение(ОбработкаПродолжения,
			УстановитьГиперссылку(ОписаниеОшибки, "its.1c.ru", "http://its.1c.ru"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗаявлениеПослеПредупреждения(Контекст) Экспорт
	
	ОтправитьЗаявлениеПродолжение("Отправить", Неопределено);
	
КонецПроцедуры

&НаСервере
Функция ОтправитьЗаявлениеНаСервере(ОписаниеОшибки)
	
	Доступность = Ложь;
	Если Не ОтправитьЗаявлениеНаПортал(ОписаниеОшибки) Тогда
		Доступность = Истина;
		Возврат Ложь;
	КонецЕсли;
	
	Объект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.Отправлено;
	
	ДатаОбновленияСостояния = ТекущаяДатаСеанса();
	СостояниеОбработкиЗаявления = НСтр("ru = 'Заявление принято для обработки.'");
	
	Доступность = Истина;
	Элементы.ГруппаОбработкаЗаявления.Видимость = Истина;
	
	ЗаписатьЗаявление();
	УстановитьВидимостьДоступность();
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ОтправитьЗаявлениеНаПортал(ОписаниеОшибки)
	
	ОписаниеОшибки = "";
	ШаблонОписанияОшибки = НСтр("ru = 'Не удалось отправить заявление по причине:
		|%1'");
	
	Попытка
		ВебСервис = ВебСервисУЦ();
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписанияОшибки,
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		ОписаниеОшибки = НСтр("ru = 'Невозможно подключиться к порталу интернет-поддержки по причине:
			|Библиотека интернет поддержки пользователей не внедрена в конфигурацию.'");
		Возврат Ложь;
	КонецЕсли;
	
	МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
	УстановитьПривилегированныйРежим(Истина);
	Результат = МодульИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки("http://regservice.1c.ru");
	УстановитьПривилегированныйРежим(Ложь);
	ИнтернетПоддержкаПользователейПодключена = Истина;
	
	Если ЗначениеЗаполнено(Результат.КодОшибки) Тогда
		
		Если Результат.КодОшибки = "НеверныйЛогинИлиПароль" Тогда
			ИнтернетПоддержкаПользователейПодключена = Ложь;
		КонецЕсли;
		
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось подключиться к порталу интернет-поддержки по причине:
			|%1'"), Результат.СообщениеОбОшибке);
		
		Возврат Ложь;
		
	Иначе
		Билет = Результат.Тикет;
	КонецЕсли;
	
	ЭтоПовторнаяОтправкаПакета = Истина;
	Если Не ЗначениеЗаполнено(ИдентификаторДокументооборота) Тогда
		ЭтоПовторнаяОтправкаПакета = Ложь;
		ИдентификаторДокументооборота = НовыйСжатыйУникальныйИдентификатор();
	КонецЕсли;
	
	ЗаявлениеXML = ЗаявлениеXML(Билет);
	ПакетЗаявления = ПакетЗаявления(ЗаявлениеXML);
	
	Попытка
		Ответ = ВебСервис.SendPacket(ПакетЗаявления);
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписанияОшибки,
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
		
		Возврат Ложь;
		
	КонецПопытки;
	
	Если Не ЗначениеЗаполнено(Ответ) Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписанияОшибки,
			НСтр("ru = 'Сервер вернул пустой ответ.'"));
		Возврат Ложь;
	КонецЕсли;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(Ответ);
	
	ПостроительDOM = Новый ПостроительDOM();
	ПостроительDOM = ПостроительDOM.Прочитать(ЧтениеXML);
	
	ЧтениеXML.Закрыть();
	
	УзелDOM = ПостроительDOM.ПолучитьЭлементыПоИмени("code");
	КодРезультата = УзелDOM[0].ТекстовоеСодержимое;
	Если КодРезультата = "0" Тогда
		ДатаОтправки = ТекущаяДатаСеанса();
		Возврат Истина;
	ИначеЕсли КодРезультата = "60" // Документооборот уже зарегистрирован.
		И ЭтоПовторнаяОтправкаПакета Тогда
		
		ДатаОтправки = ТекущаяДатаСеанса();
		Возврат Истина;
	КонецЕсли;
	
	ИдентификаторДокументооборота = "";
	
	УзелDOM = ПостроительDOM.ПолучитьЭлементыПоИмени("errorMessage");
	Если УзелDOM.Количество() > 0 Тогда
		ОписаниеОшибки = УзелDOM[0].ТекстовоеСодержимое;
	КонецЕсли;
	
	ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписанияОшибки,
		ОписаниеОшибки);
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция ПакетЗаявления(ЗаявлениеXML)
	
	// Подготовка пакета заявления.
	ВременныйКаталог = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(
		ПолучитьИмяВременногоФайла("package"));
	
	СоздатьКаталог(ВременныйКаталог);
	
	ИмяФайлаЗаявления = ВременныйКаталог + НовыйСжатыйУникальныйИдентификатор() + ".bin";
	ИмяФайлаОписания  = ВременныйКаталог + "packageDescription.xml";
	ИмяФайлаПакета    = ВременныйКаталог + ИдентификаторДокументооборота + ".zip";
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайлаЗаявления, "windows-1251");
	ЗаписьXML.ЗаписатьБезОбработки(ЗаявлениеXML);
	ЗаписьXML.Закрыть();
	
	ДанныеЗаявления = Новый ДвоичныеДанные(ИмяФайлаЗаявления);
	УдалитьФайлы(ИмяФайлаЗаявления);
	ДанныеЗаявления.Записать(ИмяФайлаЗаявления);
	
	ФайлЗаявления = Новый Файл(ИмяФайлаЗаявления);
	ОписаниеПакетаXML = ОписаниеПакетаXML(ФайлЗаявления.Имя);
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.ОткрытьФайл(ИмяФайлаОписания, "windows-1251");
	ЗаписьXML.ЗаписатьБезОбработки(ОписаниеПакетаXML);
	ЗаписьXML.Закрыть();
	
	ЗаписьАрхива = Новый ЗаписьZipФайла(ИмяФайлаПакета, , , , УровеньСжатияZIP.Максимальный);
	ЗаписьАрхива.Добавить(ИмяФайлаОписания);
	ЗаписьАрхива.Добавить(ИмяФайлаЗаявления);
	ЗаписьАрхива.Записать();
	
	ПакетЗаявления = Base64Строка(Новый ДвоичныеДанные(ИмяФайлаПакета));
	УдалитьФайлы(ВременныйКаталог);
	
	Возврат ПакетЗаявления;
	
КонецФункции

#КонецОбласти

#Область ПрограммыРаботыЭП

&НаКлиенте
Процедура ПриИзмененияСоставаИлиНастроекПрограмм()
	
	ЗаполнитьСписокПрограмм();
	ЗаполнитьПрограмму();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПрограмм()
	
	СписокПрограмм.Очистить();
	
	Если Не ЗначениеЗаполнено(КонтейнерКлючаПуть)
		И Объект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.НеПодготовлено Тогда
		
		ВариантАлгоритмов = ВариантАлгоритмов();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВариантАлгоритмов) Тогда
		ВариантАлгоритмов = "2012";
	КонецЕсли;
	
	ИдентификаторыНастроек = Новый Соответствие;
	Если ВариантАлгоритмов = "2012" Тогда
		ИдентификаторыНастроек.Вставить("VipNet2012",    Истина);
		ИдентификаторыНастроек.Вставить("CryptoPro2012", Истина);
	Иначе
		ИдентификаторыНастроек.Вставить("VipNet2001",    Истина);
		ИдентификаторыНастроек.Вставить("CryptoPro2001", Истина);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Программы.Ссылка КАК Ссылка,
	|	Программы.ИмяПрограммы КАК ИмяПрограммы,
	|	Программы.ТипПрограммы КАК ТипПрограммы
	|ИЗ
	|	Справочник.ПрограммыЭлектроннойПодписиИШифрования КАК Программы";
	Программы = Запрос.Выполнить().Выгрузить();
	
	Настройки = Справочники.ПрограммыЭлектроннойПодписиИШифрования.ПоставляемыеНастройкиПрограмм();
	Для каждого Настройка Из Настройки Цикл
		
		Если ИдентификаторыНастроек.Получить(Настройка.Идентификатор) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Строка = СписокПрограмм.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, Настройка);
		
		ПрограммыВСправочнике = Программы.НайтиСтроки(Новый Структура("ИмяПрограммы, ТипПрограммы",
			Настройка.ИмяПрограммы, Настройка.ТипПрограммы));
			
		Если ПрограммыВСправочнике.Количество() > 0 Тогда
			Строка.Ссылка = ПрограммыВСправочнике[0].Ссылка;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПрограммаОблачногоСервиса) Тогда
		НоваяПрограмма = СписокПрограмм.Добавить();
		НоваяПрограмма.Ссылка = ПрограммаОблачногоСервиса;
		НоваяПрограмма.Представление = НСтр("ru = 'Облачный сервис'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПрограмму(Оповещение = Неопределено)
	
	Если ЗначениеЗаполнено(Объект.Программа) Тогда
		ПослеВыбораПрограммы(Ложь, Оповещение);
		Возврат;
	КонецЕсли;
	
	Контекст = Новый Структура;
	Контекст.Вставить("Оповещение", Оповещение);
	Контекст.Вставить("ПерваяДобавленнаяПрограмма", Неопределено);
	Контекст.Вставить("Индекс", -1);
	
	ЗаполнитьПрограммуЦиклНачало(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПрограммуЦиклНачало(Контекст)
	
	Если СписокПрограмм.Количество() <= Контекст.Индекс + 1 Тогда
		
		Если Контекст.ПерваяДобавленнаяПрограмма <> Неопределено Тогда
			Объект.Программа = Контекст.ПерваяДобавленнаяПрограмма;
		КонецЕсли;
		
		ПослеВыбораПрограммы(Ложь, Контекст.Оповещение);
		Возврат;
		
	КонецЕсли;
	
	Контекст.Индекс = Контекст.Индекс + 1;
	Контекст.Вставить("Строка", СписокПрограмм[Контекст.Индекс]);
	
	Если Контекст.ПерваяДобавленнаяПрограмма = Неопределено
		И ЗначениеЗаполнено(Контекст.Строка.Ссылка) Тогда
		
		Контекст.ПерваяДобавленнаяПрограмма = Контекст.Строка.Ссылка;
	КонецЕсли;
	
	ЭлектроннаяПодписьСлужебныйКлиент.СоздатьМенеджерКриптографии(
		Новый ОписаниеОповещения("ЗаполнитьПрограммуЦиклПослеСозданияМенеджераКриптографии", ЭтотОбъект, Контекст),
		"", Ложь, Контекст.Строка.Ссылка, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПрограммуЦиклПослеСозданияМенеджераКриптографии(Менеджер, Контекст) Экспорт
	
	Если ТипЗнч(Менеджер) <> Тип("МенеджерКриптографии") Тогда
		ЗаполнитьПрограммуЦиклНачало(Контекст);
		Возврат;
	КонецЕсли;
	
	Объект.Программа = Контекст.Строка.Ссылка;
	Если Контекст.Оповещение <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(Контекст.Оповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПрограммыОбработчикОжидания()
	
	ПослеВыбораПрограммы(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПрограммы(ПоказатьОшибку, Оповещение = Неопределено)
	
	Криптография = Неопределено;
	Если Не ЗначениеЗаполнено(Объект.Программа)
		Или Объект.Программа = ПрограммаОблачногоСервиса Тогда
		
		Если Оповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(Оповещение);
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Контекст = Новый Структура;
	Контекст.Вставить("ПоказатьОшибку", ПоказатьОшибку);
	Контекст.Вставить("Оповещение", Оповещение);
	
	ЭлектроннаяПодписьСлужебныйКлиент.СоздатьМенеджерКриптографии(
		Новый ОписаниеОповещения("ПослеВыбораПрограммыПослеСозданияМенеджераКриптографии", ЭтотОбъект, Контекст),
		"", Неопределено, Объект.Программа);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПрограммыПослеСозданияМенеджераКриптографии(Менеджер, Контекст) Экспорт
	
	Если ТипЗнч(Менеджер) <> Тип("МенеджерКриптографии") Тогда
		
		Объект.Программа = Неопределено;
		Если Контекст.ПоказатьОшибку Тогда
			
			ЭлектроннаяПодписьСлужебныйКлиент.ПоказатьОшибкуОбращенияКПрограмме(
				НСтр("ru = 'Выбор программы электронной подписи'"), "", Менеджер,
				Новый Структура, Новый Структура("ПоказатьИнструкцию", Истина));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Контекст.Оповещение <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(Контекст.Оповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеВыбораПрограммы(ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений;
	
	Для Каждого Строка Из СписокПрограмм Цикл
		Значение = ?(ЗначениеЗаполнено(Строка.Ссылка), Строка.Ссылка, Строка.Идентификатор);
		ДанныеВыбора.Добавить(Значение, Строка.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПоставляемоеПредставлениеПрограммы(СписокПрограмм, Программа)
	
	Если ТипЗнч(Программа) = Тип("Строка") Тогда
		Строки = СписокПрограмм.НайтиСтроки(Новый Структура("Идентификатор", Программа));
	Иначе
		Строки = СписокПрограмм.НайтиСтроки(Новый Структура("Ссылка", Программа));
	КонецЕсли;
	
	Возврат Строки[0].Представление;
	
КонецФункции

#КонецОбласти

#Область WebСервис

&НаСервере
Функция ВебСервисУЦ()
	
	ПараметрыПодключения = ОбщегоНазначения.ПараметрыПодключенияWSПрокси();
	ПараметрыПодключения.АдресWSDL = "http://regservice.1c.ru/regservice/regservice.asmx?wsdl"; 
	ПараметрыПодключения.URIПространстваИмен = "http://regservice.keydisk.ru/";
	ПараметрыПодключения.ИмяСервиса = "RegService";
	ПараметрыПодключения.Таймаут = 7;
	
	Возврат ОбщегоНазначения.СоздатьWSПрокси(ПараметрыПодключения);
	
КонецФункции

&НаСервере
Функция ВариантАлгоритмов()
	
	Попытка
		
		ВебСервис = ВебСервисУЦ();
		
		Фабрика = ВебСервис.Определение.ФабрикаXDTO;
		URIПространстваИмен = ВебСервис.ТочкаПодключения.Интерфейс.URIПространстваИмен;
		
		СвойстваФункции = Фабрика.Пакеты.Получить(URIПространстваИмен).КорневыеСвойства.Получить("GetCertificateSettings");
		Если СвойстваФункции = Неопределено Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В веб-сервисе ""%1"" не определена обязательная функция ""GetCertificateSettings"".'"),
				ВебСервис.ТочкаПодключения.Местоположение + "?wsdl");
			
		КонецЕсли;
		
		ТипПустогоПараметра = СвойстваФункции.Тип;
		ЗначениеПустогоПараметра = Фабрика.Создать(ТипПустогоПараметра); 
		
		Ответ = ВебСервис.GetCertificateSettings(ЗначениеПустогоПараметра).GetCertificateSettingsResult;
		
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.УстановитьСтроку(Ответ);
		ПостроительDOM = Новый ПостроительDOM();
		ПостроительDOM = ПостроительDOM.Прочитать(ЧтениеXML);
		ЧтениеXML.Закрыть();
		
		УзелDOM = ПостроительDOM.ПолучитьЭлементыПоИмени("algorithm");
		ВариантАлгоритмов = УзелDOM[0].ТекстовоеСодержимое;
		Если ВариантАлгоритмов <> "2001"
			И ВариантАлгоритмов <> "2012" Тогда
			
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ответ сервера не соответствует ожидаемому:
				|%1'"), Ответ);
			
		КонецЕсли;
		
	Исключение
		
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось получить требуемый вариант алгоритмов от сервера 1С:Подпись по причине:
			|%1'"), КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	Возврат ВариантАлгоритмов;
	
КонецФункции

&НаСервере
Функция ПолучитьСертификат()
	
	ШаблонОписанияОшибки =
		НСтр("ru = 'Не удалось обновить состояние по причине:
		           |%1'");
	
	// Запрос состояния обработки заявления.
	
	Попытка
		ВебСервис = ВебСервисУЦ();
		Ответ = ВебСервис.ReceivePacket(Строка(ИдентификаторДокументооборота));
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ДатаОбновленияСостояния = ТекущаяДатаСеанса();
		СостояниеОбработкиЗаявления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписанияОшибки,
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
		Возврат Неопределено;
	КонецПопытки;
	
	ДатаОбновленияСостояния = ТекущаяДатаСеанса();
	
	Если Не ЗначениеЗаполнено(Ответ) Тогда
		СостояниеОбработкиЗаявления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписанияОшибки,
			НСтр("ru = 'Сервер вернул пустой ответ.'"));
		Возврат Неопределено;
	КонецЕсли;
	
	ОписаниеОшибки = "";
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(Ответ);
	ПостроительDOM = Новый ПостроительDOM();
	ПостроительDOM = ПостроительDOM.Прочитать(ЧтениеXML);
	ЧтениеXML.Закрыть();
	
	УзелDOM = ПостроительDOM.ПолучитьЭлементыПоИмени("code");
	КодРезультата = УзелDOM[0].ТекстовоеСодержимое;
	
	ИмяФайлаПакетОтвета = "";
	Если КодРезультата = "0" Тогда
		УзелDOM = ПостроительDOM.ПолучитьЭлементыПоИмени("packet");
		
		Если УзелDOM.Количество() > 0 Тогда
			ДвоичныеДанные = Base64Значение(УзелDOM[0].ТекстовоеСодержимое);
			ИмяФайлаПакетОтвета = ПолучитьИмяВременногоФайла(".zip");
			ДвоичныеДанные.Записать(ИмяФайлаПакетОтвета);
		Иначе
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписанияОшибки,
				НСтр("ru = 'Сервер вернул пустые данные.'"));
		КонецЕсли;
		
	ИначеЕсли КодРезультата = "1" Тогда
		ОписаниеОшибки = НСтр("ru = 'Заявление еще не обработано, попробуйте позже.'");
	Иначе
		УзелDOM = ПостроительDOM.ПолучитьЭлементыПоИмени("errorMessage");
		Если УзелDOM.Количество() > 0 Тогда
			ОписаниеОшибки = УзелDOM[0].ТекстовоеСодержимое;
		Иначе
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сервер вернул код ошибки: %1'"),
				Строка(КодРезультата));
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		СостояниеОбработкиЗаявления = ОписаниеОшибки;
		Возврат Неопределено;
	КонецЕсли;
	
	ВременныйКаталог = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(
		ПолучитьИмяВременногоФайла("package"));
	
	СоздатьКаталог(ВременныйКаталог);
	
	ФайлНайден = Ложь;
	Попытка
		Архив1 = Новый ЧтениеZipФайла(ИмяФайлаПакетОтвета);
		
		Для Счетчик = 0 По Архив1.Элементы.Количество() - 1 Цикл
			
			Если Архив1.Элементы[Счетчик].Расширение = "bin" Тогда
				ФайлНайден = Истина;
				
				Архив1.Извлечь(Архив1.Элементы[Счетчик], ВременныйКаталог);
				Архив2 = Новый ЧтениеZipФайла(ВременныйКаталог + Архив1.Элементы[Счетчик].Имя);
				Архив2.ИзвлечьВсе(ВременныйКаталог);
				
				ЧтениеXML = Новый ЧтениеXML;
				ЧтениеXML.ОткрытьФайл(ВременныйКаталог + "file");
				ПостроительDOM = Новый ПостроительDOM;
				ДокументDOM  = ПостроительDOM.Прочитать(ЧтениеXML);
				ЧтениеXML.Закрыть();
				
				РегистрацияУспешна = Булево(ЗначениеУзлаXML(ДокументDOM, "РегистрацияУспешна"));
				СостояниеОбработкиЗаявления = ЗначениеУзлаXML(ДокументDOM, "Результат");
				
				Если РегистрацияУспешна Тогда
					ИдентификаторАбонента = ЗначениеУзлаXML(ДокументDOM, "ИдентификаторАбонента");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ФайлНайден Тогда
			ВызватьИсключение НСтр("ru = 'Неверный формат данных.'");
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось разобрать ответ сервера по причине:
			           |%1'"),
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
	КонецПопытки;
	
	УдалитьФайлы(ИмяФайлаПакетОтвета);
	УдалитьФайлы(ВременныйКаталог);
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		СостояниеОбработкиЗаявления = ОписаниеОшибки;
		Возврат Неопределено;
	КонецЕсли;
	
	Если Не РегистрацияУспешна Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Запрос сертификата.
	
	Ответ = ВебСервис.ReceiveUpdatedPacket(Строка(ИдентификаторАбонента), Дата('00010101'));
	
	ОписаниеОшибки = "";
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(Ответ);
	ПостроительDOM = Новый ПостроительDOM();
	ПостроительDOM = ПостроительDOM.Прочитать(ЧтениеXML);
	ЧтениеXML.Закрыть();
	
	УзелDOM = ПостроительDOM.ПолучитьЭлементыПоИмени("code");
	КодРезультата = УзелDOM[0].ТекстовоеСодержимое;
	
	ИмяФайлаПакетОтвета = "";
	Если КодРезультата = "0" Тогда
		УзелDOM = ПостроительDOM.ПолучитьЭлементыПоИмени("packet");
		
		Если УзелDOM.Количество() > 0 Тогда
			ДвоичныеДанные = Base64Значение(УзелDOM[0].ТекстовоеСодержимое);
			ИмяФайлаПакетОтвета = ПолучитьИмяВременногоФайла(".zip");
			ДвоичныеДанные.Записать(ИмяФайлаПакетОтвета);
		Иначе
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОписанияОшибки,
				НСтр("ru = 'Сервер вернул пустые данные.'"));
		КонецЕсли;
	Иначе
		УзелDOM = ПостроительDOM.ПолучитьЭлементыПоИмени("errorMessage");
		Если УзелDOM.Количество() > 0 Тогда
			ОписаниеОшибки = УзелDOM[0].ТекстовоеСодержимое;
		Иначе
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Сервер вернул код ошибки: %1'"),
				Строка(КодРезультата));
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		СостояниеОбработкиЗаявления = ОписаниеОшибки;
		Возврат Неопределено;
	КонецЕсли;
	
	ВременныйКаталог = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(
		ПолучитьИмяВременногоФайла("package"));
	
	СоздатьКаталог(ВременныйКаталог);
	
	ФайлНайден = Ложь;
	
	Попытка
		Архив = Новый ЧтениеZipФайла(ИмяФайлаПакетОтвета);
		
		Для Счетчик = 0 По Архив.Элементы.Количество() - 1 Цикл
			
			Если Архив.Элементы[Счетчик].Расширение = "xml" Тогда
				ФайлНайден = Истина;
				
				Архив.Извлечь(Архив.Элементы[Счетчик], ВременныйКаталог);
				
				ЧтениеXML = Новый ЧтениеXML;
				ЧтениеXML.ОткрытьФайл(ВременныйКаталог + Архив.Элементы[Счетчик].Имя); 
				ПостроительDOM = Новый ПостроительDOM;
				ДокументDOM = ПостроительDOM.Прочитать(ЧтениеXML);
				ЧтениеXML.Закрыть();
				
				УзелDOM = ДокументDOM.ПолучитьЭлементыПоИмени("Сертификат");
				Если УзелDOM.Количество() < 2 Тогда
					ВызватьИсключение НСтр("ru = 'Неверный формат данных.'");
				КонецЕсли;
				
				СертификатСтрокой = "";
				КорневойСертификатСтрокой = "";
				
				Для Каждого Узел Из УзелDOM Цикл
					Хранилище = Узел.Атрибуты.ПолучитьИменованныйЭлемент("Хранилище").ТекстовоеСодержимое;
					Если Хранилище = "MY" Тогда
						СертификатСтрокой = Узел.ТекстовоеСодержимое;
					КонецЕсли;
					Если Хранилище = "ROOT" Тогда
						КорневойСертификатСтрокой = Узел.ТекстовоеСодержимое;
					КонецЕсли;
				КонецЦикла;
				
				Если Не ЗначениеЗаполнено(СертификатСтрокой)
				 Или Не ЗначениеЗаполнено(КорневойСертификатСтрокой) Тогда
					
					ВызватьИсключение НСтр("ru = 'Неверный формат данных.'");
				КонецЕсли;
				
				ДанныеСертификата          = Base64Значение(СертификатСтрокой);
				ДанныеКорневогоСертификата = Base64Значение(КорневойСертификатСтрокой);
				
				Сертификат         = Новый СертификатКриптографии(ДанныеСертификата);
				КорневойСертификат = Новый СертификатКриптографии(ДанныеКорневогоСертификата);
			КонецЕсли;
		КонецЦикла;
		
		Если Не ФайлНайден Тогда
			ВызватьИсключение НСтр("ru = 'Неверный формат данных.'");
		КонецЕсли;
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось разобрать ответ сервера по причине:
			           |%1'"),
			КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
	КонецПопытки;
	
	УдалитьФайлы(ИмяФайлаПакетОтвета);
	УдалитьФайлы(ВременныйКаталог);
	
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		СостояниеОбработкиЗаявления = ОписаниеОшибки;
		Возврат Неопределено;
	КонецЕсли;
	
	СвойстваСертификата = ЭлектроннаяПодпись.СвойстваСертификата(Сертификат);
	СвойстваСубъекта    = ЭлектроннаяПодпись.СвойстваСубъектаСертификата(Сертификат);
	
	АдресСертификата          = ПоместитьВоВременноеХранилище(ДанныеСертификата, УникальныйИдентификатор);
	АдресКорневогоСертификата = ПоместитьВоВременноеХранилище(ДанныеКорневогоСертификата, УникальныйИдентификатор);
	ОтпечатокКорневогоСертификата = Base64Строка(КорневойСертификат.Отпечаток);
	
	// Важно заполнить отпечаток, чтобы не было возможности добавить дубль сертификата.
	Объект.Наименование = ЭлектроннаяПодпись.ПредставлениеСертификата(Сертификат);
	
	Объект.Подписание     = СвойстваСертификата.Подписание;
	Объект.Шифрование     = СвойстваСертификата.Шифрование;
	Объект.Отпечаток      = СвойстваСертификата.Отпечаток;
	Объект.КомуВыдан      = СвойстваСертификата.КомуВыдан;
	Объект.КемВыдан       = СвойстваСертификата.КемВыдан;
	Объект.ДействителенДо = СвойстваСертификата.ДатаОкончания;
	
	Объект.Фамилия   = СвойстваСубъекта.Фамилия;
	Объект.Имя       = СвойстваСубъекта.Имя;
	Объект.Отчество  = СвойстваСубъекта.Отчество;
	Объект.Должность = СвойстваСубъекта.Должность;
	Объект.Фирма     = СвойстваСубъекта.Организация;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область РаботаСXML

&НаСервере
Функция ЗаявлениеXML(Билет)
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("windows-1251");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("Заявление");
	ЗаписьXML.ЗаписатьАтрибут("ВерсФорм", "1.3");
	ЗаписьXML.ЗаписатьАтрибут("ДатаВремяФормирования", XMLСтрока(ТекущаяДатаСеанса()));
	ЗаписьXML.ЗаписатьАтрибут("ВерсПрог", "1С");
	
		ЗаписьXMLЭлемент(ЗаписьXML, "ТипЗаявления", "1");
		ЗаписьXMLЭлемент(ЗаписьXML, "ПризнакЮридическогоЛица", Не ЭтоИндивидуальныйПредприниматель);
		ЗаписьXMLЭлемент(ЗаписьXML, "ИНН");
		
		Если Не ЭтоИндивидуальныйПредприниматель Тогда
			ЗаписьXMLЭлемент(ЗаписьXML, "КПП");
		КонецЕсли;
		
		ЗаписьXMLЭлемент(ЗаписьXML, "ОГРН");
		ЗаписьXMLЭлемент(ЗаписьXML, "ПолноеНаименование",  НаименованиеПолное);
		ЗаписьXMLЭлемент(ЗаписьXML, "КраткоеНаименование", НаименованиеСокращенное);
		ЗаписьXMLЭлемент(ЗаписьXML, "ТелефонОсновной",     Телефон);
		
		ЗаписьXMLАдрес(ЗаписьXML, "АдресЮридический", ЮридическийАдресXML);
		ЗаписьXMLАдрес(ЗаписьXML, "АдресФактический", ФактическийАдресXML);
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("ВладельцыЭЦП");
		ЗаписьXML.ЗаписатьНачалоЭлемента("ВладелецЭЦП");
		ЗаписьXML.ЗаписатьАтрибут("СНИЛС", ПредставлениеРеквизитаСтраховойНомерПФР(СтраховойНомерПФР));
		
			ЗаписьXML.ЗаписатьНачалоЭлемента("ФИО");
			ЗаписьXML.ЗаписатьАтрибут("Фамилия",  Фамилия);
			ЗаписьXML.ЗаписатьАтрибут("Имя",      Имя);
			ЗаписьXML.ЗаписатьАтрибут("Отчество", Отчество);
			ЗаписьXML.ЗаписатьКонецЭлемента(); // КонецЭлемента ФИО.
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("ЛичныеДанные");
			ЗаписьXML.ЗаписатьАтрибут("ДатаРождения",  ПредставлениеРеквизитаДатаРождения(ДатаРождения));
			ЗаписьXML.ЗаписатьАтрибут("Пол",           Пол);
			ЗаписьXML.ЗаписатьАтрибут("МестоРождения", МестоРождения);
			ЗаписьXML.ЗаписатьАтрибут("Гражданство",   ГражданствоОКСМКодАльфа3);
			ЗаписьXML.ЗаписатьКонецЭлемента(); // КонецЭлемента ЛичныеДанные.
			
			Если ДокументВид = "21" Тогда
				ДокументНомерПредставление = ПредставлениеРеквизитаНомерПаспортаРФ(ДокументНомер);
				ДокументКодПодразделенияПредставление =
					ПредставлениеРеквизитаДокументКодПодразделения(ДокументКодПодразделения);
			Иначе
				ДокументНомерПредставление = ДокументНомер;
				ДокументКодПодразделенияПредставление = "";
			КонецЕсли;
			ДокументДатаВыдачиПредставление = ПредставлениеРеквизитаДокументДатаВыдачи(ДокументДатаВыдачи);
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("УдЛичн");
			ЗаписьXML.ЗаписатьАтрибут("КодВидДок", ДокументВид);
			ЗаписьXML.ЗаписатьАтрибут("СерНомДок", ДокументНомерПредставление);
			ЗаписьXML.ЗаписатьАтрибут("ДатаДок",   ДокументДатаВыдачиПредставление);
			ЗаписьXML.ЗаписатьАтрибут("ВыдДок",    ДокументКемВыдан);
			ЗаписьXML.ЗаписатьАтрибут("КодВыдДок", ДокументКодПодразделенияПредставление);
			ЗаписьXML.ЗаписатьКонецЭлемента(); // КонецЭлемента УдЛичн.
			
			Если Не ЭтоИндивидуальныйПредприниматель Тогда
				ЗаписьXMLЭлемент(ЗаписьXML, "Должность",  Должность);
			КонецЕсли;
			
			ЗаписьXMLЭлемент(ЗаписьXML, "ЭлектроннаяПочта", ЭлектроннаяПочта);
			
			Строки = СписокПрограмм.НайтиСтроки(Новый Структура("Ссылка", Объект.Программа));
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("Криптопровайдер");
			ЗаписьXMLЭлемент(ЗаписьXML, "ТипКриптопровайдера", XMLСтрока(Строки[0].ТипПрограммы));
			ЗаписьXMLЭлемент(ЗаписьXML, "ИмяКриптопровайдера", XMLСтрока(Строки[0].ИмяПрограммы));
			ЗаписьXML.ЗаписатьКонецЭлемента(); // КонецЭлемента Криптопровайдер.
			
			ЗаписьXML.ЗаписатьНачалоЭлемента("ЗапросНаСертификат");
			ЗаписьXML.ЗаписатьАтрибут("УдостоверяющийЦентр", "ООО ""НПЦ ""1С"""); // АПК:1297 Не локализуется (формат веб-сервиса).
			ЗаписьXML.ЗаписатьТекст(ТекстФайлаЗапросаНаСертификат());
			ЗаписьXML.ЗаписатьКонецЭлемента(); // КонецЭлемента ЗапросНаСертификат.
		
		ЗаписьXML.ЗаписатьКонецЭлемента(); // КонецЭлемента ВладелецЭЦП.
		ЗаписьXML.ЗаписатьКонецЭлемента(); // КонецЭлемента ВладельцыЭЦП.
		
		ЗаписьXMLЭлемент(ЗаписьXML, "ИННПартнера", ДокументыПартнерИНН);
		Если Не ДокументыПартнерЭтоИП Тогда
			ЗаписьXMLЭлемент(ЗаписьXML, "КПППартнера", ДокументыПартнерКПП);
		КонецЕсли;
		
		ЗаписьXMLЭлемент(ЗаписьXML, "БилетПользователя", Билет);
		
	ЗаписьXML.ЗаписатьКонецЭлемента(); // КонецЭлемента Заявление.
	
	СтрокаXML = ЗаписьXML.Закрыть();
	
	Возврат СтрокаXML;
	
КонецФункции

&НаСервере
Процедура ЗаписьXMLЭлемент(ЗаписьXML, ИмяЭлемента, Значение = Неопределено)
	
	Если Значение = Неопределено Тогда
		Значение = ЭтотОбъект[ИмяЭлемента];
	КонецЕсли;
	
	ЗаписьXML.ЗаписатьНачалоЭлемента(ИмяЭлемента);
	ЗаписьXML.ЗаписатьТекст(XMLСтрока(Значение));
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписьXMLАдрес(ЗаписьXML, ИмяЭлемента, АдресXML)
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
		Возврат;
	КонецЕсли;
	
	МодульРаботаСАдресами = ОбщегоНазначения.ОбщийМодуль("РаботаСАдресами");
	
	ДополнительныеПараметры = Новый Структура("НаименованиеВключаетСокращение", Истина);
	АдресСтруктура = МодульРаботаСАдресами.СведенияОбАдресе(АдресXML, ДополнительныеПараметры);
	
	ПервыйКорпус    = ?(АдресСтруктура.Корпуса.Количество()   > 0, АдресСтруктура.Корпуса[0].Номер, "");
	ПервоеПомещение = ?(АдресСтруктура.Помещения.Количество() > 0, АдресСтруктура.Помещения[0].Номер, "");
	
	УлицаПолностью = ПредставлениеЧастиАдреса(АдресСтруктура,
		"Улица,
		|ДополнительнаяТерритория,
		|ЭлементДополнительнойТерритории");
	
	ЗаписьXML.ЗаписатьНачалоЭлемента(ИмяЭлемента);
	
	ЗаписьXML.ЗаписатьАтрибут("КодРегион",  АдресСтруктура.КодРегиона);
	ЗаписьXML.ЗаписатьАтрибут("НаселПункт", НаселенныйПунктПолностью(АдресСтруктура));
	ЗаписьXML.ЗаписатьАтрибут("Улица",      УлицаПолностью);
	ЗаписьXML.ЗаписатьАтрибут("Дом",        АдресСтруктура.Здание.Номер);
	ЗаписьXML.ЗаписатьАтрибут("Корпус",     ПервыйКорпус);
	ЗаписьXML.ЗаписатьАтрибут("Кварт",      ПервоеПомещение);
	
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

&НаСервере
Функция ТекстФайлаЗапросаНаСертификат()
	
	ТекстОшибки = НСтр("ru = 'Не удалось прочитать ранее созданный запрос на сертификат.
		|Заявление не может быть отправлено.
		|Удалите его и создайте новое.'");
	
	Если Не ЗначениеЗаполнено(АдресЗапросаНаСертификат) Тогда
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(АдресЗапросаНаСертификат);
	Если ТипЗнч(ДвоичныеДанныеФайла) <> Тип("ДвоичныеДанные") Тогда
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".p10");
	ДвоичныеДанныеФайла.Записать(ИмяВременногоФайла);
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ИмяВременногоФайла);
	Текст = СокрЛП(ТекстовыйДокумент.ПолучитьТекст());
	
	УдалитьФайлы(ИмяВременногоФайла);
	
	Если Не ЗначениеЗаполнено(Текст) Тогда
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	Возврат Текст;
	
КонецФункции

&НаСервере
Функция ОписаниеПакетаXML(ИмяФайлаЗаявления)
	
	ЗаписьXML = Новый ЗаписьXML;
	ЗаписьXML.УстановитьСтроку("windows-1251");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	
	ЗаписьXML.ЗаписатьНачалоЭлемента("пакет");
	ЗаписьXML.ЗаписатьАтрибут("версияФормата", "1С:1.0");
	
	ЗаписьXML.ЗаписатьАтрибут("версПрог", "1.0");
	ЗаписьXML.ЗаписатьАтрибут("типДокументооборота", "РегистрацияАбонентаЭДО");
	ЗаписьXML.ЗаписатьАтрибут("типТранзакции", "Регистрация");
	ЗаписьXML.ЗаписатьАтрибут("идентификаторДокументооборота", ИдентификаторДокументооборота);
	
		ЗаписьXML.ЗаписатьНачалоЭлемента("отправитель");
		ЗаписьXML.ЗаписатьАтрибут("типСубъекта", "абонент");
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("получатель");
		ЗаписьXML.ЗаписатьАтрибут("идентификаторСубъекта", "КалугаАстрал");
		ЗаписьXML.ЗаписатьАтрибут("типСубъекта", "спецоператор");
		ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьНачалоЭлемента("документ");
		ЗаписьXML.ЗаписатьАтрибут("идентификаторДокумента", НовыйСжатыйУникальныйИдентификатор());
		ЗаписьXML.ЗаписатьАтрибут("типДокумента", "Заявление");
		ЗаписьXML.ЗаписатьАтрибут("типСодержимого", "xml");
		ЗаписьXML.ЗаписатьАтрибут("сжат", "false");
		ЗаписьXML.ЗаписатьАтрибут("зашифрован", "false");
		
			ЗаписьXML.ЗаписатьНачалоЭлемента("содержимое");
			ЗаписьXML.ЗаписатьАтрибут("имяФайла", ИмяФайлаЗаявления);
			ЗаписьXML.ЗаписатьКонецЭлемента();
		
		ЗаписьXML.ЗаписатьКонецЭлемента(); // КонецЭлемента документ.
	
	ЗаписьXML.ЗаписатьКонецЭлемента(); // КонецЭлемента пакет.
	
	СтрокаXML = ЗаписьXML.Закрыть();
	
	Возврат СтрокаXML;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗначениеУзлаXML(ДокументDOM, НазваниеУзла, ЗначениеПоУмолчанию = "")
	
	УзелDOM = ДокументDOM.ПолучитьЭлементыПоИмени(НазваниеУзла);
	
	ПодходящийЭлементDOM = Неопределено;
	
	Если УзелDOM.Количество() > 0 Тогда
		ПодходящийЭлементDOM = УзелDOM[0];
	КонецЕсли;
	
	Если ПодходящийЭлементDOM = Неопределено Тогда
		Возврат ЗначениеПоУмолчанию;
		
	ИначеЕсли НазваниеУзла = "Отпечаток" Тогда
		Возврат НРег(ПодходящийЭлементDOM.ТекстовоеСодержимое);
	Иначе
		Возврат ПодходящийЭлементDOM.ТекстовоеСодержимое;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область КлючИЗапросНаСертификат

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификат(Оповещение)
	
	Контекст = Новый Структура;
	Контекст.Вставить("Оповещение", Оповещение);
	Если Криптография <> Неопределено Тогда
		СоздатьКлючИЗапросНаСертификатПослеСозданияОбъектаКриптографии(Истина, Контекст);
	Иначе
		СоздатьОбъектКриптографии(Новый ОписаниеОповещения(
			"СоздатьКлючИЗапросНаСертификатПослеСозданияОбъектаКриптографии", ЭтотОбъект, Контекст),
			НСтр("ru = 'Для создания ключа электронной подписи и запроса на сертификат
			           |требуется установить расширение для веб-клиента 1С:Предприятия.'"),
			НСтр("ru = 'Для создания ключа электронной подписи и запроса на сертификат
			           |требуется установить внешнюю компоненту.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеСозданияОбъектаКриптографии(Результат, Контекст) Экспорт
	
	Если Результат <> Истина Тогда
		ВыполнитьОбработкуОповещения(Контекст.Оповещение, Ложь);
		Возврат;
	КонецЕсли;
	
	ОповещениеПослеПроверкиСуществования = Новый ОписаниеОповещения(
		"СоздатьКлючИЗапросНаСертификатПослеПроверкиСуществованияКонтейнераКлючей", ЭтотОбъект, Контекст);
	
	ПроверитьСуществованиеКонтейнераКлючей(ОповещениеПослеПроверкиСуществования, КонтейнерКлючаПуть, КонтейнерКлючаИмя);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеПроверкиСуществованияКонтейнераКлючей(Существует, Контекст) Экспорт
	
	Если Существует Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Используется ранее созданный контейнер ключа:'"),,
			КонтейнерКлючаПуть);
		
		СоздатьКлючИЗапросНаСертификатПослеПодготовкиКонтейнераКлюча(Контекст);
	Иначе
		ПолучитьИмяНовогоКонтейнераКлюча(Новый ОписаниеОповещения(
			"СоздатьКлючИЗапросНаСертификатПослеПолученияИмениНовогоКонтейнераКлюча", ЭтотОбъект, Контекст));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеПодготовкиКонтейнераКлюча(Контекст)
	
	ПолучитьКаталогВременныхФайловКомпоненты(Новый ОписаниеОповещения(
		"СоздатьКлючИЗапросНаСертификатПослеПолученияКаталогаВременныхФайловКомпоненты", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеПолученияИмениНовогоКонтейнераКлюча(КонтейнерКлючаНовоеИмя, Контекст) Экспорт
	
	АктивноеОкно().Активизировать();
	
	Контекст.Вставить("КонтейнерКлючаНовоеИмя", КонтейнерКлючаНовоеИмя);
	ОповещениеПослеВызоваСоздатьКонтейнер = Новый ОписаниеОповещения(
		"СоздатьКлючИЗапросНаСертификатПослеВызоваСоздатьКонтейнер", ЭтотОбъект, Контекст,
		"СоздатьКлючИЗапросНаСертификатПослеОшибкиВызоваСоздатьКонтейнер", ЭтотОбъект);
	
	Криптография.НачатьВызовСоздатьКонтейнер(ОповещениеПослеВызоваСоздатьКонтейнер, КонтейнерКлючаНовоеИмя);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеВызоваСоздатьКонтейнер(Путь, ПараметрыВызова, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("ПутьПослеВызоваСоздатьКонтейнер", Путь);
	
	Криптография.НачатьПолучениеСписокОшибок(Новый ОписаниеОповещения(
		"СоздатьКлючИЗапросНаСертификатПослеВызоваСоздатьКонтейнерПослеПолученияСписокОшибок", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеВызоваСоздатьКонтейнерПослеПолученияСписокОшибок(СписокОшибок, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СписокОшибок) Тогда
		СоздатьКлючИЗапросНаСертификатПослеОшибкиВызоваСоздатьКонтейнер(СписокОшибок, Ложь, Контекст);
		Возврат;
	КонецЕсли;
	
	Путь = Контекст.ПутьПослеВызоваСоздатьКонтейнер;
	
	АктивноеОкно().Активизировать();
	
	Если Не ЗначениеЗаполнено(Путь) Тогда
		
		Строки = СписокПрограмм.НайтиСтроки(Новый Структура("Ссылка", Объект.Программа));
		Если СтрНачинаетсяС(Строки[0].Идентификатор, "CryptoPro") Тогда
			ПоказатьПредупреждение(, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ключ электронной подписи не создан.
					|
					|Следует учитывать, что для создания ключа с помощью программы %1,
					|требуются права администратора операционной системы.'"),
				Строки[0].Представление));
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Ключ электронной подписи не создан.'"));
		КонецЕсли;
		
		ВыполнитьОбработкуОповещения(Контекст.Оповещение, Ложь);
		Возврат;
		
	КонецЕсли;
	
	КонтейнерКлючаИмя  = Контекст.КонтейнерКлючаНовоеИмя;
	КонтейнерКлючаПуть = Путь;
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Создан новый контейнер ключа:'"),, КонтейнерКлючаПуть);
	
	СоздатьКлючИЗапросНаСертификатПослеПодготовкиКонтейнераКлюча(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеОшибкиВызоваСоздатьКонтейнер(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт
	
	Криптография = Неопределено;
	СтандартнаяОбработка = Ложь;
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Ошибка = Новый Структура;
	Ошибка.Вставить("ПоказатьИнструкцию", Истина);
	Ошибка.Вставить("ОписаниеОшибки", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не удалось выполнить операцию по причине:
			|%1'"), ПредставлениеОшибкиДляПользователя(ИнформацияОбОшибке)));
	
	ЭлектроннаяПодписьСлужебныйКлиент.ПоказатьОшибкуОбращенияКПрограмме(
		НСтр("ru = 'Создание ключа электронной подписи'"), "", Ошибка,
		Новый Структура, Новый Структура("ПоказатьИнструкцию", Истина));
	
	ВыполнитьОбработкуОповещения(Контекст.Оповещение, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеПолученияКаталогаВременныхФайловКомпоненты(Каталог, Контекст) Экспорт
	
	Контекст.Вставить("ИмяФайлаЗапроса", Каталог + КонтейнерКлючаИмя + ".p10");
	Контекст.Вставить("ПомещаемыеФайлы", Новый Массив);
	Контекст.ПомещаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(Контекст.ИмяФайлаЗапроса));
	
	Вызовы = Новый Массив;
	ДобавитьВызов(Вызовы, "НачатьПомещениеФайлов", Контекст.ПомещаемыеФайлы, Неопределено, Ложь, УникальныйИдентификатор);
	ДобавитьВызов(Вызовы, "НачатьУдалениеФайлов",  Контекст.ИмяФайлаЗапроса, Неопределено);
	
	НачатьЗапросРазрешенияПользователя(Новый ОписаниеОповещения(
		"СоздатьКлючИЗапросНаСертификатПослеПолученияРазрешений", ЭтотОбъект, Контекст), Вызовы);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеПолученияРазрешений(РазрешенияПолучены, Контекст) Экспорт
	
	Если Не РазрешенияПолучены Тогда
		ВыполнитьОбработкуОповещения(Контекст.Оповещение, Ложь);
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("ЗапросСоздан", Истина);
	Контекст.Вставить("ПоляСертификата", Неопределено);
	
	ОписаниеЗапроса = ОписаниеЗапросаНаКвалифицированныйСертификат(Контекст.ПоляСертификата);
	ФлагКвалифицированнойЭП = 67108864;
	
	АктивноеОкно().Активизировать();
	
	ОповещениеПослеВызоваСоздатьЗапрос = Новый ОписаниеОповещения(
		"СоздатьКлючИЗапросНаСертификатПослеВызоваСоздатьЗапросНаСертификат", ЭтотОбъект, Контекст,
		"СоздатьКлючИЗапросНаСертификатПослеОшибкиВызоваСоздатьЗапросНаСертификат", ЭтотОбъект);
	
	Криптография.НачатьВызовСоздатьЗапросНаСертификат(ОповещениеПослеВызоваСоздатьЗапрос,
		ОписаниеЗапроса, КонтейнерКлючаПуть, Контекст.ИмяФайлаЗапроса, ФлагКвалифицированнойЭП);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеВызоваСоздатьЗапросНаСертификат(Результат, ПараметрыВызова, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("РезультатПослеВызоваСоздатьЗапросНаСертификат", Результат);
	
	Криптография.НачатьПолучениеСписокОшибок(Новый ОписаниеОповещения(
		"СоздатьКлючИЗапросНаСертификатПослеВызоваСоздатьЗапросНаСертификатПослеПолученияСписокОшибок", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеВызоваСоздатьЗапросНаСертификатПослеПолученияСписокОшибок(СписокОшибок, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СписокОшибок) Тогда
		СоздатьКлючИЗапросНаСертификатПослеОшибкиВызоваСоздатьЗапросНаСертификат(СписокОшибок, Ложь, Контекст);
		Возврат;
	КонецЕсли;
	
	Результат = Контекст.РезультатПослеВызоваСоздатьЗапросНаСертификат;
	
	Если Результат <> Истина Тогда
		Контекст.ЗапросСоздан = Ложь;
		СоздатьКлючИЗапросНаСертификатЗавершение(Контекст);
		Возврат;
	КонецЕсли;
	
	АктивноеОкно().Активизировать();
	
	Контекст.Вставить("Файл", Новый Файл(Контекст.ИмяФайлаЗапроса));
	ОповещениеПослеПроверкиСуществования = Новый ОписаниеОповещения(
		"СоздатьКлючИЗапросНаСертификатПослеПроверкиСуществованияФайла", ЭтотОбъект, Контекст);
	
	Контекст.Файл.НачатьПроверкуСуществования(ОповещениеПослеПроверкиСуществования);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеПроверкиСуществованияФайла(Существует, Контекст) Экспорт
	
	Если Не Существует Тогда
		Контекст.ЗапросСоздан = Ложь;
		СоздатьКлючИЗапросНаСертификатЗавершение(Контекст);
		Возврат;
	КонецЕсли;
	
	Контекст.Файл.НачатьПолучениеРазмера(Новый ОписаниеОповещения(
		"СоздатьКлючИЗапросНаСертификатПослеПолученияРазмераФайла", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеПолученияРазмераФайла(Размер, Контекст) Экспорт
	
	Если Размер = 0 Тогда
		Контекст.ЗапросСоздан = Ложь;
		СоздатьКлючИЗапросНаСертификатЗавершение(Контекст);
		Возврат;
	КонецЕсли;
	
	ОповещениеПолучитьОткрытыйКлюч = Новый ОписаниеОповещения(
		"СоздатьКлючИЗапросНаСертификатПослеВызоваПолучитьОткрытыйКлюч", ЭтотОбъект, Контекст,
		"СоздатьКлючИЗапросНаСертификатПослеОшибкиВызоваПолучитьОткрытыйКлюч", ЭтотОбъект);
	
	Криптография.НачатьВызовПолучитьОткрытыйКлюч(ОповещениеПолучитьОткрытыйКлюч, Контекст.ИмяФайлаЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеВызоваПолучитьОткрытыйКлюч(Результат, ПараметрыВызова, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("РезультатПослеВызоваПолучитьОткрытыйКлюч", Результат);
	
	Криптография.НачатьПолучениеСписокОшибок(Новый ОписаниеОповещения(
		"СоздатьКлючИЗапросНаСертификатПослеВызоваПолучитьОткрытыйКлючПослеПолученияСписокОшибок", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеОшибкиВызоваПолучитьОткрытыйКлюч(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Криптография = Неопределено;
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьКлючИЗапросНаСертификатЗавершение(Контекст, ИнформацияОбОшибке,
		НСтр("ru = 'Не удалось получить открытую часть ключа по причине:'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеВызоваПолучитьОткрытыйКлючПослеПолученияСписокОшибок(СписокОшибок, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СписокОшибок) Тогда
		СоздатьКлючИЗапросНаСертификатПослеОшибкиВызоваПолучитьОткрытыйКлюч(СписокОшибок, Ложь, Контекст);
		Возврат;
	КонецЕсли;
	
	Результат = Контекст.РезультатПослеВызоваПолучитьОткрытыйКлюч;
	
	Контекст.Вставить("ТекущаяОткрытаяЧастьКлючаЭП", Результат);
	Контекст.Вставить("ТекущийИКС", ИдентификаторКлючаСубъектаHexСтрокой(Результат));
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.Интерактивно = Ложь;
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	ФайловаяСистемаКлиент.ЗагрузитьФайлы(Новый ОписаниеОповещения(
		"СоздатьКлючИЗапросНаСертификатПослеПомещенияФайлов", ЭтотОбъект, Контекст,
		"СоздатьКлючИЗапросНаСертификатПослеОшибкиПомещенияФайлов", ЭтотОбъект),
		ПараметрыЗагрузки, Контекст.ПомещаемыеФайлы);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеОшибкиПомещенияФайлов(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт
	
	СтандартнаяОбработка = Ложь;
	СоздатьКлючИЗапросНаСертификатЗавершение(Контекст, ИнформацияОбОшибке,
		НСтр("ru = 'Не удалось передать файл запроса на сертификат на сервер по причине:'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеПомещенияФайлов(ПомещенныеФайлы, Контекст) Экспорт
	
	Если Не ЗначениеЗаполнено(ПомещенныеФайлы) Тогда
		Контекст.ЗапросСоздан = Ложь;
		СоздатьКлючИЗапросНаСертификатЗавершение(Контекст);
		Возврат;
	КонецЕсли;
	
	Модифицированность         = Истина;
	АдресЗапросаНаСертификат   = ПомещенныеФайлы[0].Хранение;
	ОткрытаяЧастьКлючаЭП       = ПредставлениеДвоичныхДанных(Контекст.ТекущаяОткрытаяЧастьКлючаЭП);
	ИдентификаторКлючаСубъекта = ПредставлениеДвоичныхДанных(Контекст.ТекущийИКС);
	ДокументыПоляСертификата   = Контекст.ПоляСертификата;
	ДатаЗаявления = Формат(ОбщегоНазначенияКлиент.ДатаСеанса(), "ДФ=dd.MM.yyyy"); // АПК:335 Не локализуется (формат веб-сервиса).
	
	СоздатьКлючИЗапросНаСертификатЗавершение(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеОшибкиВызоваСоздатьЗапросНаСертификат(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Криптография = Неопределено;
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьКлючИЗапросНаСертификатЗавершение(Контекст, ИнформацияОбОшибке,
		НСтр("ru = 'Не удалось выполнить операцию по причине:'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатЗавершение(Контекст, ИнформацияОбОшибке = Неопределено, ЗаголовокОшибки = "")
	
	Если ИнформацияОбОшибке <> Неопределено Тогда
		Контекст.ЗапросСоздан = Ложь;
		Ошибка = Новый Структура;
		Ошибка.Вставить("ПоказатьИнструкцию", Истина);
		Ошибка.Вставить("ОписаниеОшибки", ЗаголовокОшибки
			+ Символы.ПС + ПредставлениеОшибкиДляПользователя(ИнформацияОбОшибке));
		
		ЭлектроннаяПодписьСлужебныйКлиент.ПоказатьОшибкуОбращенияКПрограмме(
			НСтр("ru = 'Создание запроса на сертификат'"), "", Ошибка,
			Новый Структура, Новый Структура("ПоказатьИнструкцию", Истина));
		
	ИначеЕсли Не Контекст.ЗапросСоздан Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Запрос на сертификат не создан.'"));
	КонецЕсли;
	
	Если Контекст.Свойство("Файл") Тогда
		Контекст.Файл.НачатьПроверкуСуществования(Новый ОписаниеОповещения(
			"СоздатьКлючИЗапросНаСертификатПослеПроверкиСуществованияФайлаДляУдаления", ЭтотОбъект, Контекст));
	Иначе
		СоздатьКлючИЗапросНаСертификатПослеУдаленияФайлов(Контекст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеПроверкиСуществованияФайлаДляУдаления(Существует, Контекст) Экспорт
	
	Если Существует Тогда
		
		ОповещениеПослеУдаленияФайлов = Новый ОписаниеОповещения(
			"СоздатьКлючИЗапросНаСертификатПослеУдаленияФайлов", ЭтотОбъект, Контекст,
			"СоздатьКлючИЗапросНаСертификатПослеОшибкиУдаленияФайлов", ЭтотОбъект);
		
		НачатьУдалениеФайлов(ОповещениеПослеУдаленияФайлов, Контекст.ИмяФайлаЗапроса);
		
	Иначе
		СоздатьКлючИЗапросНаСертификатПослеУдаленияФайлов(Контекст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеУдаленияФайлов(Контекст) Экспорт
	
	ВыполнитьОбработкуОповещения(Контекст.Оповещение, Контекст.ЗапросСоздан);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьКлючИЗапросНаСертификатПослеОшибкиУдаленияФайлов(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт
	
	СтандартнаяОбработка = Ложь;
	СоздатьКлючИЗапросНаСертификатЗавершение(Контекст, ИнформацияОбОшибке,
		НСтр("ru = 'Не удалось удалить файл запроса на сертификат на компьютере по причине:'"));
	
КонецПроцедуры

#КонецОбласти

#Область КонтейнерКлючей

&НаКлиенте
Процедура ПроверитьСуществованиеКонтейнераКлючей(Оповещение, ПутьПослеСоздания, ИмяДляСоздания)
	
	Контекст = Новый Структура;
	Контекст.Вставить("Оповещение",        Оповещение);
	Контекст.Вставить("ПутьПослеСоздания", ПутьПослеСоздания);
	Контекст.Вставить("ИмяДляСоздания",    ИмяДляСоздания);
	
	Если Не ЗначениеЗаполнено(Контекст.ПутьПослеСоздания) Тогда
		ВыполнитьОбработкуОповещения(Контекст.Оповещение, Ложь);
		Возврат;
	КонецЕсли;
	
	ПолучитьИменаКонтейнеровКлючей(Новый ОписаниеОповещения(
		"ПроверитьСуществованиеКонтейнераКлючейПослеПолученияИменКонтейнеров", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИменаКонтейнеровКлючей(Оповещение)
	
	Контекст = Новый Структура;
	Контекст.Вставить("Оповещение", Оповещение);
	Контекст.Вставить("СписокИмен", Новый СписокЗначений);
	Контекст.Вставить("ПервыйПроход", Истина);
	Контекст.Вставить("Индекс", -1);
	
	ПолучитьИменаКонтейнеровКлючейЦиклНачало(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИменаКонтейнеровКлючейЦиклНачало(Контекст)
	
	Контекст.Вставить("ТекущееИмя", "");
	
	ОповещениеПолучитьСледующийКонтейнер = Новый ОписаниеОповещения(
		"ПолучитьИменаКонтейнеровКлючейЦиклПослеВызоваПолучитьСледующийКонтейнерКлючей", ЭтотОбъект, Контекст,
		"ПолучитьИменаКонтейнеровКлючейЦиклПослеОшибкиВызоваПолучитьСледующийКонтейнерКлючей", ЭтотОбъект);
	
	Криптография.НачатьВызовПолучитьСледующийКонтейнерКлючей(ОповещениеПолучитьСледующийКонтейнер,
		Контекст.ТекущееИмя, Контекст.ПервыйПроход);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИменаКонтейнеровКлючейЦиклПослеВызоваПолучитьСледующийКонтейнерКлючей(Результат, ПараметрыВызова, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("РезультатПослеВызоваПолучитьСледующийКонтейнерКлючей",       Результат);
	Контекст.Вставить("ПараметрыВызоваПослеВызоваПолучитьСледующийКонтейнерКлючей", ПараметрыВызова);
	
	Криптография.НачатьПолучениеСписокОшибок(Новый ОписаниеОповещения(
		"ПолучитьИменаКонтейнеровКлючейЦиклПослеВызоваПолучитьСледующийКонтейнерКлючейПослеПолученияСписокОшибок", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИменаКонтейнеровКлючейЦиклПослеВызоваПолучитьСледующийКонтейнерКлючейПослеПолученияСписокОшибок(СписокОшибок, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СписокОшибок) Тогда
		ПолучитьИменаКонтейнеровКлючейЦиклПослеОшибкиВызоваПолучитьСледующийКонтейнерКлючей(СписокОшибок, Ложь, Контекст);
		Возврат;
	КонецЕсли;
	
	Результат       = Контекст.РезультатПослеВызоваПолучитьСледующийКонтейнерКлючей;
	ПараметрыВызова = Контекст.ПараметрыВызоваПослеВызоваПолучитьСледующийКонтейнерКлючей;
	
	Если Не Результат Тогда
		
		Если Контекст.ПервыйПроход Тогда
			Контекст.ПервыйПроход = Ложь;
			ПолучитьИменаКонтейнеровКлючейЦиклНачало(Контекст);
		Иначе
			
			Имена = "";
			Для каждого ЭлементСписка Из Контекст.СписокИмен Цикл
				
				УникальноеИмя = ЭлементСписка.Значение;
				Если УникальноеИмя = ЭлементСписка.Представление
					И СтрНайти(УникальноеИмя, "/") = 0
					И СтрНайти(УникальноеИмя, "\") = 0
					И СтрНайти(УникальноеИмя, ":") = 0 Тогда
					
					// ViPNet 3.2 (основная папка контейнеров не указана).
					УникальноеИмя = "\Infotecs\Containers\" + УникальноеИмя;
				КонецЕсли;
				
				Имена = Имена + Символы.ПС + УникальноеИмя;
				Имена = Имена + Символы.ПС + ЭлементСписка.Представление;
				Имена = Имена + Символы.ПС;
				
			КонецЦикла;
			
			ВыполнитьОбработкуОповещения(Контекст.Оповещение, СокрЛП(Имена));
			
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	ТекущееИмя = ПараметрыВызова[0];
	
	Если Контекст.ПервыйПроход Тогда
		Контекст.СписокИмен.Добавить(ТекущееИмя);
	Иначе
		Контекст.Индекс = Контекст.Индекс + 1;
		Контекст.СписокИмен[Контекст.Индекс].Представление = ТекущееИмя;
	КонецЕсли;
	
	ПолучитьИменаКонтейнеровКлючейЦиклНачало(Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИменаКонтейнеровКлючейЦиклПослеОшибкиВызоваПолучитьСледующийКонтейнерКлючей(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Криптография = Неопределено;
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьПредупреждение(,СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не удалось получить имя следующего контейнера ключей по причине:
		           |%1'"),
		ПредставлениеОшибкиДляПользователя(ИнформацияОбОшибке)));
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьСуществованиеКонтейнераКлючейПослеПолученияИменКонтейнеров(ИменаКонтейнеровКлючей, Контекст) Экспорт
	
	Строки = СписокПрограмм.НайтиСтроки(Новый Структура("Ссылка", Объект.Программа));
	
	Если СтрНачинаетсяС(Строки[0].Идентификатор, "CryptoPro") Тогда
		ВыполнитьОбработкуОповещения(Контекст.Оповещение, НайтиБезУчетаРегистраИРазделителяПути(
			ИменаКонтейнеровКлючей, Контекст.ИмяДляСоздания) > 0);
		Возврат;
	КонецЕсли;
	
	Позиция = СтрНайти(Контекст.ПутьПослеСоздания, ":.");
	Если Позиция = 0 Тогда
		ВыполнитьОбработкуОповещения(Контекст.Оповещение, НайтиБезУчетаРегистраИРазделителяПути(
			ИменаКонтейнеровКлючей, Контекст.ИмяДляСоздания) > 0);
		Возврат;
	КонецЕсли;
	
	ИмяПослеСоздания = Лев(Контекст.ПутьПослеСоздания, Позиция - 1);
	
	Если НайтиБезУчетаРегистраИРазделителяПути(ИменаКонтейнеровКлючей, ИмяПослеСоздания) > 0 Тогда
		ВыполнитьОбработкуОповещения(Контекст.Оповещение, Истина);
		Возврат;
	КонецЕсли;
	
	Позиция = НайтиБезУчетаРегистраИРазделителяПути(ИмяПослеСоздания, "\Infotecs\Containers\");
	Если Позиция = 0 Тогда
		ВыполнитьОбработкуОповещения(Контекст.Оповещение, Ложь);
		Возврат;
	КонецЕсли;
	
	// ViPNet 3.2 (основная папка контейнеров не указана).
	ИмяПослеСоздания = Сред(ИмяПослеСоздания, Позиция);
	
	ВыполнитьОбработкуОповещения(Контекст.Оповещение, НайтиБезУчетаРегистраИРазделителяПути(
		ИменаКонтейнеровКлючей, ИмяПослеСоздания) > 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИмяНовогоКонтейнераКлюча(Оповещение)
	
	// Дата используется только для уникальности имени, поэтому требуется часы компьютера.
	Дата = ТекущаяДата(); // Не заменять на текущую дату сеанса.
	
	ДатаСоздания = Формат(Дата, "ДФ=гггг-ММ-дд") + " " + Формат(Дата, "ДФ=ЧЧ") + "-" + Формат(Дата, "ДФ=мм");
	
	Если ЭтоИндивидуальныйПредприниматель Тогда
		// С<Фамилия Имя>, Индивидуальный предприниматель".
		Субъект = ПодготовитьСтрокуДляИмениКонтейнера(Фамилия) + " "
			+ ПодготовитьСтрокуДляИмениКонтейнера(Имя) + ", "
			+ ПодготовитьСтрокуДляИмениКонтейнера(НСтр("ru = 'Индивидуальный предприниматель'"));
	Иначе
		// С<Фамилия Имя>, <Сокращенное наименование организации>".
		Субъект = ПодготовитьСтрокуДляИмениКонтейнера(Фамилия) + " "
			+ ПодготовитьСтрокуДляИмениКонтейнера(Имя) + ", "
			+ ПодготовитьСтрокуДляИмениКонтейнера(НаименованиеСокращенное);
	КонецЕсли;
	
	Контекст = Новый Структура;
	Контекст.Вставить("Оповещение", Оповещение);
	Контекст.Вставить("НовоеИмя1",  ДатаСоздания + ", " + Лев(Субъект, 60));
	Контекст.Вставить("НовоеИмя2",  ДатаСоздания + "-" + Формат(Дата, "ДФ=сс") + ", " + Лев(Субъект, 57));
	
	ПолучитьИменаКонтейнеровКлючей(Новый ОписаниеОповещения(
		"ПолучитьИмяНовогоКонтейнераКлючаПослеПолученияИменКонтейнеров", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИмяНовогоКонтейнераКлючаПослеПолученияИменКонтейнеров(ИменаКонтейнеровКлючей, Контекст) Экспорт
	
	НовоеИмя = Контекст.НовоеИмя1;
	
	Если НайтиБезУчетаРегистраИРазделителяПути(ИменаКонтейнеровКлючей, НовоеИмя) > 0 Тогда
		НовоеИмя = Контекст.НовоеИмя2;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.Оповещение, НовоеИмя);
	
КонецПроцедуры

&НаКлиенте
Функция ПодготовитьСтрокуДляИмениКонтейнера(Строка, ЗаменаПробела = Неопределено)
	
	ЗаменаСимволов = Новый Соответствие;
	ЗаменаСимволов.Вставить("\", " ");
	ЗаменаСимволов.Вставить("/", " ");
	ЗаменаСимволов.Вставить("*", " ");
	ЗаменаСимволов.Вставить("<", " ");
	ЗаменаСимволов.Вставить(">", " ");
	ЗаменаСимволов.Вставить("|", " ");
	ЗаменаСимволов.Вставить(":", "");
	ЗаменаСимволов.Вставить("""", "");
	ЗаменаСимволов.Вставить("?", "");
	ЗаменаСимволов.Вставить(Символы.ВК, "");
	ЗаменаСимволов.Вставить(Символы.ПС, " ");
	ЗаменаСимволов.Вставить(Символы.Таб, " ");
	ЗаменаСимволов.Вставить(Символы.НПП, " ");
	// Замена символов кавычек.
	ЗаменаСимволов.Вставить(Символ(171), "");
	ЗаменаСимволов.Вставить(Символ(187), "");
	ЗаменаСимволов.Вставить(Символ(8195), "");
	ЗаменаСимволов.Вставить(Символ(8194), "");
	ЗаменаСимволов.Вставить(Символ(8216), "");
	ЗаменаСимволов.Вставить(Символ(8218), "");
	ЗаменаСимволов.Вставить(Символ(8217), "");
	ЗаменаСимволов.Вставить(Символ(8220), "");
	ЗаменаСимволов.Вставить(Символ(8222), "");
	ЗаменаСимволов.Вставить(Символ(8221), "");
	
	СтрокаПодготовленная = "";
	
	СтрокаЛатиницей = СтроковыеФункцииКлиент.СтрокаЛатиницей(Строка);
	КоличествоСимволов = СтрДлина(СтрокаЛатиницей);
	
	Для НомерСимвола = 1 По КоличествоСимволов Цикл
		Символ = Сред(СтрокаЛатиницей, НомерСимвола, 1);
		Если ЗаменаСимволов[Символ] <> Неопределено Тогда
			Символ = ЗаменаСимволов[Символ];
		КонецЕсли;
		СтрокаПодготовленная = СтрокаПодготовленная + Символ;
	КонецЦикла;
	
	Если ЗаменаПробела <> Неопределено Тогда
		СтрокаПодготовленная = СтрЗаменить(ЗаменаПробела, " ", ЗаменаПробела);
	КонецЕсли;
	
	Возврат СокрЛП(СтрокаПодготовленная);
	
КонецФункции

#КонецОбласти

#Область ОбъектКриптографии

&НаКлиенте
Процедура СоздатьОбъектКриптографии(ОбработкаПродолжения, НазначениеРасширенияДляРаботыСФайлами, НазначениеВнешнейКомпонентыКриптографии)
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОбработкаПродолжения", ОбработкаПродолжения);
	Контекст.Вставить("НазначениеВнешнейКомпонентыКриптографии", НазначениеВнешнейКомпонентыКриптографии);
	
	ФайловаяСистемаКлиент.ПодключитьРасширениеДляРаботыСФайлами(
		Новый ОписаниеОповещения("СоздатьОбъектКриптографииПослеПодключенияРасширенияРаботыСФайлами", ЭтотОбъект, Контекст),
		НазначениеРасширенияДляРаботыСФайлами, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОбъектКриптографииПослеПодключенияРасширенияРаботыСФайлами(Результат, Контекст) Экспорт
	
	Если Результат <> Истина Тогда
		ВыполнитьОбработкуОповещения(Контекст.ОбработкаПродолжения, Ложь);
		Возврат;
	КонецЕсли;
	
	ПараметрыПодключения = ОбщегоНазначенияКлиент.ПараметрыПодключенияКомпоненты();
	ПараметрыПодключения.ТекстПояснения = Контекст.НазначениеВнешнейКомпонентыКриптографии;
	// Объект создается на одно действие до исключения, кэширование на время сеанса не подходит.
	ПараметрыПодключения.Кэшировать = Ложь;
	ПараметрыПодключения.ИдентификаторыСозданияОбъектов.Добавить("BinaryDataS");
	ПараметрыПодключения.ИдентификаторыСозданияОбъектов.Добавить("CryptS");
	
	ОбщегоНазначенияКлиент.ПодключитьКомпонентуИзМакета(
		Новый ОписаниеОповещения("СоздатьОбъектКриптографииПослеПодключенияКомпоненты", ЭтотОбъект, Контекст),
		Неопределено, "Обработка.ЗаявлениеНаВыпускНовогоКвалифицированногоСертификата.Макет.КомпонентаОбмена",
		ПараметрыПодключения);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОбъектКриптографииПослеПодключенияКомпоненты(Результат, Контекст) Экспорт
	
	Если Результат.Подключено Тогда
		РаботаСДвоичнымиДанными = Результат.ПодключаемыйМодуль.Получить("BinaryDataS");
		Криптография = Результат.ПодключаемыйМодуль.Получить("CryptS");
	Иначе
		
		Криптография = Неопределено;
		РаботаСДвоичнымиДанными = Неопределено;
		
		Если ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
			// Пользователь отказался от установки внешней компоненты.
			ВыполнитьОбработкуОповещения(Контекст.ОбработкаПродолжения, Ложь);
		Иначе
			ПоказатьПредупреждение(Новый ОписаниеОповещения("СоздатьОбъектКриптографииПослеПредупрежденияОбОшибке",
				ЭтотОбъект, Контекст),Результат.ОписаниеОшибки);
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Криптография.НачатьУстановкуНеВыводитьСообщенияОбОшибках(
		Новый ОписаниеОповещения("СоздатьОбъектКриптографииПослеУстановкиНеВыводитьСообщенияОбОшибках",
		ЭтотОбъект, Контекст), Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОбъектКриптографииПослеУстановкиНеВыводитьСообщенияОбОшибках(Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	ПерсональныеНастройки = ЭлектроннаяПодписьКлиент.ПерсональныеНастройки();
	ПутиКПрограммам = ПерсональныеНастройки.ПутиКПрограммамЭлектроннойПодписиИШифрования;
	
	Строки = СписокПрограмм.НайтиСтроки(Новый Структура("Ссылка", Объект.Программа));
	ИмяПрограммы = Строки[0].ИмяПрограммы;
	ТипПрограммы = Строки[0].ТипПрограммы;
	ПутьКПрограмме = Строка(ПутиКПрограммам.Получить(Объект.Программа));
	
	ОповещениеПослеВызоваСоздать = Новый ОписаниеОповещения("СоздатьОбъектКриптографииПослеВызоваСоздатьМенеджераКриптографии",
		ЭтотОбъект, Контекст, "СоздатьОбъектКриптографииПослеОшибкиВызоваСоздатьМенеджераКриптографии", ЭтотОбъект);
	
	Криптография.НачатьВызовСоздатьМенеджераКриптографии(
		ОповещениеПослеВызоваСоздать, ИмяПрограммы, ПутьКПрограмме, ТипПрограммы);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОбъектКриптографииПослеОшибкиВызоваСоздатьМенеджераКриптографии(ИнформацияОбОшибке, СтандартнаяОбработка, Контекст) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Криптография = Неопределено;
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьПредупреждение(
		Новый ОписаниеОповещения("СоздатьОбъектКриптографииПослеПредупрежденияОбОшибке", ЭтотОбъект, Контекст),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Программа электронной подписи не доступна через внешнюю компоненту по причине:
			           |%1'"),
			ПредставлениеОшибкиДляПользователя(ИнформацияОбОшибке)));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОбъектКриптографииПослеВызоваСоздатьМенеджераКриптографии(Результат, ПараметрыВызова, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Криптография.НачатьПолучениеСписокОшибок(Новый ОписаниеОповещения(
		"СоздатьОбъектКриптографииПослеВызоваСоздатьМенеджераКриптографииПослеПолученияСписокОшибок", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОбъектКриптографииПослеВызоваСоздатьМенеджераКриптографииПослеПолученияСписокОшибок(СписокОшибок, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СписокОшибок) Тогда
		СоздатьОбъектКриптографииПослеОшибкиВызоваСоздатьМенеджераКриптографии(СписокОшибок, Ложь, Контекст);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.ОбработкаПродолжения, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОбъектКриптографииПослеПредупрежденияОбОшибке(Контекст) Экспорт
	
	ВыполнитьОбработкуОповещения(Контекст.ОбработкаПродолжения, Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область УстановкаСертификата

&НаКлиенте
Процедура УстановитьСертификат(Оповещение)
	
	Если СписокПрограмм.Количество() = 0 Тогда
		ЗаполнитьСписокПрограмм();
	КонецЕсли;
	
	ДатаУстановкиСертификата = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	Контекст = Новый Структура("Оповещение", Оповещение);
	ОповещениеПослеСозданияМенеджера = Новый ОписаниеОповещения(
		"УстановитьСертификатПослеСозданияМенеджераКриптографии", ЭтотОбъект, Контекст);
	
	ЭлектроннаяПодписьСлужебныйКлиент.СоздатьМенеджерКриптографии(ОповещениеПослеСозданияМенеджера,
		"", Неопределено, , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеСозданияМенеджераКриптографии(Менеджер, Контекст) Экспорт
	
	Если ТипЗнч(Менеджер) <> Тип("МенеджерКриптографии") Тогда
		
		ЭлектроннаяПодписьСлужебныйКлиент.ПоказатьОшибкуОбращенияКПрограмме(
			НСтр("ru = 'Установка сертификата на компьютер'"), "", Менеджер,
			Новый Структура, Новый Структура("ПоказатьИнструкцию", Истина));
		
		ОшибкаУстановкиСертификата = Менеджер.ОписаниеОшибки;
		ВыполнитьОбработкуОповещения(Контекст.Оповещение, Ложь);
		
		Возврат;
		
	КонецЕсли;
	
	Контекст.Вставить("МенеджерКриптографии", Менеджер);
	
	Если Криптография <> Неопределено Тогда
		УстановитьСертификатПослеСозданияОбъектаКриптографии(Истина, Контекст);
	Иначе
		СоздатьОбъектКриптографии(Новый ОписаниеОповещения(
			"УстановитьСертификатПослеСозданияОбъектаКриптографии", ЭтотОбъект, Контекст),
			НСтр("ru = 'Для установки сертификата на компьютер требуется
			|установить расширение для веб-клиента 1С:Предприятия.'"),
			НСтр("ru = 'Для установки сертификата на компьютер требуется
			|установить внешнюю компоненту.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеСозданияОбъектаКриптографии(Результат, Контекст) Экспорт
	
	Если Результат <> Истина Тогда
		
		ВыполнитьОбработкуОповещения(Контекст.Оповещение, Ложь);
		ФайловаяСистемаКлиент.ПодключитьРасширениеДляРаботыСФайлами(Новый ОписаниеОповещения(
			"УстановитьСертификатПриОшибкеСозданияОбъектаКриптографииПослеПодключенияРасширенияРаботыСФайлами", ЭтотОбъект));
		
	Иначе
		УстановитьСертификатПослеУспехаСозданияОбъектаКриптографии(Контекст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПриОшибкеСозданияОбъектаКриптографииПослеПодключенияРасширенияРаботыСФайлами(Подключено, Контекст) Экспорт
	
	Если Не Подключено Тогда
		ОшибкаУстановкиСертификата = НСтр("ru = 'Не установлено расширение для работы с файлами.'");
	Иначе
		НачатьПодключениеРасширенияРаботыСКриптографией(Новый ОписаниеОповещения(
			"УстановитьСертификатПриОшибкеСозданияОбъектаКриптографииПослеПодключенияРасширенияРаботыСКриптографией", ЭтотОбъект));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПриОшибкеСозданияОбъектаКриптографииПослеПодключенияРасширенияРаботыСКриптографией(Подключено, Контекст) Экспорт
	
	Если Не Подключено Тогда
		ОшибкаУстановкиСертификата = НСтр("ru = 'Не установлено расширение для работы с криптографией.'");
	Иначе
		ОшибкаУстановкиСертификата = НСтр("ru = 'Не установлена внешняя компонента.'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеУспехаСозданияОбъектаКриптографии(Контекст)
	
	ПолучитьКаталогВременныхФайловКомпоненты(Новый ОписаниеОповещения(
		"УстановитьСертификатПослеПолученияКаталогаВременныхФайловКомпоненты", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеПолученияКаталогаВременныхФайловКомпоненты(Каталог, Контекст) Экспорт
	
	Контекст.Вставить("ВременныйКаталог", Каталог + НовыйСжатыйУникальныйИдентификатор());
	
	Контекст.ВременныйКаталог = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(
		Контекст.ВременныйКаталог);
	
	Контекст.Вставить("ИмяФайлаСертификата",          "my.cer");
	Контекст.Вставить("ИмяФайлаКорневогоСертификата", "root.cer");
	
	Контекст.Вставить("ПолучаемыеФайлы", Новый Массив);
	Контекст.ПолучаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(
		Контекст.ИмяФайлаСертификата, АдресСертификата));
	
	Контекст.ПолучаемыеФайлы.Добавить(Новый ОписаниеПередаваемогоФайла(
		Контекст.ИмяФайлаКорневогоСертификата, АдресКорневогоСертификата));
	
	Вызовы = Новый Массив;
	ДобавитьВызов(Вызовы, "НачатьПолучениеФайлов", Контекст.ПолучаемыеФайлы, Контекст.ВременныйКаталог, Ложь);
	ДобавитьВызов(Вызовы, "НачатьУдалениеФайлов",  Контекст.ВременныйКаталог, Неопределено);
	
	НачатьЗапросРазрешенияПользователя(Новый ОписаниеОповещения(
		"УстановитьСертификатПослеПолученияРазрешений", ЭтотОбъект, Контекст), Вызовы);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеПолученияРазрешений(РазрешенияПолучены, Контекст) Экспорт
	
	Если Не РазрешенияПолучены Тогда
		
		ОшибкаУстановкиСертификата = НСтр("ru = 'Сохранение сертификатов во временную папку отменено пользователем.'");
		ВыполнитьОбработкуОповещения(Контекст.Оповещение, Ложь);
		Возврат;
		
	КонецЕсли;
	
	НачатьСозданиеКаталога(Новый ОписаниеОповещения(
		"УстановитьСертификатПослеСозданияКаталога", ЭтотОбъект, Контекст), Контекст.ВременныйКаталог);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеСозданияКаталога(РазрешенияПолучены, Контекст) Экспорт
	
	ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайлов();
	ПараметрыСохранения.Диалог.Каталог = Контекст.ВременныйКаталог;
	ПараметрыСохранения.Интерактивно = Ложь;
	ФайловаяСистемаКлиент.СохранитьФайлы(
		Новый ОписаниеОповещения("УстановитьСертификатПослеПолученияФайлов", ЭтотОбъект, Контекст),
		Контекст.ПолучаемыеФайлы, ПараметрыСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеПолученияФайлов(ПолученныеФайлы, Контекст) Экспорт
	
	Если ПолученныеФайлы = Неопределено
		Или ПолученныеФайлы.Количество() <> 2 Тогда
		
		ОшибкаУстановкиСертификата = НСтр("ru = 'Сертификаты не были сохранены во временную папку.'");
		УстановитьСертификатУдалитьВременныйКаталогИЗавершить(Ложь, Контекст);
		Возврат;
		
	КонецЕсли;
	
	ПроверитьСуществованиеКонтейнераКлючей(Новый ОписаниеОповещения(
		"УстановитьСертификатПослеПроверкиСуществованияКонтейнераКлючей", ЭтотОбъект, Контекст),
		КонтейнерКлючаПуть, КонтейнерКлючаИмя);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеПроверкиСуществованияКонтейнераКлючей(Существует, Контекст) Экспорт
	
	Если Не Существует Тогда
		
		ОшибкаУстановкиСертификата = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось найти контейнер ключа на компьютере.
			|Имя контейнера: ""%1"".
			|Путь к контейнеру: ""%2"".'"),
			КонтейнерКлючаИмя,
			КонтейнерКлючаПуть);
		
		Контекст.Вставить("ОшибкаПоискаКонтейнера", ОшибкаУстановкиСертификата);
		
	Иначе
		ОшибкаУстановкиСертификата = "";
	КонецЕсли;
	
	АктивноеОкно().Активизировать();
	
	Криптография.НачатьВызовУстановитьСертификатВКонтейнерИХранилище(Новый ОписаниеОповещения(
		"УстановитьСертификатПослеВызоваУстановитьСертификатВКонтейнерИХранилище", ЭтотОбъект, Контекст,
		"УстановитьСертификатПослеОшибкиВызоваУстановитьСертификатВКонтейнерИХранилище", ЭтотОбъект),
		Контекст.ВременныйКаталог + Контекст.ИмяФайлаСертификата, КонтейнерКлючаПуть);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеОшибкиВызоваУстановитьСертификатВКонтейнерИХранилище(
	ИнформацияОбОшибке,
	СтандартнаяОбработка,
	Контекст) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Криптография = Неопределено;
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	ОшибкаУстановкиСертификата = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не удалось установить личный сертификат в контейнер ключей и хранилище ОС по причине:
		|%1'"),
		ПредставлениеОшибкиДляПользователя(ИнформацияОбОшибке));
	
	Если Контекст.Свойство("ОшибкаПоискаКонтейнера") Тогда
		ОшибкаУстановкиСертификата = ОшибкаУстановкиСертификата
			+ Символы.ПС + Символы.ПС + Контекст.ОшибкаПоискаКонтейнера;
	КонецЕсли;
	
	Контекст.Вставить("РезультатПослеВызоваУстановитьСертификатВКонтейнерИХранилище", Неопределено);
	УстановитьСертификатПослеВызоваУстановитьСертификатВКонтейнерИХранилищеПослеПолученияСписокОшибок(Неопределено, Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеВызоваУстановитьСертификатВКонтейнерИХранилище(Результат, ПараметрыВызова, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("РезультатПослеВызоваУстановитьСертификатВКонтейнерИХранилище", Результат);
	
	Криптография.НачатьПолучениеСписокОшибок(Новый ОписаниеОповещения(
		"УстановитьСертификатПослеВызоваУстановитьСертификатВКонтейнерИХранилищеПослеПолученияСписокОшибок", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеВызоваУстановитьСертификатВКонтейнерИХранилищеПослеПолученияСписокОшибок(СписокОшибок, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СписокОшибок) Тогда
		УстановитьСертификатПослеОшибкиВызоваУстановитьСертификатВКонтейнерИХранилище(СписокОшибок, Ложь, Контекст);
		Возврат;
	КонецЕсли;
	
	Результат = Контекст.РезультатПослеВызоваУстановитьСертификатВКонтейнерИХранилище;
	
	АктивноеОкно().Активизировать();
	
	Если Результат <> Истина И Не ЗначениеЗаполнено(ОшибкаУстановкиСертификата) Тогда
		ОшибкаУстановкиСертификата = НСтр("ru = 'Не удалось установить личный сертификат в контейнер ключей и хранилище ОС.'");
	КонецЕсли;
	
	ЭлектроннаяПодписьСлужебныйКлиент.ПолучитьСертификатПоОтпечатку(Новый ОписаниеОповещения(
		"УстановитьСертификатПослеПоискаСертификата", ЭтотОбъект, Контекст),
		Объект.Отпечаток,
		ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты,
		Неопределено,
		Контекст.МенеджерКриптографии);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеПоискаСертификата(Результат, Контекст) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("СертификатКриптографии") Тогда
		
		Если Результат.Свойство("СертификатНеНайден") Тогда
			ОшибкаПоиска =
				НСтр("ru = 'Не удалось найти личный сертификат, установленный на компьютер.'");
		Иначе
			
			ОшибкаПоиска = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось найти личный сертификат, установленный на компьютер по причине:
				           |%1'"),
				Результат.ОписаниеОшибки);
				
		КонецЕсли;
		
		ОшибкаУстановкиСертификата = СокрЛ(ОшибкаУстановкиСертификата + Символы.ПС + Символы.ПС) + ОшибкаПоиска;
		УстановитьСертификатУдалитьВременныйКаталогИЗавершить(Ложь, Контекст);
		
		Возврат;
		
	КонецЕсли;
	
	ОшибкаУстановкиСертификата = "";
	Контекст.Вставить("СертификатКриптографии", Результат);
	
	АктивноеОкно().Активизировать();
	
	Если Криптография <> Неопределено Тогда
		УстановитьСертификатПослеПроверкиНаличияСертификат(Истина, Контекст);
	Иначе
		
		СоздатьОбъектКриптографии(Новый ОписаниеОповещения(
			"УстановитьСертификатПослеПроверкиНаличияСертификат", ЭтотОбъект, Контекст),
			НСтр("ru = 'Для установки сертификата на компьютер требуется
			|установить расширение для веб-клиента 1С:Предприятия.'"),
			НСтр("ru = 'Для установки сертификата на компьютер требуется
			|установить внешнюю компоненту.'"));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеПроверкиНаличияСертификат(Результат, Контекст) Экспорт
	
	Если Результат <> Истина Тогда
		УстановитьСертификатПослеВызоваИмпортироватьСертификатПослеПолученияСписокОшибок(Неопределено, Контекст);
		Возврат;
	КонецЕсли;
	
	Криптография.НачатьВызовИмпортироватьСертификат(Новый ОписаниеОповещения(
		"УстановитьСертификатПослеВызоваИмпортироватьСертификат", ЭтотОбъект, Контекст,
		"УстановитьСертификатПослеОшибкиВызоваИмпортироватьСертификат", ЭтотОбъект),
		Контекст.ВременныйКаталог + Контекст.ИмяФайлаКорневогоСертификата, "ROOT");
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеОшибкиВызоваИмпортироватьСертификат(
	ИнформацияОбОшибке,
	СтандартнаяОбработка,
	Контекст) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Криптография = Неопределено;
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	ОшибкаУстановкиСертификата = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не удалось установить корневой сертификат в хранилище ОС по причине:
		|%1'"),
		ПредставлениеОшибкиДляПользователя(ИнформацияОбОшибке));
	
	Контекст.Вставить("РезультатПослеВызоваИмпортироватьСертификат", Неопределено);
	УстановитьСертификатПослеВызоваИмпортироватьСертификатПослеПолученияСписокОшибок(Неопределено, Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеВызоваИмпортироватьСертификат(Результат, ПараметрыВызова, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("РезультатПослеВызоваИмпортироватьСертификат", Результат);
	Криптография.НачатьПолучениеСписокОшибок(Новый ОписаниеОповещения(
		"УстановитьСертификатПослеВызоваИмпортироватьСертификатПослеПолученияСписокОшибок", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеВызоваИмпортироватьСертификатПослеПолученияСписокОшибок(СписокОшибок, Контекст) Экспорт
	
	Если Не ВозможноПродолжениеПослеОперацииКомпоненты() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СписокОшибок) Тогда
		УстановитьСертификатПослеОшибкиВызоваИмпортироватьСертификат(СписокОшибок, Ложь, Контекст);
		Возврат;
	КонецЕсли;
	
	Результат = Контекст.РезультатПослеВызоваИмпортироватьСертификат;
	
	АктивноеОкно().Активизировать();
	
	Если Результат <> Истина И Не ЗначениеЗаполнено(ОшибкаУстановкиСертификата) Тогда
		ОшибкаУстановкиСертификата = НСтр("ru = 'Не удалось установить корневой сертификат в хранилище ОС.'");
	КонецЕсли;
	
	АктивноеОкно().Активизировать();
	
	ЭлектроннаяПодписьСлужебныйКлиент.ПолучитьСертификатПоОтпечатку(Новый ОписаниеОповещения(
		"УстановитьСертификатПослеПоискаКорневогоСертификата", ЭтотОбъект, Контекст),
		ОтпечатокКорневогоСертификата,
		ТипХранилищаСертификатовКриптографии.КорневыеСертификаты,
		Неопределено,
		Контекст.МенеджерКриптографии);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеПоискаКорневогоСертификата(Результат, Контекст) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("СертификатКриптографии") Тогда
		
		Если Результат.Свойство("СертификатНеНайден") Тогда
			ОшибкаПоиска =
				НСтр("ru = 'Не удалось найти корневой сертификат, установленный на компьютер.'");
		Иначе
			
			ОшибкаПоиска = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось найти корневой сертификат, установленный на компьютер по причине:
				|%1'"),
				Результат.ОписаниеОшибки);
			
		КонецЕсли;
		
		ОшибкаУстановкиСертификата = СокрЛ(ОшибкаУстановкиСертификата + Символы.ПС + Символы.ПС) + ОшибкаПоиска;
		УстановитьСертификатУдалитьВременныйКаталогИЗавершить(Ложь, Контекст);
		
		Возврат;
		
	КонецЕсли;
	
	ОшибкаУстановкиСертификата = "";
	УстановитьСертификатУдалитьВременныйКаталогИЗавершить(
		Не ЗначениеЗаполнено(ОшибкаУстановкиСертификата), Контекст);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатУдалитьВременныйКаталогИЗавершить(Результат, Контекст)
	
	Контекст.Вставить("Результат", Результат);
	НачатьУдалениеФайлов(Новый ОписаниеОповещения(
		"УстановитьСертификатПослеУдаленияВременногоКаталога", ЭтотОбъект, Контекст),
		Контекст.ВременныйКаталог);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатПослеУдаленияВременногоКаталога(Контекст) Экспорт
	
	ВыполнитьОбработкуОповещения(Контекст.Оповещение, Контекст.Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеУстановкиСертификата(Установлен, Контекст) Экспорт
	
	ПослеУстановкиСертификатаНаСервере(Установлен);
	Если Установлен Тогда
		ПослеЗаписи();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеУстановкиСертификатаНаСервере(Установлен)
	
	Если Установлен Тогда
		СостояниеОбработкиЗаявления = НСтр("ru = 'Заявление исполнено.'");
		Объект.СостояниеЗаявления = Перечисления.СостоянияЗаявленияНаВыпускСертификата.Исполнено;
		ЗаписатьЗаявление();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПредставлениеДанных

&НаКлиенте
Функция ПредставлениеОшибкиДляПользователя(ИнформацияОбОшибке)
	
	Если ТипЗнч(ИнформацияОбОшибке) = Тип("ИнформацияОбОшибке") Тогда
		Возврат КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	Иначе
		Возврат Строка(ИнформацияОбОшибке);
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяРегионаРФПоРекомендациямДляСКПЭП(КодРегиона)
	
	// Методические рекомендации по составу квалифицированного сертификата ключа
	// проверки электронной подписи (Версия 1.4).
	//
	// Приложение 2. Формат названия субъекта федерации.
	// Из раздела "Справочник кодов регионов".
	
	ИменаВсехРегионовРФ = Обработки.ЗаявлениеНаВыпускНовогоКвалифицированногоСертификата.ПолучитьМакет(
		"ИменаРегионовРФ");
	
	Строка = ИменаВсехРегионовРФ.ПолучитьСтроку(Число(КодРегиона));
	Если Не СтрНачинаетсяС(Строка, КодРегиона + " ") Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Регион РФ с кодом ""%1"" не существует.'"), КодРегиона);
	КонецЕсли;
	
	Строка = СокрЛП(Сред(Строка, 4));
	Если Не ЗначениеЗаполнено(Строка) Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для региона РФ с кодом ""%1"" имя, рекомендованное для СКПЭП, еще не назначено.'"), КодРегиона);
	КонецЕсли;
	
	Возврат Строка;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеЧастиАдреса(АдресСтруктура, СписокПолей)
	
	Представление = "";
	СтруктураПолей = Новый Структура(СписокПолей);
	ЗаполнитьЗначенияСвойств(СтруктураПолей, АдресСтруктура);
	
	МодульУправлениеКонтактнойИнформацией = ОбщегоНазначения.ОбщийМодуль("УправлениеКонтактнойИнформацией");
	Представление = МодульУправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(СтруктураПолей);
	
	Возврат Представление;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТолькоЦифры(Строка)
	
	ДлинаСтроки = СтрДлина(Строка);
	
	ОбработаннаяСтрока = "";
	Для НомерСимвола = 1 По ДлинаСтроки Цикл
		
		Символ = Сред(Строка, НомерСимвола, 1);
		Если Символ >= "0" И Символ <= "9" Тогда
			ОбработаннаяСтрока = ОбработаннаяСтрока + Символ;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ОбработаннаяСтрока;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НаселенныйПунктПолностью(АдресСтруктура)
	
	// Округ ОкругСокращение, Район РайонСокращение, Город ГородСокращение,
	// ВнутригородскойРайон ВнутригородскойРайонСокращение, НаселенныйПункт НаселенныйПунктСокращение.
	
	СписокПолей = "";
	Если АдресСтруктура.Свойство("Регион")
		И АдресСтруктура.Свойство("КодРегиона")
		И (АдресСтруктура.КодРегиона = "77"
		Или АдресСтруктура.КодРегиона = "78"
		Или АдресСтруктура.КодРегиона = "92"
		Или АдресСтруктура.КодРегиона = "99") Тогда
		
		СписокПолей = "Регион, КодРегиона, ";
	КонецЕсли;
	
	Возврат ПредставлениеЧастиАдреса(АдресСтруктура, СписокПолей +
		"Округ,
		|Район,
		|Город,
		|ВнутригородскойРайон,
		|НаселенныйПункт,
		|Территория");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеДвоичныхДанных(Знач Строка, БайтВСтроке = 20)
	
	ОстатокСтроки = НРег(СтрЗаменить(Строка, " ", ""));
	Представление = "";
	БайтВТекущейСтроке = 0;
	
	Пока ЗначениеЗаполнено(ОстатокСтроки) Цикл
		Представление = Представление + Лев(ОстатокСтроки, 2) + " ";
		ОстатокСтроки = Сред(ОстатокСтроки, 3);
		БайтВТекущейСтроке = БайтВТекущейСтроке + 1;
		Если БайтВТекущейСтроке = БайтВСтроке Тогда
			Представление = СокрЛП(Представление) + Символы.ПС;
			БайтВТекущейСтроке = 0;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СокрЛП(Представление);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеРеквизитаНомерПаспортаРФ(НомерПаспортаРФ)
	
	Возврат Лев(НомерПаспортаРФ, 2) + " " + Сред(НомерПаспортаРФ, 3, 2) + " " + Прав(НомерПаспортаРФ, 6);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеРеквизитаДокументКодПодразделения(КодПодразделения)
	
	Если Не ЗначениеЗаполнено(КодПодразделения) Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат Лев(КодПодразделения, 3) + "-" + Сред(КодПодразделения, 4);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеРеквизитаДокументДатаВыдачи(ДокументДатаВыдачи)
	
	Возврат Формат(ДокументДатаВыдачи, "ДФ=dd.MM.yyyy"); // АПК:335 Не локализуется (формат веб-сервиса).
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеВидаДокумента(ВидДокумента)
	
	Возврат ?(ВидДокумента = "21",
		НСтр("ru = 'паспорт гражданина Российской Федерации'"),
		НСтр("ru = 'иной документ, предусмотренный законодательством Российской Федерации'"));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеРеквизитаСтраховойНомерПФР(СтраховойНомерПФР)
	
	Возврат Сред(СтраховойНомерПФР, 1, 3) + "-" + Сред(СтраховойНомерПФР,  4, 3)
	+ "-" + Сред(СтраховойНомерПФР, 7, 3) + " " + Сред(СтраховойНомерПФР, 10, 2);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеРеквизитаДатаРождения(ДатаРождения)
	
	Возврат Формат(ДатаРождения, "ДФ=dd.MM.yyyy"); // АПК:335 Не локализуется (формат веб-сервиса).
	
КонецФункции

#КонецОбласти

#Область ОблачныйСервисПодписи

&НаКлиенте
Процедура СоздатьЗапросВОблачныйСервис()
	
	ИдентификаторЗаявленияВОблачныйСервис = Новый УникальныйИдентификатор;
	
	ЗапросНаСертификат = Неопределено;
	ТекстЗапросаНаСертификат = ОписаниеЗапросаНаКвалифицированныйСертификат(ЗапросНаСертификат);
	
	МодульМенеджерСервисаКриптографииКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("МенеджерСервисаКриптографииКлиент");
	МодульМенеджерСервисаКриптографииКлиент.СоздатьКонтейнерИЗапросНаСертификат(
		Новый ОписаниеОповещения("СоздатьЗапросВОблачныйСервисПослеОтправкиЗапроса", ЭтотОбъект, ЗапросНаСертификат),
		ИдентификаторЗаявленияВОблачныйСервис, ТекстЗапросаНаСертификат);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьЗапросВОблачныйСервисПослеОтправкиЗапроса(ОтветСервиса, ЗапросНаСертификат) Экспорт
	
	Доступность = Истина;
	Если Не ОтветСервиса.Выполнено Тогда
		
		ИдентификаторЗаявленияВОблачныйСервис = Неопределено;
		
		ЗаголовокОшибки = НСтр("ru = 'Создание ключа электронной подписи и запроса на сертификат'");
		ПоказатьОшибкуВзаимодействияСОблачнымСервисом(ЗаголовокОшибки, ОтветСервиса.ОписаниеОшибки);
		Возврат;
		
	КонецЕсли;
	
	Модифицированность         = Истина;
	АдресЗапросаНаСертификат   = АдресЗапросаНаСертификат(ОтветСервиса.ЗапросСертификата, УникальныйИдентификатор);
	ОткрытаяЧастьКлючаЭП       = ПредставлениеДвоичныхДанных(Строка(ОтветСервиса.ОткрытыйКлюч));
	ИдентификаторКлючаСубъекта = ПредставлениеДвоичныхДанных(ИдентификаторКлючаСубъектаHexСтрокой(ОткрытаяЧастьКлючаЭП));
	ДокументыПоляСертификата   = ЗапросНаСертификат;
	ДатаЗаявления              = Формат(ОбщегоНазначенияКлиент.ДатаСеанса(), "ДФ=dd.MM.yyyy"); // АПК:335 Не локализуется (формат веб-сервиса).
	
	Программы = СписокПрограмм.НайтиСтроки(Новый Структура("Ссылка", Объект.Программа));
	Программы[0].ИмяПрограммы = ОтветСервиса.ИмяПровайдера;
	Программы[0].ТипПрограммы = ОтветСервиса.ТипПровайдера;
	
	ПечатьЗаявленияПослеСозданияКлючаИЗапросаНаСертификат(Истина, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатОблачногоСервиса()
	
	МодульМенеджерСервисаКриптографииКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("МенеджерСервисаКриптографииКлиент");
	МодульМенеджерСервисаКриптографииКлиент.УстановитьСертификатВКонтейнерИХранилище(
		Новый ОписаниеОповещения("УстановитьСертификатОблачногоСервисаПослеУстановки", ЭтотОбъект),
		ИдентификаторЗаявленияВОблачныйСервис, ПолучитьИзВременногоХранилища(АдресСертификата));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСертификатОблачногоСервисаПослеУстановки(ОтветСервиса, ДополнительныеПараметры) Экспорт
	
	Если Не ОтветСервиса.Выполнено Тогда
		
		ЗаголовокОшибки = НСтр("ru = 'Установка сертификата в контейнер ключа и хранилище сертификатов'");
		ПоказатьОшибкуВзаимодействияСОблачнымСервисом(ЗаголовокОшибки, ОтветСервиса.ОписаниеОшибки);
		Возврат;
		
	КонецЕсли;
	
	ПослеУстановкиСертификата(ОтветСервиса.Выполнено, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОшибкуВзаимодействияСОблачнымСервисом(ЗаголовокОшибки, ОписаниеОшибки)
	
	Ошибка = Новый Структура;
	Ошибка.Вставить("ПоказатьИнструкцию", Истина);
	Ошибка.Вставить("ОписаниеОшибки", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Не удалось выполнить операцию в облачном сервисе по причине:
		|
		|%1'"), ОписаниеОшибки));
	
	ЭлектроннаяПодписьСлужебныйКлиент.ПоказатьОшибкуОбращенияКПрограмме(
		ЗаголовокОшибки, "", Ошибка,Новый Структура, Новый Структура("ПоказатьИнструкцию", Истина));
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция АдресЗапросаНаСертификат(ЗапросСертификата, УникальныйИдентификатор)
	
	ФайлЗапросаНаСертификат = ПолучитьИмяВременногоФайла(".p10");
	
	ТекстЗапросаНаСертификат = "-----BEGIN NEW CERTIFICATE REQUEST-----" + Символы.ПС
		+ СтрЗаменить(Base64Строка(ЗапросСертификата), Символы.ВК, "") + Символы.ПС
		+ "-----END NEW CERTIFICATE REQUEST-----" + Символы.ПС;
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ТекстЗапросаНаСертификат);
	ТекстовыйДокумент.Записать(ФайлЗапросаНаСертификат);
	
	ДанныеЗапросаНаСертификат = Новый ДвоичныеДанные(ФайлЗапросаНаСертификат);
	УдалитьФайлы(ФайлЗапросаНаСертификат);
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеЗапросаНаСертификат, УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ВыгрузитьСертификатЗавершение(Результат, Контекст) Экспорт
	
	ЭлектроннаяПодписьСлужебныйКлиент.СохранитьСертификат(Неопределено,
		АдресКорневогоСертификата, НСтр("ru = 'Корневой сертификат ООО НПЦ 1С'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКаталогВременныхФайловКомпоненты(Оповещение)
	
	Контекст = Новый Структура("Оповещение", Оповещение);
	
	РаботаСДвоичнымиДанными.НачатьВызовПолучитьКаталогВременныхФайлов(Новый ОписаниеОповещения(
			"ПолучитьКаталогВременныхФайловКомпонентыПослеВызова", ЭтотОбъект, Контекст));
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКаталогВременныхФайловКомпонентыПослеВызова(Каталог, ПараметрыВызова, Контекст) Экспорт
	
	ВыполнитьОбработкуОповещения(Контекст.Оповещение,
		ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(Каталог));
	
КонецПроцедуры

&НаКлиенте
Функция ИмяФайлаБезРасширения()
	
	Если ЭтоИндивидуальныйПредприниматель Тогда
		ИмяФайлаБезРасширения = СокрП(Фамилия + " " + Имя + " " + Отчество);
	Иначе
		ИмяФайлаБезРасширения = СокрП(Фамилия + " " + Имя + " " + Отчество) + ", " + НаименованиеСокращенное
			+ ?(ЗначениеЗаполнено(Подразделение), ", " + Подразделение, "") + ", " + Должность;
	КонецЕсли;
	
	Возврат ИмяФайлаБезРасширения;
	
КонецФункции

&НаКлиенте
Функция ВозможноПродолжениеПослеОперацииКомпоненты()
	
	Если Открыта() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Криптография = Неопределено;
	
	ПоказатьПредупреждение(,
		НСтр("ru = 'Заявление на выпуск сертификата было закрыто, операция отменена.'"));
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Функция СимволРазделенияЧастейИмениИлиЧастейФамилииИлиЧастейОтчестваВСертификате()
	
	Возврат "_";
	
КонецФункции

&НаСервере
Процедура ЗаполнитьЗаголовкиРеквизитов()
	
	Реквизиты = ПолучитьРеквизиты();
	Для каждого Реквизит Из Реквизиты Цикл
		ЗаголовкиРеквизитов.Добавить(Реквизит.Имя, Реквизит.Заголовок);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИдентификаторКлючаСубъектаHexСтрокой(ОткрытаяЧастьКлючаHexСтрокой)
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.SHA1);
	ХешированиеДанных.Добавить(ПолучитьДвоичныеДанныеИзHexСтроки(ОткрытаяЧастьКлючаHexСтрокой));
	
	Возврат ПолучитьHexСтрокуИзДвоичныхДанных(ХешированиеДанных.ХешСумма);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПрограммаОблачногоСервиса()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрограммыЭлектроннойПодписиИШифрования.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ПрограммыЭлектроннойПодписиИШифрования КАК ПрограммыЭлектроннойПодписиИШифрования
	|ГДЕ
	|	ПрограммыЭлектроннойПодписиИШифрования.ЭтоПрограммаОблачногоСервиса";
	ПрограммыОблачногоСервиса = Запрос.Выполнить().Выбрать();
	
	Если ПрограммыОблачногоСервиса.Следующий() Тогда
		Возврат ПрограммыОблачногоСервиса.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НайтиБезУчетаРегистраИРазделителяПути(Строка, ПодстрокаПоиска)
	
	Возврат СтрНайти(НРег(СтрЗаменить(Строка, "/", "\")), НРег(СтрЗаменить(ПодстрокаПоиска, "/", "\")));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьГиперссылку(Строка, Подстрока, Гиперссылка)
	
	Позиция = СтрНайти(Строка, Подстрока);
	Если Позиция = 0 Тогда
		Возврат Новый ФорматированнаяСтрока(Строка);
	КонецЕсли;
	
	СтрокаСоСсылкой = Новый ФорматированнаяСтрока(
		Подстрока,,,, Гиперссылка);
	
	Возврат Новый ФорматированнаяСтрока(Лев(Строка, Позиция-1),
		СтрокаСоСсылкой, Сред(Строка, Позиция + СтрДлина(Подстрока)));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НовыйСжатыйУникальныйИдентификатор()
	
	Возврат НРег(СтрЗаменить(Строка(Новый УникальныйИдентификатор), "-", ""));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьВызов(Вызовы, Метод, П1, П2, П3 = Неопределено, П4 = Неопределено, П5 = Неопределено)
	
	Вызов = Новый Массив;
	Вызов.Добавить(Метод);
	Вызов.Добавить(П1);
	Вызов.Добавить(П2);
	Вызов.Добавить(П3);
	Вызов.Добавить(П4);
	Вызов.Добавить(П5);
	
	Вызовы.Добавить(Вызов);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти