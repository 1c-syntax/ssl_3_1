///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ИмяУстройстваСканирования, ТекстОшибки, ПодробноеПредставлениеОшибки");
	Заголовок = Параметры.Заголовок;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьТекстОшибки();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьНастройки" Тогда
		СтандартнаяОбработка = Ложь;
		РаботаСФайламиКлиент.ОткрытьФормуНастройкиСканирования();
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ТехническаяИнформация" Тогда
		СтандартнаяОбработка = Ложь;
		ПолучитьТехническуюИнформацию();
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "Запустить32БитныйКлиент" Тогда
		СтандартнаяОбработка = Ложь;
		Запустить32БитныйКлиент();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПовторитьСканирование(Команда)
	Закрыть("ПовторитьСканирование");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьТекстОшибки()
	Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
		Если Не ПоказыватьДиалогСканера И Не ОбщегоНазначенияКлиент.ЭтоLinuxКлиент() Тогда
			ТекстОшибкиПриИспользованииНастроекИзПриложения = Символы.ПС + " • "
				+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru ='Укажите в <a href = ""%1"">настройках сканирования</a> способ задания 
							|   настроек - ""Расширенные в диалоге сканера"".'"), "ОткрытьНастройки");
		Иначе
			ТекстОшибкиПриИспользованииНастроекИзПриложения = "";
		КонецЕсли;
		
		ТекстОшибки = СтроковыеФункцииКлиент.ФорматированнаяСтрока(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Сканер ""%1"" не обнаружен или не подключен.
						|Попробуйте выполнить одно из следующих действий:
						| • Проверьте подключение сканера и повторите попытку сканирования.
						| • Укажите в <a href = ""%2"">настройках сканирования</a> доступный сканер.%4
						| • Если проблема повторится, обратитесь в службу поддержки фирмы ""1С"", 
						|   предоставив <a href = ""%3"">техническую информацию о возникшей проблеме</a>.'"), 
			ИмяУстройстваСканирования, "ОткрытьНастройки", "ТехническаяИнформация", ТекстОшибкиПриИспользованииНастроекИзПриложения));
  	КонецЕсли;
  	Элементы.ТекстОшибки.Заголовок = ТекстОшибки;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьТехническуюИнформацию()
	РаботаСФайламиСлужебныйКлиент.ПолучитьТехническуюИнформацию(ПодробноеПредставлениеОшибки);
КонецПроцедуры

&НаКлиенте
Процедура Запустить32БитныйКлиент()
#Если Не ВебКлиент Тогда
	КаталогПрограммы32 = СтрЗаменить(КаталогПрограммы(), "\Program Files\", "\Program Files (x86)\");
	ИмяПриложения = КаталогПрограммы32 + "1cv8.exe";
	ФайлПриложения = Новый Файл(ИмяПриложения);
	Если ФайлПриложения.Существует() Тогда 
		ФайловаяСистемаКлиент.ЗапуститьПрограмму(ИмяПриложения);
	Иначе
		СистемнаяИнформация = Новый СистемнаяИнформация();
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://releases.1c.ru/version_files?nick=Platform83&ver=" + СистемнаяИнформация.ВерсияПриложения);
	КонецЕсли;
#КонецЕсли
КонецПроцедуры

#КонецОбласти
