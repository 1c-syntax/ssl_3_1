///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция РаботаСВнешнимиРесурсамиДоступна() Экспорт
	
	Возврат Не РегламентныеЗаданияСервер.РаботаСВнешнимиРесурсамиЗаблокирована();
	
КонецФункции

Функция АдресАрхиваТехническойИнформации(Знач ПараметрыОбращения) Экспорт
	
	ВременныйКаталог = ФайловаяСистема.СоздатьВременныйКаталог();
	АдресаФайлов = АдресаФайловТехническойИнформации(ПараметрыОбращения);
	
	ДобавитьФайлТехническойИнформацииВоВременныйКаталог(
		АдресаФайлов.ТехнологическаяИнформация,
		НСтр("ru = 'Технологическая информация.txt'"),
		ВременныйКаталог);
	
	ДобавитьФайлТехническойИнформацииВоВременныйКаталог(
		АдресаФайлов.ЖурналРегистрации,
		НСтр("ru = 'Журнал регистрации.xml'"),
		ВременныйКаталог);
	
	Для Каждого ДополнительныйФайл Из ПараметрыОбращения.ДополнительныеФайлы Цикл
		ДобавитьФайлТехническойИнформацииВоВременныйКаталог(
			ДополнительныйФайл.АдресФайла,
			ДополнительныйФайл.ПолноеИмяФайла,
			ВременныйКаталог);
	КонецЦикла;
	
	АрхивТехническойИнформации = Новый ЗаписьZipФайла();
	АрхивТехническойИнформации.Добавить(ВременныйКаталог);
	
	ДанныеАрхива = АрхивТехническойИнформации.ПолучитьДвоичныеДанные();
	Результат = ПоместитьВоВременноеХранилище(ДанныеАрхива, Новый УникальныйИдентификатор);
	
	ФайловаяСистема.УдалитьВременныйКаталог(ВременныйКаталог);
	
	Возврат Результат;
	
КонецФункции

Функция АдресаФайловТехническойИнформации(Знач ПараметрыОбращения) Экспорт
	
	Результат = Новый Структура("ТехнологическаяИнформация, ЖурналРегистрации");
	Результат.ТехнологическаяИнформация = АдресТехнологическаяИнформация(ПараметрыОбращения.ТехнологическаяИнформация);
	Результат.ЖурналРегистрации = АдресЖурналаРегистрации(ПараметрыОбращения.ОтборЖурналаРегистрации);
	
	Возврат Результат;
	
КонецФункции

Функция АдресТехнологическаяИнформация(ТехнологическаяИнформация)
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("txt");
	
	ТекстИнформацииДляПоддержки = Новый ТекстовыйДокумент;
	ТекстИнформацииДляПоддержки.УстановитьТекст(ТехнологическаяИнформация);
	ТекстИнформацииДляПоддержки.Записать(ИмяВременногоФайла);
	
	Результат = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяВременногоФайла), Новый УникальныйИдентификатор);
	
	УдалитьФайлы(ИмяВременногоФайла);
	
	Возврат Результат;
	
КонецФункции

Функция АдресЖурналаРегистрации(ОтборЖурналаРегистрации)
	
	СекундДоНачалаСобытий = 600;
	
	Если ОтборЖурналаРегистрации.Свойство("ДатаНачала") Тогда // АПК:1415 Динамический состав свойств.
		ДатаНачала = ОтборЖурналаРегистрации.ДатаНачала - СекундДоНачалаСобытий;
	Иначе
		ДатаНачала = ТекущаяДатаСеанса() - СекундДоНачалаСобытий;
	КонецЕсли;
	
	ОтборЖурналаРегистрации.Вставить("ДатаНачала", ДатаНачала);
	ОтборЖурналаРегистрации.Вставить("Пользователь", ПользователиИнформационнойБазы.ТекущийПользователь());
	
	КоличествоСобытий = 100;
	
	УстановитьОтключениеБезопасногоРежима(Истина);
	УстановитьПривилегированныйРежим(Истина);
	Результат = ЖурналРегистрации.ЖурналДляТехподдержки(ОтборЖурналаРегистрации, КоличествоСобытий);
	УстановитьПривилегированныйРежим(Ложь);
	УстановитьОтключениеБезопасногоРежима(Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция АдресСнимкаЭкрана(Знач СнимокЭкрана) Экспорт
	
	Возврат ПоместитьВоВременноеХранилище(СнимокЭкрана.ПолучитьДвоичныеДанные(), Новый УникальныйИдентификатор);
	
КонецФункции

Процедура ДобавитьФайлТехническойИнформацииВоВременныйКаталог(АдресФайла, ИмяФайла, ВременныйКаталог)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
	ДвоичныеДанные.Записать(ВременныйКаталог + ИмяФайла);
	
	УдалитьИзВременногоХранилища(АдресФайла);
	
КонецПроцедуры

#КонецОбласти
