///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОткрытьФормуМакетовВыгрузки(Знач МассивСсылок, Знач ПараметрыВыполнения) Экспорт
	
	Форма = ПараметрыВыполнения.Форма;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("МакетыВыгрузки", Истина);
	ДополнительныеПараметры = ПараметрыВыполнения.ОписаниеКоманды.ДополнительныеПараметры;
	ПараметрыФормы.Вставить("Владелец", ДополнительныеПараметры.Владелец);
	
	ОткрытьФорму("РегистрСведений.ПользовательскиеМакетыПечати.Форма.МакетыПечатныхФорм",
		ПараметрыФормы,
		Форма);
	
КонецПроцедуры

// Запускает процедуру открытия формы выгрузки
//  с предварительной установкой расширение для работы с 1С:Предприятием в веб-клиенте
//  см. ФайловаяСистемаКлиент.ПодключитьРасширениеДляРаботыСФайлами
//  
// Параметры:
//  МассивСсылок - Массив из ЛюбаяСсылка
//  ПараметрыВыполнения - см. ПодключаемыеКомандыКлиент.ПараметрыВыполненияКоманды 
// 
Процедура ОбработчикКомандыВыполнитьВыгрузку(Знач МассивСсылок, Знач ПараметрыВыполнения) Экспорт
	
	ПараметрыВыполнения.Вставить("ОбъектыПечати", МассивСсылок);
	ПараметрыВыполнения.Вставить("ФормироватьФайлВыгрузки", Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(
		ПараметрыВыполнения.ОписаниеКоманды, 
		ПараметрыВыполнения.ОписаниеКоманды.ДополнительныеПараметры, 
		Истина); 
	
	ДопПараметр = Новый Структура;
	ДопПараметр.Вставить("ПараметрыВыполнения", ПараметрыВыполнения);
	Оповещение = Новый ОписаниеОповещения("ПриПодключенииРасширенияДляВыгрузки", ЭтотОбъект, ДопПараметр);
	ФайловаяСистемаКлиент.ПодключитьРасширениеДляРаботы1СПредприятия(Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Открывает общую форму СохранениеПоФорматуВыгрузки
// 
// Параметры:
//  РасширениеПодключено - Булево
//  ДополнительныеПараметры - структура :
// * ПараметрыВыполнения - см. ПодключаемыеКомандыКлиент.ПараметрыВыполненияКоманды
//
Процедура ПриПодключенииРасширенияДляВыгрузки(РасширениеПодключено, ДополнительныеПараметры) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РасширениеДляРаботыСФайламиПодключено", РасширениеПодключено);
	
	ОбъектыПечати = ДополнительныеПараметры.ПараметрыВыполнения.ОбъектыПечати;
	ОписаниеКомандыВыполнения = Новый Структура;
	ОписаниеКомандыВыполнения.Вставить("ОписаниеКоманды", ДополнительныеПараметры.ПараметрыВыполнения.ОписаниеКоманды);
	ОписаниеКомандыВыполнения.Вставить("ОбъектыПечати", ОбъектыПечати);
	ПараметрыФормы.Вставить("ОписаниеКомандыВыполнения", ОписаниеКомандыВыполнения);
	
	ОткрытьФорму(
		"ОбщаяФорма.СохранениеПоФорматуВыгрузки",
		ПараметрыФормы,
		ДополнительныеПараметры.ПараметрыВыполнения.Форма);

КонецПроцедуры

#КонецОбласти
