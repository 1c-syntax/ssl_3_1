///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Разделяет строку с полным именем физического лица на составляющие (фамилия и имя).
// Если в модуле локализации ФизическиеЛицаКлиентСерверЛокализация реализован специализированный алгоритм разбиения ФИО 
// с учетом национальных особенностей (процедура ПриОпределенииЧастейПолногоИмени), возвращается результат его выполнения. 
// В этом случае состав полей возвращаемой структуры определяется указанной локализованной процедурой.
// При отсутствии такого алгоритма либо результата его выполнения применяется стандартный алгоритм разбора ФИО.
//
// Параметры:
//  ПолноеИмя   - Строка - Строка, содержащая ФИО, которую необходимо разобрать.
//  ФорматИмени - Строка - (необязательный) Задает порядок следования частей имени во входной строке. 
//                          В локализованной реализации могут поддерживаться дополнительные значения (форматы) этого параметра.
//                Возможные значения для стандартного алгоритма:
//                 # "" (пустая строка) или "Имя,Фамилия" - имя в начале, фамилия в конце (по умолчанию);
//                 # "Фамилия,Имя" - фамилия в начале, имя в конце.
//
// Возвращаемое значение:
//  Структура - При отсутствии локализованного алгоритма содержит:
//   * Фамилия - Строка - Выделенная фамилия.
//   * Имя     - Строка - Оставшаяся часть (имя и отчество, если есть).
//  см. ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииЧастейПолногоИмени.Результат
//
// Пример:
//   1. ЧастиИмени("Иван Иванович Иванов") 
//      вернет структуру: Фамилия = "Иванов", Имя = "Иван Иванович".
//   2. ЧастиИмени("Иванов Иван Иванович", "Фамилия,Имя") 
//      вернет структуру: Фамилия = "Иванов", Имя = "Иван Иванович".
//   3. ЧастиИмени("Джон Смит") 
//      вернет структуру: Фамилия = "Смит", Имя = "Джон".
//   4. ЧастиИмени("Джон") 
//      вернет структуру: Фамилия = "", Имя = "Джон".
//
Функция ЧастиИмени(ПолноеИмя, Знач ФорматИмени = "") Экспорт
	
	Результат = Новый Структура;
	
	ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииЧастейПолногоИмени(ПолноеИмя, ФорматИмени, Результат);
	Если ЗначениеЗаполнено(Результат) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Фамилия", "");
	Результат.Вставить("Имя", "");

	Если Не ЗначениеЗаполнено(ПолноеИмя) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ИмяВНачале = ФорматИмени = "Имя,Фамилия" Или ФорматИмени = "";
	ЧастиИмени = СтрРазделить(ПолноеИмя, " ", Ложь); 
	
	Если ИмяВНачале Тогда
		Если ЧастиИмени.Количество() > 1 Тогда
			Результат.Фамилия = ЧастиИмени[ЧастиИмени.ВГраница()];
			ЧастиИмени.Удалить(ЧастиИмени.ВГраница());
		КонецЕсли;
	
		Результат.Имя = СтрСоединить(ЧастиИмени, " ");
	Иначе
		Если ЧастиИмени.Количество() >= 1 Тогда
			Результат.Фамилия = ЧастиИмени[0];
			ЧастиИмени.Удалить(0);
		КонецЕсли;
	
		Результат.Имя = СтрСоединить(ЧастиИмени, " ");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует краткое представление полного имени физического лица (фамилия и инициалы).
// Если в процедуре ПриОпределенииФамилииИнициалов модуля локализации ФизическиеЛицаКлиентСерверЛокализация
// реализован собственный алгоритм формирования фамилии и инициалов с учетом национальных особенностей, 
// то возвращается результат, сформированный этим алгоритмом.
// В этом случае формат результата, а также правила его формирования определяются локализованной процедурой.
// При отсутствии локализованного алгоритма либо результата его выполнения применяется стандартное формирование.
//
// Параметры:
//  ПолноеИмя          - Строка, Структура - Полное имя физического лица.
//                       Если передана строка, она разбирается на части функцией ЧастиИмени().
//                       Если передана структура, предполагается наличие полей "Фамилия" и "Имя".
//  ФорматПолногоИмени - Строка - (необязательный) Определяет порядок следования частей имени во входных данных.
//                       В локализованной реализации могут поддерживаться дополнительные значения (форматы)
//                       этого параметра.
//                       Возможные значения для стандартного алгоритма:
//                        * "" (пустая строка) - "Фамилия,Имя" (по умолчанию);
//                        * "Имя,Фамилия"      - имя в начале, фамилия в конце.
//  ИнициалыВНачале    - Булево - (необязательный) Используется локализованным алгоритмом для выбора вида
//                       представления (например, "Фамилия И. О." или "И. О. Фамилия").
//                       При стандартном алгоритме порядок определяется параметром ФорматПолногоИмени.
//
// Возвращаемое значение:
//  Строка - Краткое представление имени.
//           Если результат сформирован локализованной процедурой, его формат определяется этой процедурой
//           и может отличаться от стандартного.
//           При отсутствии локализованного алгоритма возвращается:
//            * "Фамилия" - если имя отсутствует;
//            * "Фамилия И." или "И. Фамилия" - если имя заполнено.
//  см. ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииФамилииИнициалов.Результат
//
// Пример:
//  1. ФамилияИнициалы("Иванов Василий") -> "Иванов В."
//  2. ФамилияИнициалы("Василий Иванов", "Имя,Фамилия") -> "В. Иванов"
//  3. ФамилияИнициалы("Иванов") -> "Иванов"
//
Функция ФамилияИнициалы(Знач ПолноеИмя, Знач ФорматПолногоИмени = "", Знач ИнициалыВНачале = Ложь) Экспорт
	
	Результат = "";
	
	ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииФамилииИнициалов(ПолноеИмя, ФорматПолногоИмени, ИнициалыВНачале, Результат);
	Если ЗначениеЗаполнено(Результат) Или Не ЗначениеЗаполнено(ПолноеИмя) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ТипЗнч(ПолноеИмя) = Тип("Строка") Тогда
		ПолноеИмя = ЧастиИмени(ПолноеИмя);
	КонецЕсли;
	
	Фамилия = ПолноеИмя.Фамилия;
	Имя = ПолноеИмя.Имя;
	
	Если ПустаяСтрока(Имя) Тогда
		Возврат Фамилия;
	КонецЕсли;
	
	Если ФорматПолногоИмени = "Имя,Фамилия" Тогда
		Шаблон = "%2. %1";
	Иначе
		Шаблон = "%1 %2.";
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Фамилия, Лев(Имя, 1));
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать СтроковыеФункцииКлиентСервер.ТолькоСимволыНациональногоАлфавитаВСтроке
// 
// Проверяет, верно ли написано ФИО физического лица.
// ФИО считается верным, если содержит только символы национального алфавита, либо только латиницу.
//
// Параметры:
//  ФИО - Строка - фамилия, имя и отчество. Например, "Пупкин Василий Иванович".
//  ТолькоНациональныеСимволы - Булево - при проверке ФИО должна включать только символы национального алфавита.
//
// Возвращаемое значение:
//  Булево - Истина, если ФИО написано верно.
//
Функция ФИОНаписаноВерно(Знач ФИО, ТолькоНациональныеСимволы = Ложь) Экспорт
	
	РезультатПроверки = Истина;
	ФизическиеЛицаКлиентСерверЛокализация.ФИОНаписаноВерно(ФИО, ТолькоНациональныеСимволы, РезультатПроверки);
	
	Возврат РезультатПроверки;
	
КонецФункции

#КонецОбласти

#КонецОбласти
