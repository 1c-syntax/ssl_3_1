///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Раскладывает полное имя физического лица на составные части.
//
// Параметры:
//  ПолноеИмя - см. ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииЧастейПолногоИмени.ПолноеИмя
//  ФорматИмени - см. ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииЧастейПолногоИмени.ФорматИмени
//	
// Возвращаемое значение:
//   см. ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииЧастейПолногоИмени.Результат
//
Функция ЧастиИмени(ПолноеИмя, Знач ФорматИмени = "") Экспорт
	
	Результат = Новый Структура;
	
	ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииЧастейПолногоИмени(ПолноеИмя, ФорматИмени, Результат);
	Если ЗначениеЗаполнено(Результат) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Фамилия", "");
	Результат.Вставить("Имя", "");

	Если Не ЗначениеЗаполнено(ПолноеИмя) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ИмяВНачале = ФорматИмени = "Имя,Фамилия" Или ФорматИмени = "";
	ЧастиИмени = СтрРазделить(ПолноеИмя, " ", Ложь); 
	
	Если ИмяВНачале Тогда
		Если ЧастиИмени.Количество() > 1 Тогда
			Результат.Фамилия = ЧастиИмени[ЧастиИмени.ВГраница()];
			ЧастиИмени.Удалить(ЧастиИмени.ВГраница());
		КонецЕсли;
	
		Результат.Имя = СтрСоединить(ЧастиИмени, " ");
	Иначе
		Если ЧастиИмени.Количество() >= 1 Тогда
			Результат.Фамилия = ЧастиИмени[0];
			ЧастиИмени.Удалить(0);
		КонецЕсли;
	
		Результат.Имя = СтрСоединить(ЧастиИмени, " ");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует краткое представление из полного имени физического лица.
//
// Параметры:
//  ПолноеИмя - см. ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииФамилииИнициалов.ПолноеИмя
//  ФорматПолногоИмени - см. ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииФамилииИнициалов.ФорматПолногоИмени
//  ИнициалыВНачале - см. ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииФамилииИнициалов.ИнициалыВНачале
//
// Возвращаемое значение:
//   см. ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииФамилииИнициалов.Результат
//
Функция ФамилияИнициалы(Знач ПолноеИмя, Знач ФорматПолногоИмени = "", Знач ИнициалыВНачале = Ложь) Экспорт
	
	Результат = "";
	
	ФизическиеЛицаКлиентСерверЛокализация.ПриОпределенииФамилииИнициалов(ПолноеИмя, ФорматПолногоИмени, ИнициалыВНачале, Результат);
	Если ЗначениеЗаполнено(Результат) Или Не ЗначениеЗаполнено(ПолноеИмя) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если ТипЗнч(ПолноеИмя) = Тип("Строка") Тогда
		ПолноеИмя = ЧастиИмени(ПолноеИмя);
	КонецЕсли;
	
	Фамилия = ПолноеИмя.Фамилия;
	Имя = ПолноеИмя.Имя;
	
	Если ПустаяСтрока(Имя) Тогда
		Возврат Фамилия;
	КонецЕсли;
	
	Если ФорматПолногоИмени = "Имя,Фамилия" Тогда
		Шаблон = "%2. %1";
	Иначе
		Шаблон = "%1 %2.";
	КонецЕсли;
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон, Фамилия, Лев(Имя, 1));
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Устарела. Следует использовать СтроковыеФункцииКлиентСервер.ТолькоСимволыНациональногоАлфавитаВСтроке
// 
// Проверяет, верно ли написано ФИО физического лица.
// ФИО считается верным, если содержит только символы национального алфавита, либо только латиницу.
//
// Параметры:
//  ФИО - Строка - фамилия, имя и отчество. Например, "Пупкин Василий Иванович".
//  ТолькоНациональныеСимволы - Булево - при проверке ФИО должна включать только символы национального алфавита.
//
// Возвращаемое значение:
//  Булево - Истина, если ФИО написано верно.
//
Функция ФИОНаписаноВерно(Знач ФИО, ТолькоНациональныеСимволы = Ложь) Экспорт
	
	РезультатПроверки = Истина;
	ФизическиеЛицаКлиентСерверЛокализация.ФИОНаписаноВерно(ФИО, ТолькоНациональныеСимволы, РезультатПроверки);
	
	Возврат РезультатПроверки;
	
КонецФункции

#КонецОбласти

#КонецОбласти
