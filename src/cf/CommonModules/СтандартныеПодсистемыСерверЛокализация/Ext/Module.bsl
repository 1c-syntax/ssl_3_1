///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Позволяет добавить новые или поменять форматы сохранения табличного документа,
// в зависимости от национальной специфики. Например, указать те форматы, в которых
// принимают документы в местных контролирующих органах.  
// 
// Параметры: 
//  ТаблицаФорматов - см. СтандартныеПодсистемыСервер.НастройкиФорматовСохраненияТабличногоДокумента
//
Процедура ПриНастройкеФорматовСохраненияТабличногоДокумента(ТаблицаФорматов) Экспорт
	
	// Локализация
	// PDF (.pdf) (устаревший формат для ФНС)
	НовыйФормат = ТаблицаФорматов.Добавить();
	НовыйФормат.ТипФайлаТабличногоДокумента = ТипФайлаТабличногоДокумента.PDF;
	НовыйФормат.Ссылка = Перечисления.ФорматыСохраненияОтчетов.PDF;
	НовыйФормат.Расширение = "PDF";
	НовыйФормат.Картинка = БиблиотекаКартинок.ФорматPDF;
	НовыйФормат.Представление = НСтр("ru = 'Документ PDF (для ФНС)'");
	// Конец Локализация
	
КонецПроцедуры

// Процедура доопределяет минимальную и рекомендуемую версии платформы "1С:Предприятие" для работы конфигурации,
// заданные в ОбщегоНазначенияПереопределяемый.ПриОпределенииОбщихПараметровБазовойФункциональности.
// Может использоваться для установки минимальной и рекомендуемой версий платформы по данным внешнего сервиса.
// 
// Параметры:
//  МинимальнаяВерсияПлатформы   - см. ОбщегоНазначенияПереопределяемый.ПриОпределенииОбщихПараметровБазовойФункциональности.ОбщиеПараметры.МинимальнаяВерсияПлатформы
//  РекомендуемаяВерсияПлатформы - см. ОбщегоНазначенияПереопределяемый.ПриОпределенииОбщихПараметровБазовойФункциональности.ОбщиеПараметры.РекомендуемаяВерсияПлатформы
//
Процедура ПриОпределенииМинимальнойИРекомендуемойВерсийПлатформы(МинимальнаяВерсияПлатформы, РекомендуемаяВерсияПлатформы) Экспорт
	
	// Локализация

	Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеОбновленийПрограммы") Тогда
		Возврат;
	КонецЕсли;

	МодульИнтернетПоддержкаПользователейКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователейКлиентСервер");
	ВерсияБИП = МодульИнтернетПоддержкаПользователейКлиентСервер.ВерсияБиблиотеки();
	
	Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияБИП, "2.7.1.35") <= 0 Тогда
		Возврат;
	КонецЕсли;
		
	Попытка
		МодульПолучениеОбновленийПрограммы = ОбщегоНазначения.ОбщийМодуль("ПолучениеОбновленийПрограммы");
		ИнформацияОВерсияхПлатформы        = МодульПолучениеОбновленийПрограммы.ИнформацияОВерсияхПлатформы();
		
		// Контроль заполнения версий платформы по данным сервиса
		Если ПустаяСтрока(ИнформацияОВерсияхПлатформы.МинимальнаяВерсияПлатформы)
			И ПустаяСтрока(ИнформацияОВерсияхПлатформы.РекомендуемаяВерсияПлатформы) Тогда
			Возврат;
		КонецЕсли;
				
		МинимальнаяВерсияПлатформы = ИнформацияОВерсияхПлатформы.МинимальнаяВерсияПлатформы;
		РекомендуемаяВерсияПлатформы = ИнформацияОВерсияхПлатформы.РекомендуемаяВерсияПлатформы;
	Исключение
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось изменить минимальную и рекомендуемую версию платформы ""1С:Предприятие"", полученную с сервиса обновления приложений, по причине:
				|%1'",
				ОбщегоНазначения.КодОсновногоЯзыка()), ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Базовая функциональность'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Предупреждение,,,
			ТекстСообщения);
	КонецПопытки;
	
	// Конец Локализация
	
КонецПроцедуры

// Вызывается при неверном заполнении минимальной и рекомендуемой версий платформы
// в процедуре ПриОпределенииМинимальнойИРекомендуемойВерсийПлатформы.
//
// Параметры:
//  УказаннаяМинимальная - Строка
//  УказаннаяРекомендуемая - Строка
//  ТребуемаяБСПМинимальная - Строка
//  ОбщиеПараметры - см. ОбщегоНазначения.ОбщиеПараметрыБазовойФункциональности
//
Процедура ПриНеверномЗаполненииМинимальнойИРекомендуемойВерсийПлатформы(УказаннаяМинимальная, УказаннаяРекомендуемая,
	ТребуемаяБСПМинимальная, ОбщиеПараметры) Экспорт
	
	// Локализация

	Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеОбновленийПрограммы") Тогда
		Возврат;
	КонецЕсли;

	МодульИнтернетПоддержкаПользователейКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователейКлиентСервер");
	ВерсияБИП = МодульИнтернетПоддержкаПользователейКлиентСервер.ВерсияБиблиотеки();
	
	Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияБИП, "2.7.1.35") <= 0 Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		МодульПолучениеОбновленийПрограммы = ОбщегоНазначения.ОбщийМодуль("ПолучениеОбновленийПрограммы");
		МодульПолучениеОбновленийПрограммы.УдалитьИнформациюОВерсияхПлатформы();
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Минимальная и рекомендуемая версии платформы ""1С:Предприятие"", полученные с сервиса обновления приложений, не соответствуют следующим требованиям:
				| - минимальная версия должна быть заполнена;
				| - минимальная версия не должна быть меньше минимальной версии БСП (см. %1);
				| - минимальная версия не должна быть меньше рекомендуемой версии.
				|Минимальная версия сервиса обновления приложений: %2
				|Минимальная версия БСП: %3
				|Рекомендуемая версия сервиса обновления приложений: %4
				|
				|Минимальная и рекомендуемая версия платформы ""1С:Предприятие"" установлены по данным см. %5.
				|Минимальная версия: %6
				|Рекомендуемая версия: %7'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			"ОбщегоНазначения.МинимальнаяВерсияПлатформы", УказаннаяМинимальная, ТребуемаяБСПМинимальная,
			УказаннаяРекомендуемая, "ОбщегоНазначенияПереопределяемый.ПриОпределенииОбщихПараметровБазовойФункциональности",
			ОбщиеПараметры.МинимальнаяВерсияПлатформы, ОбщиеПараметры.РекомендуемаяВерсияПлатформы);
			
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Базовая функциональность'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Предупреждение,,, 
			ТекстСообщения);
	Исключение
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось удалить ранее сохраненный ответ сервиса по получению информации о версиях платформы для текущей конфигурации по причине:
				|%1'",
				ОбщегоНазначения.КодОсновногоЯзыка()), ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Базовая функциональность'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Предупреждение,,,
			ТекстСообщения);
	КонецПопытки;
	
	// Конец Локализация

КонецПроцедуры

#КонецОбласти