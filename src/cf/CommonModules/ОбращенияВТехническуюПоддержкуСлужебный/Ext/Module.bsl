///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Подготавливает форму для работы с обращениями в техническую поддержку.
// Вызывается из обработчика формы "ПриСозданииНаСервере" со свойствами.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма со свойствами:
//    * Элементы - ВсеЭлементыФормы - элементы формы со свойствами:
//      * ГруппаТребуетсяПомощь           - ГруппаФормы - раздел, в котором размещены элементы для взаимодействия с поддержкой.
//                                                        Используется всегда. Недоступно отображение раздела для сеанса
//                                                        внешнего пользователя.
//
//      * ОписаниеОбращенияВПоддержку     - ДекорацияФормы - описание обращения в поддержку.
//                                                           Используется всегда.
//                                                           1. Как описание при взаимодействии через кнопки
//                                                           "ВопросВПоддержку" и "ИнформацияДляОтправкиВПоддержку".
//                                                           2. Как форматированная строка с навигационными ссылками
//                                                           "ВопросВПоддержку" и "ИнформацияДляОтправкиВПоддержку".
//
//      * ВопросВПоддержку                - КнопкаФормы - кнопка отправки вопроса в поддержку.
//                                                        Используется опционально.
//
//      * ИнформацияДляОтправкиВПоддержку - КнопкаФормы - кнопка скачивания информации для отправки в поддержку.
//                                                        Используется опционально.
//
Процедура ПриСозданииНаСервере(Форма) Экспорт
	
	ЭлементыФормы = Форма.Элементы;
	
	Если Пользователи.ЭтоСеансВнешнегоПользователя() Тогда
		СкрытьРазделТребуетсяПомощь(ЭлементыФормы);
		Возврат;
	КонецЕсли;
	
	ОпределитьДоступностьЭлементовФормы(ЭлементыФормы);
	ЗаполнитьОписаниеОбращенияВПоддержку(ЭлементыФормы);
	
КонецПроцедуры

// Отключает видимость для раздела "Требуется помощь".
//
// Параметры:
//  ЭлементыФормы - ВсеЭлементыФормы - элементы формы.
//
Процедура СкрытьРазделТребуетсяПомощь(ЭлементыФормы) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		ЭлементыФормы, "ГруппаТребуетсяПомощь", "Видимость", Ложь);
	
КонецПроцедуры

// Включается видимость для раздела "Требуется помощь".
// Недоступно отображение раздела для сеанса внешнего пользователя.
//
// Параметры:
//  ЭлементыФормы - ВсеЭлементыФормы - элементы формы.
//
Процедура ПоказатьРазделТребуетсяПомощь(ЭлементыФормы) Экспорт
	
	Если Пользователи.ЭтоСеансВнешнегоПользователя() Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		ЭлементыФормы, "ГруппаТребуетсяПомощь", "Видимость", Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОпределитьДоступностьЭлементовФормы(ЭлементыФормы)
	
	ВопросВПоддержкуДоступен = ВопросВПоддержкуДоступен();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		ЭлементыФормы,
		"ВопросВПоддержку",
		"Видимость",
		ВопросВПоддержкуДоступен);
	
КонецПроцедуры

Процедура ЗаполнитьОписаниеОбращенияВПоддержку(ЭлементыФормы)
	
	ОписаниеОбращенияВПоддержку = ОписаниеОбращенияВПоддержку();
	
	Если Не ИспользуетсяВзаимодействиеЧерезНавигационнуюСсылку(ЭлементыФормы) Тогда
		ОписаниеОбращенияВПоддержку = Строка(ОписаниеОбращенияВПоддержку);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		ЭлементыФормы,
		"ОписаниеОбращенияВПоддержку",
		"Заголовок",
		ОписаниеОбращенияВПоддержку);
	
КонецПроцедуры

Функция ИспользуетсяВзаимодействиеЧерезНавигационнуюСсылку(ЭлементыФормы)
	
	Результат = ЭлементыФормы.Найти("ВопросВПоддержку") = Неопределено
		И ЭлементыФормы.Найти("ИнформацияДляОтправкиВПоддержку") = Неопределено;
	
	Возврат Результат;
	
КонецФункции

Функция ОписаниеОбращенияВПоддержку()
	
	ВопросВПоддержкуДоступен = ВопросВПоддержкуДоступен();
	
	Если ВопросВПоддержкуДоступен Тогда
		ОписаниеОбращенияВПоддержку = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'При возникновении затруднений обратитесь в <a href = %1>службу поддержки</a>. В случае необходимости предоставьте <a href = %2>техническую информацию</a> о возникшей проблеме.'"),
			"ВопросВПоддержку",
			"ИнформацияДляОтправкиВПоддержку");
	Иначе
		ОписаниеОбращенияВПоддержку = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru = 'При возникновении затруднений обратитесь в службу поддержки. В случае необходимости предоставьте <a href = %1>техническую информацию</a> о возникшей проблеме.'"),
			"ИнформацияДляОтправкиВПоддержку");
	КонецЕсли;
	
	ОбращенияВТехническуюПоддержкуЛокализация.ПриОпределенииОписанияОбращенияВПоддержку(
		ОписаниеОбращенияВПоддержку,
		ВопросВПоддержкуДоступен);
	
	Возврат ОписаниеОбращенияВПоддержку;
	
КонецФункции

Функция ВопросВПоддержкуДоступен()
	
	СообщенияВСлужбуТехническойПоддержкиДоступны = ОбщегоНазначения.ПодсистемаСуществует(
		"ИнтернетПоддержкаПользователей.СообщенияВСлужбуТехническойПоддержки");
	
	РаботаСПочтовымиСообщениямиДоступна = ОбщегоНазначения.ПодсистемаСуществует(
		"СтандартныеПодсистемы.РаботаСПочтовымиСообщениями");
	
	Возврат СообщенияВСлужбуТехническойПоддержкиДоступны Или РаботаСПочтовымиСообщениямиДоступна;
	
КонецФункции

#КонецОбласти
