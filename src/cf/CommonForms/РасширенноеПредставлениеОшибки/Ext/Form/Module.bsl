///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2020, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Заголовок = Параметры.ЗаголовокПредупреждения;
	АвтоЗаголовок = Ложь;
	
	ТекстОшибкиКлиент = Параметры.ТекстОшибкиКлиент;
	ТекстОшибкиСервер = Параметры.ТекстОшибкиСервер;
	
	КлючНазначенияИспользования = "";
	УстановитьЭлементы(ТекстОшибкиКлиент);
	УстановитьЭлементы(ТекстОшибкиСервер, Истина);
	
	Если Не ПустаяСтрока(ТекстОшибкиКлиент)
		И Не ПустаяСтрока(ТекстОшибкиСервер) Тогда
		
		Элементы.ДекорацияРазделитель.Видимость = Истина;
		Если Элементы.ИнструкцияКлиент.Заголовок <> НСтр("ru = 'Подробнее'")
			И Элементы.ИнструкцияСервер.Заголовок <> НСтр("ru = 'Подробнее'") Тогда
		
			Элементы.ИнструкцияКлиент.Видимость = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИнструкцияНажатие(Элемент)
	
	ЯкорьОшибки = "";
	Если СтрЗаканчиваетсяНа(Элемент.Имя, "Клиент")
		И Не ПустаяСтрока(ЯкорьОшибкиКлиент) Тогда
		
		ЯкорьОшибки = ЯкорьОшибкиКлиент;
	ИначеЕсли СтрЗаканчиваетсяНа(Элемент.Имя, "Сервер")
		И Не ПустаяСтрока(ЯкорьОшибкиСервер) Тогда
		
		ЯкорьОшибки = ЯкорьОшибкиСервер;
	КонецЕсли;
	
	ЭлектроннаяПодписьКлиент.ОткрытьИнструкциюПоТипичнымПроблемамПриРаботеСПрограммами(ЯкорьОшибки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЭлементы(ТекстОшибки, ОшибкаНаСервере = Ложь)
	
	Суффикс = ?(ОшибкаНаСервере, "Сервер", "Клиент");
	
	Элементы["Ошибка" + Суффикс].Видимость = Не ПустаяСтрока(ТекстОшибки);
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		
		ОписаниеОшибки = ЭлектроннаяПодписьСлужебный.ОшибкаПоКлассификатору(ТекстОшибки);
		ЭтоИзвестнаяОшибка = ОписаниеОшибки <> Неопределено;
		
		КлючНазначенияИспользования = КлючНазначенияИспользования
			+ ?(ЭтоИзвестнаяОшибка, "ИзвестнаяОшибка", "НеизвестнаяОшибка") + Суффикс;
		
		Элементы["ВозможныеПричиныИРешения" + Суффикс].Видимость = ЭтоИзвестнаяОшибка;
		Если ЭтоИзвестнаяОшибка Тогда
			
			Элементы["Инструкция" + Суффикс].Заголовок = НСтр("ru = 'Подробнее'");
			Элементы["Причины" + Суффикс + "Текст"].Заголовок = ОписаниеОшибки.Причина;
			Элементы["Решения" + Суффикс + "Текст"].Заголовок = ОписаниеОшибки.Решение;
			
			ЭтотОбъект["ЯкорьОшибки" + Суффикс] = ОписаниеОшибки.Ссылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти