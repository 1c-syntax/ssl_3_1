///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ЕстьРассчитанныеСрокиХранения() Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СрокХраненияНеРассчитан", Дата(3999, 12, 31));
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	СрокиХраненияПерсональныхДанных.СрокХранения КАК СрокХранения
		|ИЗ
		|	РегистрСведений.СрокиХраненияПерсональныхДанных КАК СрокиХраненияПерсональныхДанных
		|ГДЕ
		|	СрокиХраненияПерсональныхДанных.СрокХранения <> &СрокХраненияНеРассчитан";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Параметры:
//  Субъекты - ОпределяемыйТип.СубъектПерсональныхДанных
//  ДатаАктуальности - Дата
// 
// Возвращаемое значение:
//  Массив из ОпределяемыйТип.СубъектПерсональныхДанных
//
Функция СубъектыСИстекшимСрокомХранения(Субъекты, ДатаАктуальности = Неопределено) Экспорт
	
	Если ДатаАктуальности = Неопределено Тогда
		ДатаАктуальности = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СрокиХраненияПерсональныхДанных.Субъект
		|ИЗ
		|	РегистрСведений.СрокиХраненияПерсональныхДанных КАК СрокиХраненияПерсональныхДанных
		|ГДЕ
		|	СрокиХраненияПерсональныхДанных.СрокХранения < &ДатаАктуальности
		|	И СрокиХраненияПерсональныхДанных.Субъект В (&Субъекты)";
	
	Запрос.УстановитьПараметр("ДатаАктуальности", ДатаАктуальности);
	Запрос.УстановитьПараметр("Субъекты", Субъекты);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Субъект");
	
КонецФункции

#КонецОбласти

#КонецЕсли
