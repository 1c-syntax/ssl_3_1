///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура обновляет данные регистра при изменении использования видов доступа.
//
// Параметры:
//  ЕстьИзменения - Булево - (возвращаемое значение) - если производилась запись,
//                  устанавливается Истина, иначе не изменяется.
//
//  БезОбновленияЗависимыхДанных - Булево - если Истина, тогда
//                  не вызвать процедуру ПриИзмененииИспользованияВидовДоступа и
//                  не планировать обновление параметров ограничения доступа.
//
Процедура ОбновитьДанныеРегистра(ЕстьИзменения = Неопределено, БезОбновленияЗависимыхДанных = Ложь) Экспорт
	
	РегистрыСведений.ПараметрыРаботыВерсийРасширений.ЗаблокироватьДляИзмененияВФайловойИБ();
	СвойстваВидовДоступа = УправлениеДоступомСлужебный.СвойстваВидовДоступа();
	
	НаборЗаписей = СоздатьНаборЗаписей();
	
	Для Каждого СвойстваВидаДоступа Из СвойстваВидовДоступа.Массив Цикл
		
		Если СвойстваВидаДоступа.Имя = "ВнешниеПользователи"
		 Или СвойстваВидаДоступа.Имя = "Пользователи" Тогда
			// Эти виды доступа не могут быть отключены по функциональным опциям.
			Используется = Истина;
		Иначе
			Используется = Истина;
			ИнтеграцияПодсистемБСП.ПриЗаполненииИспользованияВидаДоступа(СвойстваВидаДоступа.Имя, Используется);
			УправлениеДоступомПереопределяемый.ПриЗаполненииИспользованияВидаДоступа(СвойстваВидаДоступа.Имя, Используется);
		КонецЕсли;
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ТипЗначенийДоступа = СвойстваВидаДоступа.Ссылка;
		НоваяЗапись.Используется = Используется;
		
		Если НоваяЗапись.ТипЗначенийДоступа <> СвойстваВидаДоступа.Ссылка Тогда
			НаборЗаписей.Удалить(НоваяЗапись);
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЕстьИзмененияИспользованияВидовДоступа(НаборЗаписей) Тогда
		Возврат;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	Блокировка.Добавить("РегистрСведений.ИспользуемыеВидыДоступа");
	
	НачатьТранзакцию();
	Попытка
		Блокировка.Заблокировать();
		
		Если ЕстьИзмененияИспользованияВидовДоступа(НаборЗаписей) Тогда
			НаборЗаписей.Записать();
			ЕстьИзменения = Истина;
			Если Не БезОбновленияЗависимыхДанных Тогда
				ПриИзмененииИспользованияВидовДоступа(Истина);
			КонецЕсли;
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Параметры:
//  НаборЗаписей - РегистрСведенийНаборЗаписей.ИспользуемыеВидыДоступа
//
// Возвращаемое значение:
//  Булево
//
Функция ЕстьИзмененияИспользованияВидовДоступа(НаборЗаписей)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИспользуемыеВидыДоступа.ТипЗначенийДоступа КАК ТипЗначенийДоступа,
	|	ИспользуемыеВидыДоступа.Используется КАК Используется
	|ПОМЕСТИТЬ НовыеЗаписи
	|ИЗ
	|	&НовыеЗаписи КАК ИспользуемыеВидыДоступа
	|ГДЕ
	|	&Отбор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЗначениеИстина
	|ИЗ
	|	НовыеЗаписи КАК НовыеЗаписи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИспользуемыеВидыДоступа КАК ИспользуемыеВидыДоступа
	|		ПО (ИспользуемыеВидыДоступа.ТипЗначенийДоступа = НовыеЗаписи.ТипЗначенийДоступа)
	|			И (ИспользуемыеВидыДоступа.Используется = НовыеЗаписи.Используется)
	|ГДЕ
	|	ИспользуемыеВидыДоступа.ТипЗначенийДоступа ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЗначениеИстина
	|ИЗ
	|	РегистрСведений.ИспользуемыеВидыДоступа КАК ИспользуемыеВидыДоступа
	|		ЛЕВОЕ СОЕДИНЕНИЕ НовыеЗаписи КАК НовыеЗаписи
	|		ПО (НовыеЗаписи.ТипЗначенийДоступа = ИспользуемыеВидыДоступа.ТипЗначенийДоступа)
	|			И (НовыеЗаписи.Используется = ИспользуемыеВидыДоступа.Используется)
	|ГДЕ
	|	&Отбор
	|	И НовыеЗаписи.ТипЗначенийДоступа ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("НовыеЗаписи", НаборЗаписей.Выгрузить());
	
	Если НаборЗаписей.Отбор.ТипЗначенийДоступа.Использование Тогда
		Запрос.УстановитьПараметр("ТипЗначенийДоступа",
			НаборЗаписей.Отбор.ТипЗначенийДоступа.Значение);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Отбор",
			"ИспользуемыеВидыДоступа.ТипЗначенийДоступа = &ТипЗначенийДоступа");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Отбор", "ИСТИНА");
	КонецЕсли;
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	Возврат Не РезультатыЗапроса[1].Пустой() Или Не РезультатыЗапроса[2].Пустой();
	
КонецФункции

// Параметры:
//  ЭлементДанных - РегистрСведенийНаборЗаписей.ИспользуемыеВидыДоступа
//
Процедура ЗарегистрироватьИзменениеПриПолученииДанных(ЭлементДанных) Экспорт
	
	Если Не ЕстьИзмененияИспользованияВидовДоступа(ЭлементДанных) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПользователиСлужебный.ЗарегистрироватьСсылки("ИспользуемыеВидыДоступа", Неопределено);
	
КонецПроцедуры

// Только для внутреннего использования.
Процедура ОбработатьИзменениеЗарегистрированноеПриПолученииДанных() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		// Изменение настроек прав в АРМ заблокированы и не загружаются в область данных.
		Возврат;
	КонецЕсли;
	
	Изменения = ПользователиСлужебный.ЗарегистрированныеСсылки("ИспользуемыеВидыДоступа");
	Если Изменения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПриИзмененииИспользованияВидовДоступа(Изменения.Количество() = 1 И Изменения[0] <> Неопределено);
	
	ПользователиСлужебный.ЗарегистрироватьСсылки("ИспользуемыеВидыДоступа", Null);
	
КонецПроцедуры

Процедура ЗапланироватьОбновлениеПриИзмененииИспользованияВидовДоступа() Экспорт
	ПользователиСлужебный.ЗарегистрироватьСсылки("ИспользуемыеВидыДоступа", Неопределено);
КонецПроцедуры

// Для процедур ОбновитьДанныеРегистра, ОбработатьИзменениеЗарегистрированноеПриЗагрузке.
Процедура ПриИзмененииИспользованияВидовДоступа(ПланироватьОбновлениеПараметровОграниченияДоступа = Ложь) Экспорт
	
	РегистрыСведений.ЗначенияГруппДоступа.ОбновитьДанныеРегистра();
	РегистрыСведений.ИспользуемыеВидыДоступаПоТаблицам.ОбновитьДанныеРегистра();
	
	Если ПланироватьОбновлениеПараметровОграниченияДоступа Тогда
		УправлениеДоступомСлужебный.ЗапланироватьОбновлениеПараметровОграниченияДоступа(
			"ПриИзмененииИспользованияВидовДоступа");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
