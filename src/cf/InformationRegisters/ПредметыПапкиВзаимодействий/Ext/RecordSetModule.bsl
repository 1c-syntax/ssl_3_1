///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	СтарыйНабор = ОбщегоНазначения.ЗаписиНабораИзБазыДанных(ЭтотОбъект, Замещение, ПоляДляАнализа());
	
	ДополнительныеСвойства.Вставить("СтарыйНабор", СтарыйНабор);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Взаимодействия.РассчитыватьРассмотрено(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	СтарыйНабор = ДополнительныеСвойства.СтарыйНабор;
	
	Если ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		НовыйНабор = Выгрузить(Новый Массив, ПоляДляАнализа());
	Иначе
		НовыйНабор = Выгрузить(, ПоляДляАнализа());
	КонецЕсли;
	
	СтараяЗапись = СтруктураЗаписи(СтарыйНабор);
	НоваяЗапись  = СтруктураЗаписи(НовыйНабор);
	
	ДанныеДляРасчета = Новый Структура("НоваяЗапись, СтараяЗапись", НоваяЗапись, СтараяЗапись);
	
	Если НоваяЗапись.Рассмотрено <> СтараяЗапись.Рассмотрено Тогда
		
		Взаимодействия.РассчитатьРассмотреноПоПапкам(Взаимодействия.ТаблицаДанныхДляРасчетаРассмотрено(ДанныеДляРасчета, "Папка"));
		Взаимодействия.РассчитатьРассмотреноПоПредметам(Взаимодействия.ТаблицаДанныхДляРасчетаРассмотрено(ДанныеДляРасчета, "Предмет"));
		
		Возврат;
		
	КонецЕсли;
	
	Если НоваяЗапись.Папка <> СтараяЗапись.Папка Тогда
		
		Взаимодействия.РассчитатьРассмотреноПоПапкам(Взаимодействия.ТаблицаДанныхДляРасчетаРассмотрено(ДанныеДляРасчета, "Папка"));
		
		Возврат;
		
	КонецЕсли;
	
	Если НоваяЗапись.Предмет <> СтараяЗапись.Предмет Тогда
		
		Взаимодействия.РассчитатьРассмотреноПоПредметам(Взаимодействия.ТаблицаДанныхДляРасчетаРассмотрено(ДанныеДляРасчета, "Предмет"));
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПоляДляАнализа()
	
	МетаданныеРегистра = Метаданные();
	
	ПоляДляАнализа = Новый Массив;
	ПоляДляАнализа.Добавить(МетаданныеРегистра.Ресурсы.Предмет.Имя);
	ПоляДляАнализа.Добавить(МетаданныеРегистра.Ресурсы.ПапкаЭлектронногоПисьма.Имя);
	ПоляДляАнализа.Добавить(МетаданныеРегистра.Ресурсы.Рассмотрено.Имя);
	
	Возврат СтрСоединить(ПоляДляАнализа, ",");
	
КонецФункции

Функция СтруктураЗаписи(НаборЗаписей)

	СтруктураЗаписи = Новый Структура;
	СтруктураЗаписи.Вставить("Предмет",     Неопределено);
	СтруктураЗаписи.Вставить("Папка",       Справочники.ПапкиЭлектронныхПисем.ПустаяСсылка());
	СтруктураЗаписи.Вставить("Рассмотрено", Неопределено);
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Возврат СтруктураЗаписи;
	КонецЕсли;
	
	ЗаписьНабора = НаборЗаписей[0];
	
	СтруктураЗаписи.Предмет     = ЗаписьНабора.Предмет;
	СтруктураЗаписи.Папка       = ЗаписьНабора.ПапкаЭлектронногоПисьма;
	СтруктураЗаписи.Рассмотрено = ЗаписьНабора.Рассмотрено;
	
	Возврат СтруктураЗаписи;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли