///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем СтарыеЗаписи; // Заполняется ПередЗаписью для использования ПриЗаписи.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;
	
	СтарыеЗаписи = ОбщегоНазначения.ЗаписиНабораИзБазыДанных(ЭтотОбъект, Замещение, "МашиночитаемаяДоверенность,ТехническийСтатус,Подписана");
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;

	ИзменившиесяСтатусы = Новый Соответствие;
		
	Для каждого Запись Из ЭтотОбъект Цикл
		Статусы = Новый Структура;
		Статусы.Вставить("НовыйСтатус", Запись.ТехническийСтатус);
		Статусы.Вставить("Подписана", Запись.Подписана);
		
		ЗаписьДоИзменения = СтарыеЗаписи.Найти(Запись.МашиночитаемаяДоверенность, "МашиночитаемаяДоверенность");
		Если ЗаписьДоИзменения <> Неопределено Тогда
			Статусы.Вставить("СтатусДоИзменения", ЗаписьДоИзменения.ТехническийСтатус);
		Иначе
			Статусы.Вставить("СтатусДоИзменения", Перечисления.ТехническиеСтатусыМЧД.ПустаяСсылка());
		КонецЕсли;
		Если ЗаписьДоИзменения = Неопределено Или ЗаписьДоИзменения.Подписана <> Статусы.Подписана
			Или Статусы.СтатусДоИзменения <> Статусы.НовыйСтатус Тогда
			ИзменившиесяСтатусы.Вставить(Запись.МашиночитаемаяДоверенность, Статусы);
		КонецЕсли;
	КонецЦикла;
		
	Если ИзменившиесяСтатусы.Количество() > 0 Тогда
		МашиночитаемыеДоверенностиФНССлужебный.ПриИзмененииСтатусаДоверенности(ИзменившиесяСтатусы);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
