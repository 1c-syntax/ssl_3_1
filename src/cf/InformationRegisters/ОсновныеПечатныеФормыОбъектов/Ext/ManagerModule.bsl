///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ИдентификаторКоманды(Объект) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОсновныеПечатныеФормыОбъектов.Идентификатор
		|ИЗ
		|	РегистрСведений.ОсновныеПечатныеФормыОбъектов КАК ОсновныеПечатныеФормыОбъектов
		|ГДЕ
		|	ОсновныеПечатныеФормыОбъектов.Объект = &Объект";
	
	Запрос.УстановитьПараметр("Объект", Объект);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Идентификатор;
	Иначе
		Результат = "";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ИдентификаторыКоманд(Объекты) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОсновныеПечатныеФормыОбъектов.Объект КАК Объект,
		|	ОсновныеПечатныеФормыОбъектов.Идентификатор КАК Идентификатор
		|ИЗ
		|	РегистрСведений.ОсновныеПечатныеФормыОбъектов КАК ОсновныеПечатныеФормыОбъектов
		|ГДЕ
		|	ОсновныеПечатныеФормыОбъектов.Объект В (&Объекты)
		|ИТОГИ
		|ПО
		|	Идентификатор";
	
	Запрос.УстановитьПараметр("Объекты", Объекты);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	ВыборкаИдентификаторы = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Результат = Новый Структура;
	
	Пока ВыборкаИдентификаторы.Следующий() Цикл
		Выборка = ВыборкаИдентификаторы.Выбрать();
		
		Ссылки = Новый Массив;
		Пока Выборка.Следующий() Цикл
			Ссылки.Добавить(Выборка.Объект);
		КонецЦикла;
		Результат.Вставить(ВыборкаИдентификаторы.Идентификатор, Ссылки);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура СохранитьНаименованиеОсновнойПечатнойФормы(Ссылки, НаименованиеПечатнойФормы, Идентификатор) Экспорт
	
	Если ТипЗнч(Ссылки) <> Тип("Массив") Тогда
		МассивСсылок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылки);
	Иначе
		МассивСсылок = ОбщегоНазначения.СкопироватьРекурсивно(Ссылки);
	КонецЕсли;
	
	Пользователь = Пользователи.ТекущийПользователь();
	Дата = ТекущаяДатаСеанса();
	
	Для Каждого СсылкаНаОбъект Из МассивСсылок Цикл
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.Объект = СсылкаНаОбъект;
		Запись.Идентификатор = УправлениеПечатьюКлиентСервер.ИдентификаторБезСпецСимволов(Идентификатор);
		Запись.Пользователь = Пользователь;
		Запись.Дата = Дата;
		Запись.НаименованиеПечатнойФормы = НаименованиеПечатнойФормы;
		УстановитьПривилегированныйРежим(Истина);
		Запись.Записать(Истина);
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЦикла;
	
КонецПроцедуры

Функция НаименованияПечатныхФорм(Ссылки) Экспорт
	
	Результат = Новый Соответствие;
	Для Каждого Ссылка Из Ссылки Цикл
		Результат.Вставить(Ссылка, Неопределено);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОсновныеПечатныеФормыОбъектов.Объект КАК Объект,
		|	ОсновныеПечатныеФормыОбъектов.НаименованиеПечатнойФормы КАК НаименованиеПечатнойФормы
		|ИЗ
		|	РегистрСведений.ОсновныеПечатныеФормыОбъектов КАК ОсновныеПечатныеФормыОбъектов
		|ГДЕ
		|	ОсновныеПечатныеФормыОбъектов.Объект В (&Ссылки)";
	
	Запрос.УстановитьПараметр("Ссылки", Ссылки);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат[Выборка.Объект] = Выборка.НаименованиеПечатнойФормы;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СохраненныеНаименования(Объекты) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОсновныеПечатныеФормыОбъектов.НаименованиеПечатнойФормы КАК НаименованиеПечатнойФормы,
		|	ОсновныеПечатныеФормыОбъектов.Идентификатор,
		|	ОсновныеПечатныеФормыОбъектов.Пользователь,
		|	ОсновныеПечатныеФормыОбъектов.Дата
		|ИЗ
		|	РегистрСведений.ОсновныеПечатныеФормыОбъектов КАК ОсновныеПечатныеФормыОбъектов
		|ГДЕ
		|	ОсновныеПечатныеФормыОбъектов.Объект В (&Объекты)";
	
	Запрос.УстановитьПараметр("Объекты", Объекты);
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(РезультатЗапроса);
	
КонецФункции
#КонецОбласти

#КонецЕсли
