///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.Подписант) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Подписант", Параметры.Подписант);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "Подписант", "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "ПодписанныйОбъект", "Видимость", Ложь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Операция) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Операция", Параметры.Операция);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, "Операция", "Видимость", Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ПоказатьВопросОбУдалении();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСтатусы(Команда)
	
	ОбновитьСтатусНаСервере(Элементы.Список.ВыделенныеСтроки);
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Процедура ПоказатьВопросОбУдалении()
	
	Ответ = Ждать ВопросАсинх(НСтр("ru = 'Удалить выделенные строки?'"), РежимДиалогаВопрос.ДаНет);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		УдалитьОжидаемыеПодписи(Элементы.Список.ВыделенныеСтроки);
	КонецЕсли;
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьОжидаемыеПодписи(Знач ИдентификаторыСтрок)
	
	МассивИдентификаторов = Новый Массив;
	Для Каждого КлючЗаписи Из ИдентификаторыСтрок Цикл
		МассивИдентификаторов.Добавить(КлючЗаписи.ИдентификаторДокумента);
	КонецЦикла;
	
	Если МассивИдентификаторов.Количество() > 0 Тогда
		СервисМобильнойПодписиСлужебный.УдалитьОжидаемыеПодписи(МассивИдентификаторов);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьСтатусНаСервере(Знач ИдентификаторыСтрок)
	
	МассивИдентификаторов = Новый Массив;
	Для Каждого КлючЗаписи Из ИдентификаторыСтрок Цикл
		МассивИдентификаторов.Добавить(КлючЗаписи.ИдентификаторДокумента);
	КонецЦикла;
	СервисМобильнойПодписиСлужебный.ПолучитьРезультатыИзСервисаМобильнойПодписи(МассивИдентификаторов);
	
КонецПроцедуры

#КонецОбласти
