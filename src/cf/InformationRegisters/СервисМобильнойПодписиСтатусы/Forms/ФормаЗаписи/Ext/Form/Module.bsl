///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТолькоПросмотр = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	Элементы.ГруппаОшибка.Видимость = Ложь;
	Элементы.ФормаПоказатьПодпись.Видимость = Ложь;
	ИдентификаторДокумента = Строка(ТекущийОбъект.ИдентификаторДокумента);
	Результат = ТекущийОбъект.Результат.Получить();
	
	Если ТипЗнч(Результат) = Тип("ДвоичныеДанные") И Запись.Операция = Перечисления.СервисМобильнойПодписиОперации.Подписание Тогда
		АдресПодписи = ПоместитьВоВременноеХранилище(Результат, УникальныйИдентификатор);
		Элементы.ФормаПоказатьПодпись.Видимость = Истина;
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура") И ЗначениеЗаполнено(Результат.ТекстОшибки) Тогда
		ТекстОшибки = Результат.ТекстОшибки;
		Ошибка = ПоместитьВоВременноеХранилище(Результат, УникальныйИдентификатор);
		Элементы.ГруппаОшибка.Видимость = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьВозможностьРедактирования(Команда)
	
	ТолькоПросмотр = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПодпись(Команда)
	
	ЭлектроннаяПодписьКлиент.ПрочитатьСвойстваПодписи(
		Новый ОписаниеОповещения("ПослеЧтенияСвойствПодписи", ЭтотОбъект), АдресПодписи, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИнформацию(Команда)
	СервисМобильнойПодписиКлиент.ОткрытьФормуРасширенногоПредставленияОшибки(
		НСтр("ru='Статус получения подписи'"), ПолучитьИзВременногоХранилища(Ошибка), ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЧтенияСвойствПодписи(СвойстваПодписи, Контекст) Экспорт
	
	НовыеСвойстваПодписи = ЭлектроннаяПодписьКлиентСервер.РезультатПроверкиПодписиВФорме();
	ЗаполнитьЗначенияСвойств(НовыеСвойстваПодписи, СвойстваПодписи);
	НовыеСвойстваПодписи.АдресПодписи = АдресПодписи;
	НовыеСвойстваПодписи.УстановившийПодпись = Запись.Отправитель;
	НовыеСвойстваПодписи.АдресСертификата = ПоместитьВоВременноеХранилище(СвойстваПодписи.Сертификат, УникальныйИдентификатор);
	НовыеСвойстваПодписи.ДатаПодписи = СвойстваПодписи.НеподтвержденнаяДатаПодписи;
	ЭлектроннаяПодписьКлиент.ОткрытьПодпись(НовыеСвойстваПодписи);
	
КонецПроцедуры

#КонецОбласти

