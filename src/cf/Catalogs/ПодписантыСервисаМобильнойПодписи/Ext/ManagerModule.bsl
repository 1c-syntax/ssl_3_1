///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	ЗначениеРазрешено(Организация, ПустаяСсылка КАК Истина) ИЛИ ЭтоАвторизованныйПользователь(Добавил) ИЛИ ЭтоАвторизованныйПользователь(Пользователи.Пользователь)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ЭтоАвторизованныйПользователь(Добавил) ИЛИ ЭтоАвторизованныйПользователь(Пользователи.Пользователь)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция АвтоНаименование(Объект) Экспорт
	
	Наименование = Новый Массив;
	
	ФИО = "";
	Если ЗначениеЗаполнено(Объект.Фамилия) Тогда
		СтруктураФИО = Новый Структура("Фамилия, Имя, Отчество", Объект.Фамилия, Объект.Имя, Объект.Отчество);
		ФИО = ФизическиеЛицаКлиентСервер.ФамилияИнициалы(СтруктураФИО);
	ИначеЕсли ЗначениеЗаполнено(Объект.ФизическоеЛицо) Тогда
		ФИО = Объект.ФизическоеЛицо;
	ИначеЕсли ЗначениеЗаполнено(Объект.ИННФЛ) Тогда
		ФИО = Объект.ИННФЛ;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФИО) Тогда
		Наименование.Добавить(ФИО);
	КонецЕсли;
	
	Организация = Неопределено;
	Если ЗначениеЗаполнено(Объект.ИНН) Или ФИО = Объект.ИННФЛ Тогда
		Если ЗначениеЗаполнено(Объект.НаименованиеОрганизации) Тогда
			Организация = СокрЛП(Объект.НаименованиеОрганизации);
		ИначеЕсли ЗначениеЗаполнено(Объект.Организация) Тогда
			Организация = СокрЛП(Объект.Организация);
		Иначе
			Организация = Объект.ИНН;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		Наименование.Добавить(Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Должность) Тогда
		Наименование.Добавить(Объект.Должность);
	КонецЕсли;
		
	Наименование.Добавить("*" + Прав(Объект.НомерТелефона, 4));
	Наименование = Лев(СтрСоединить(Наименование, ", "), 150);
	Возврат Наименование;
	
КонецФункции

#КонецОбласти

#КонецЕсли
