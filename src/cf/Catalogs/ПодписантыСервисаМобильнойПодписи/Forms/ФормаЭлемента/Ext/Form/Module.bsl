///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НомерТелефона = Прав(Объект.НомерТелефона, 10);
	ИспользуетсяАвтоНаименование = ИспользуетсяАвтоНаименование(Объект);

	Если ЗначениеЗаполнено(Объект.ИНН) Тогда
		ВидПодписанта = "ЮридическоеЛицо";
	Иначе
		ВидПодписанта = "ИндивидуальныйПредприниматель";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ЕстьОтправленныеДокументы = Объект.ОтправленВСервис Или ЕстьОтправленныеДокументы();
		Элементы.ДекорацияБылиОтправки.Видимость = ЕстьОтправленныеДокументы;
		Элементы.Организация.Видимость = ЭлектроннаяПодпись.ОбщиеНастройки().ОрганизацияИспользуется;
		Элементы.ФизическоеЛицо.Видимость = ЭлектроннаяПодпись.ОбщиеНастройки().ОрганизацияИспользуется;
		Элементы.НомерТелефона.Видимость = Пользователи.ТекущийПользователь() = Объект.Добавил;
		УстановитьВидимостьИДоступность(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Отказ = Истина;
		СтандартныеПодсистемыКлиент.УстановитьХранениеФормы(ЭтотОбъект, Истина);
		ПодключитьОбработчикОжидания("ОбработчикОжиданияДобавитьПодписанта", 0.1, Истина);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)

	Если ВидПодписанта = "ЮридическоеЛицо" И Не ЗначениеЗаполнено(Объект.ИНН) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнен ИНН юридического лица.'"),,"Объект.ИНН",,Отказ);
	КонецЕсли;
	
	ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
	ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Объект);
	ПараметрыПодписанта.НомерТелефона = ?(СтрНачинаетсяС(НомерТелефона, "+7"), НомерТелефона, "+7" + НомерТелефона);
	
	Попытка
		ПроверитьПараметрыПодписанта(ПараметрыПодписанта);
	Исключение
		ОбщегоНазначенияКлиент.СообщитьПользователю(ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()),,,,Отказ);
		Возврат;
	КонецПопытки;
	
	Объект.НомерТелефона = ПараметрыПодписанта.НомерТелефона;
	НомерТелефона = Прав(Объект.НомерТелефона, 10);
	Объект.ИНН = ПараметрыПодписанта.ИНН;
	Объект.ИННФЛ = ПараметрыПодписанта.ИННФЛ;
	
	Если ИспользуетсяАвтонаименование Или Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		Объект.Наименование = АвтоНаименование(ПараметрыПодписанта);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.ИНН) И ЗначениеЗаполнено(Объект.ОГРН) Тогда
		ОГРН = "";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	ИспользуетсяАвтоНаименование = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПодписантаПриИзменении(Элемент)
	Если ВидПодписанта <> "ЮридическоеЛицо" Тогда
		Объект.ИНН = "";
		Объект.Должность = "";
		Объект.НаименованиеОрганизации = "";
		Объект.ОГРН = "";
	Иначе
		Объект.ИННФЛ = "";
	КонецЕсли;
	УстановитьВидимостьИДоступность(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработчикОжиданияДобавитьПодписанта()
	
	СтандартныеПодсистемыКлиент.УстановитьХранениеФормы(ЭтотОбъект, Ложь);
	ПараметрыДобавления = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
	СервисМобильнойПодписиКлиент.ДобавитьПодписанта(ПараметрыДобавления);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьИДоступность(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Если Не Форма.ТолькоПросмотр И Форма.ЕстьОтправленныеДокументы Тогда
		Элементы.ВидПодписанта.ТолькоПросмотр = Истина;
		Элементы.ОГРН.ТолькоПросмотр = Истина;
		Элементы.ИНН.ТолькоПросмотр = Истина;
		Элементы.ИННФЛ.ТолькоПросмотр = Истина;
		Элементы.НомерТелефона.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ЭтоЮрЛицо = Форма.ВидПодписанта = "ЮридическоеЛицо";
	
	Элементы.ВидПодписанта.Видимость = Не Элементы.ВидПодписанта.ТолькоПросмотр;
	Элементы.ГруппаОрганизация.Видимость = ЭтоЮрЛицо;
	Элементы.ИННФЛ.Видимость = Не ЭтоЮрЛицо;
	
	Элементы.НаименованиеОрганизации.Видимость = Не ЗначениеЗаполнено(Объект.Организация)
		Или ЗначениеЗаполнено(Объект.НаименованиеОрганизации);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользуетсяАвтоНаименование(Объект)
	
	Если Не ЗначениеЗаполнено(Объект.Наименование) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат  Объект.Наименование = Справочники.ПодписантыСервисаМобильнойПодписи.АвтоНаименование(Объект);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПроверитьПараметрыПодписанта(ПараметрыПодписанта)
	СервисМобильнойПодписиСлужебный.ПроверкаПараметровПодписанта(ПараметрыПодписанта);
КонецПроцедуры

&НаСервереБезКонтекста
Функция АвтоНаименование(ПараметрыПодписанта)
	Возврат Справочники.ПодписантыСервисаМобильнойПодписи.АвтоНаименование(ПараметрыПодписанта);
КонецФункции

&НаСервере
Функция ЕстьОтправленныеДокументы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СервисМобильнойПодписиСтатусы.ИдентификаторДокумента КАК ИдентификаторДокумента
	|ИЗ
	|	РегистрСведений.СервисМобильнойПодписиСтатусы КАК СервисМобильнойПодписиСтатусы
	|ГДЕ
	|	СервисМобильнойПодписиСтатусы.Подписант = &Подписант";
	
	Запрос.УстановитьПараметр("Подписант", Объект.Ссылка);
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти
