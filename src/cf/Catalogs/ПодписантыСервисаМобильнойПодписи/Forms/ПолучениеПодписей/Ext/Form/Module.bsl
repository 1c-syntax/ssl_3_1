///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ИдентификаторыДокументов) Тогда
		Для Каждого Ид Из Параметры.ИдентификаторыДокументов Цикл
			ИдентификаторыДокументов.Добавить().ИдентификаторДокумента = Ид;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Подписант) Тогда
		Если ТипЗнч(Параметры.Подписант) = Тип("Структура") Тогда
			Подписант = ?(ЗначениеЗаполнено(Параметры.Подписант.Наименование), Параметры.Подписант.Наименование,
				Справочники.ПодписантыСервисаМобильнойПодписи.АвтоНаименование(Параметры.Подписант));
		Иначе
			Подписант = Параметры.Подписант;
		КонецЕсли;
		Элементы.НадписьОтправлено.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			НСтр("ru='Документы отправлены подписанту <b>%1</b> в мобильное приложение <b>Моя подпись</b> от ФНС.
			|
			|• Зайдите в приложение и подпишите документы.
			|
			|• После подписания нажмите кнопку <b>Документы подписаны</b>.'"),
			Подписант);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ДокументыПодписаныВПриложении" Тогда
		ПолучениеРезультатовПослеПодписания();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДокументыПодписаны(Команда)
	ПолучениеРезультатовПослеПодписания();
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ЕстьПодписи = Ложь;
	ОжидаемыеПодписи = Новый Массив;
	Результат = Новый Соответствие;
	Для Каждого Строка Из ИдентификаторыДокументов Цикл
		Если ЗначениеЗаполнено(Строка.СвойстваПодписи) И ЗначениеЗаполнено(Строка.СвойстваПодписи.ДатаПодписи) Тогда
			Результат.Вставить(Строка.ИдентификаторДокумента, Строка.СвойстваПодписи);
			ЕстьПодписи = Истина;
		КонецЕсли;
		ОжидаемыеПодписи.Добавить(Строка.ИдентификаторДокумента);
	КонецЦикла;
	
	Если ОжидаемыеПодписи.Количество() > 0 Тогда
		УдалитьЗапросыНаПодписание(ОжидаемыеПодписи, Ложь);
	КонецЕсли;
	
	Если ЕстьПодписи Тогда
		Закрыть(Результат);
	Иначе
		Закрыть(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолучениеРезультатовПослеПодписания()
	Элементы.ФормаДокументыПодписаны.Видимость = Ложь;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОжидание;
	ПодключитьОбработчикОжидания("ПолучениеРезультатов", 1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеРезультатов()
	
	Если ИдентификаторыДокументов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПолучитьРезультатыИзСервисаМобильнойПодписи();
	
	Если ЗначениеЗаполнено(Ошибка) Тогда
		СервисМобильнойПодписиКлиент.ОткрытьФормуРасширенногоПредставленияОшибки(
			НСтр("ru='Не удалось получить результат подписания'"),
			Ошибка, ЭтотОбъект, Новый ОписаниеОповещения("ПослеЗакрытияФормыОшибки", ЭтотОбъект));
		Возврат;
	КонецЕсли;
	
	Если ИдентификаторыДокументов.НайтиСтроки(Новый Структура("ПодписьПолучена", Ложь)).Количество() > 0 Тогда
		ПодключитьОбработчикОжидания("ПолучениеРезультатов", 15, Истина);
		Возврат;
	КонецЕсли;
	
	ОжидаемыеПодписи = Новый Массив;
	Результат = Новый Соответствие;
	Для Каждого Строка Из ИдентификаторыДокументов Цикл
		Результат.Вставить(Строка.ИдентификаторДокумента, Строка.СвойстваПодписи);
		ОжидаемыеПодписи.Добавить(Строка.ИдентификаторДокумента);
	КонецЦикла;
	
	УдалитьЗапросыНаПодписание(ОжидаемыеПодписи, Истина);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыОшибки(Результат, ДополнительныеПараметры) Экспорт
	Элементы.ФормаДокументыПодписаны.Видимость = Истина;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОтправлено;
КонецПроцедуры

&НаСервере
Процедура ПолучитьРезультатыИзСервисаМобильнойПодписи()
	
	Идентификаторы = ИдентификаторыДокументов.Выгрузить().Скопировать(Новый Структура("ПодписьПолучена", Ложь)).ВыгрузитьКолонку("ИдентификаторДокумента");
	Если Идентификаторы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Ошибка = Неопределено;
	СервисМобильнойПодписиСлужебный.ПолучитьРезультатыИзСервисаМобильнойПодписи(Идентификаторы, Ошибка);
	Если Не ЗначениеЗаполнено(Ошибка) Тогда
		РезультатПроверкиОжидаемыхПодписей = СервисМобильнойПодписиСлужебный.РезультатПроверкиОжидаемыхПодписей(Идентификаторы);
		РезультатыПроверки = РезультатПроверкиОжидаемыхПодписей.РезультатыПроверки;
		Для Каждого СтрокаТаблицы Из ИдентификаторыДокументов Цикл
			СвойстваПодписи = РезультатыПроверки.Получить(СтрокаТаблицы.ИдентификаторДокумента);
			Если СвойстваПодписи = Неопределено
				Или (Не ЗначениеЗаполнено(СвойстваПодписи.ДатаПодписи)
					И Не ЗначениеЗаполнено(СвойстваПодписи.ОшибкаПроверкиДополнительныхАтрибутов)) Тогда
				Продолжить;
			КонецЕсли;
			Если СвойстваПодписи.СтатусВМобильномСервисе = Перечисления.СервисМобильнойПодписиСтатусы.ОжидаетОбработки Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТаблицы.ПодписьПолучена = Истина;
			СтрокаТаблицы.СвойстваПодписи = СвойстваПодписи;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура УдалитьЗапросыНаПодписание(Знач ИдентификаторыДокументов, УспешноеПодписание)
	
	СервисМобильнойПодписиСлужебный.УдалитьОжидаемыеПодписи(ИдентификаторыДокументов, УспешноеПодписание);
	
КонецПроцедуры

#КонецОбласти


