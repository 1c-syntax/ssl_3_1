///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
	ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, Параметры);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыПодписанта);
	
	ИННФЛ = СервисМобильнойПодписиКлиентСервер.ТолькоЦифры(ПараметрыПодписанта.ИННФЛ);
	ИНН = СервисМобильнойПодписиКлиентСервер.ТолькоЦифры(ПараметрыПодписанта.ИНН);
	Если ЗначениеЗаполнено(ПараметрыПодписанта.НомерТелефона) Тогда
		НомерТелефона = Прав(СервисМобильнойПодписиКлиентСервер.ТолькоЦифры(ПараметрыПодписанта.НомерТелефона), 10);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИННФЛ) И Не ЗначениеЗаполнено(ИНН) Тогда
		ВидПодписанта = "ИндивидуальныйПредприниматель"
	Иначе
		ВидПодписанта = "ЮридическоеЛицо"
	КонецЕсли;
	
	Элементы.Организация.Видимость = ЭлектроннаяПодпись.ОбщиеНастройки().ОрганизацияИспользуется;
	Элементы.НаименованиеОрганизации.Видимость = Не Элементы.Организация.Видимость;
	Элементы.ФизическоеЛицо.Видимость = ЭлектроннаяПодпись.ОбщиеНастройки().ФизическоеЛицоИспользуется;
	
	УстановитьВидимостьИДоступность(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидПодписантаПриИзменении(Элемент)
	УстановитьВидимостьИДоступность(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)

	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		РеквизитыФизическогоЛица = РеквизитыФизическогоЛица(ФизическоеЛицо);
		ЗаполненныеРеквизиты = ЗаполнитьРеквизитыФизическогоЛица(РеквизитыФизическогоЛица);
		ЗаполнитьЗначенияСвойств(РеквизитыФизическогоЛица, ЗаполненныеРеквизиты);
	Иначе
		РеквизитыФизическогоЛица = РеквизитыФизическогоЛица();
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыФизическогоЛица);

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		РеквизитыОрганизации = РеквизитыОрганизации(Организация);
		ЗаполненныеРеквизиты = ЗаполнитьРеквизитыОрганизации(РеквизитыОрганизации);
		ЗаполнитьЗначенияСвойств(РеквизитыОрганизации, ЗаполненныеРеквизиты);
		
		ИННИП = Неопределено;
		Если СтрДлина(РеквизитыОрганизации.ИНН) = 12 Тогда
			ВидПодписанта = "ИндивидуальныйПредприниматель";
			ИННИП = РеквизитыОрганизации.ИНН;
			РеквизитыОрганизации.ИНН = "";
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РеквизитыОрганизации.ФизическиеЛица) Тогда
			Если РеквизитыОрганизации.ФизическиеЛица.Количество() > 0 Тогда
				РеквизитыФизическогоЛица = РеквизитыФизическогоЛица();
				ЗаполнитьЗначенияСвойств(РеквизитыФизическогоЛица, РеквизитыОрганизации.ФизическиеЛица[0]);
				ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыФизическогоЛица);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИННИП) Тогда
			ИННФЛ = ИННИП;
			Должность = "";
		КонецЕсли;
	Иначе
		РеквизитыОрганизации = РеквизитыОрганизации();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыОрганизации);
	УстановитьВидимостьИДоступность(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Добавить(Команда)
	
	ПараметрыПодписанта = СервисМобильнойПодписиКлиентСервер.ПараметрыПодписанта();
	ЗаполнитьЗначенияСвойств(ПараметрыПодписанта, ЭтотОбъект);
	ПараметрыПодписанта.НомерТелефона = НомерТелефона;
	
	Попытка
		ПроверитьПараметрыПодписанта(ПараметрыПодписанта);
	Исключение
		ОбщегоНазначенияКлиент.СообщитьПользователю(ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат;
	КонецПопытки;
	
	Подписант = ДобавитьПодписанта(ПараметрыПодписанта);
	
	Если ЗначениеЗаполнено(Подписант) Тогда
		ОповеститьОЗаписиНового(Подписант);
		Оповестить("Запись_ПодписантыСервисаМобильнойПодписи", Новый Структура("ЭтоНовый", Истина), Подписант);
		Закрыть(Подписант);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПроверитьПараметрыПодписанта(ПараметрыПодписанта)
	СервисМобильнойПодписиСлужебный.ПроверкаПараметровПодписанта(ПараметрыПодписанта);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьПодписанта(ПараметрыПодписанта)
	Возврат СервисМобильнойПодписиСлужебный.ДобавитьПодписантаВСправочник(ПараметрыПодписанта);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыОрганизации(Организация = Неопределено)
	РеквизитыОрганизации = Новый Структура;
	РеквизитыОрганизации.Вставить("Организация", Организация);
	РеквизитыОрганизации.Вставить("НаименованиеОрганизации");
	РеквизитыОрганизации.Вставить("ИНН");
	РеквизитыОрганизации.Вставить("ОГРН");
	РеквизитыОрганизации.Вставить("ФизическиеЛица");
	Возврат РеквизитыОрганизации;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыФизическогоЛица(ФизическоеЛицо = Неопределено)
	РеквизитыФизическогоЛица = Новый Структура;
	РеквизитыФизическогоЛица.Вставить("ФизическоеЛицо", ФизическоеЛицо);
	РеквизитыФизическогоЛица.Вставить("ИННФЛ");
	РеквизитыФизическогоЛица.Вставить("Фамилия");
	РеквизитыФизическогоЛица.Вставить("Имя");
	РеквизитыФизическогоЛица.Вставить("Отчество");
	РеквизитыФизическогоЛица.Вставить("Должность");
	РеквизитыФизическогоЛица.Вставить("Телефон");
	Возврат РеквизитыФизическогоЛица;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьИДоступность(Форма)
	
	Форма.Элементы.ГруппаОрганизация.Видимость = Форма.ВидПодписанта = "ЮридическоеЛицо";
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаполнитьРеквизитыОрганизации(Знач Реквизиты)
	
	СтандартнаяОбработка = Истина;
	
	СервисМобильнойПодписиПереопределяемый.ПриЗаполненииРеквизитовОрганизации(Реквизиты, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда

		Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Организации") Тогда
			Возврат Реквизиты;
		КонецЕсли;

		Если Не ЗначениеЗаполнено(Реквизиты.Организация) Тогда
			Возврат Реквизиты; // Реквизиты организации нельзя заполнить, если организация не выбрана.
		КонецЕсли;

		МодульОрганизацииСервер = ОбщегоНазначения.ОбщийМодуль("ОрганизацииСервер");
		СведенияОбОрганизации = МодульОрганизацииСервер.СведенияОбОрганизации(Реквизиты.Организация,,
			ТекущаяДатаСеанса());

		ЭтоИП = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ЭтоИП", Ложь);
		
		Если ЭтоИП Тогда

			Реквизиты.ИНН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации,
				"ИндивидуальныйПредпринимательИНН", "");
				
			ФИО = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ФизическоеЛицоНаименование",
				"");
			
			РеквизитыФизическогоЛица = РеквизитыФизическогоЛица();
			Если ЗначениеЗаполнено(ФИО) Тогда
				ФИО = ФизическиеЛицаКлиентСервер.ЧастиИмени(ФИО);
				РеквизитыФизическогоЛица.Фамилия = ФИО.Фамилия;
				РеквизитыФизическогоЛица.Имя = ФИО.Имя;
				РеквизитыФизическогоЛица.Отчество = ФИО.Имя;
			КонецЕсли;
			
			Реквизиты.ФизическиеЛица = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(РеквизитыФизическогоЛица);
			
		Иначе
			Реквизиты.ОГРН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ОГРН", "");
			Реквизиты.ИНН = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации, "ИНН", "");
			Реквизиты.НаименованиеОрганизации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СведенияОбОрганизации,
				"НаименованиеСокращенное", "");

		КонецЕсли;

	КонецЕсли;

	Возврат Реквизиты;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьРеквизитыФизическогоЛица(Знач Параметры)
	СервисМобильнойПодписиПереопределяемый.ПриЗаполненииРеквизитовФизическогоЛица(Параметры);
	Возврат Параметры;
КонецФункции


#КонецОбласти
