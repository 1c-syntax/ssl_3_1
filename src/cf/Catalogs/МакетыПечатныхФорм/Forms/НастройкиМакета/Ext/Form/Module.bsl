///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.КодЯзыка) Тогда
		Элементы.ОсновнаяПечатнаяФорма.Доступность = (Параметры.КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	
	ВидимостьГруппы = УправлениеПечатью.ВходитВСоставОбъектовСОсновнымиПечатнымиФормами(ТипЗнч(Параметры.ВладелецМакета));
	Элементы.ГруппаОсновнаяПечатнаяФорма.Видимость = ВидимостьГруппы;
	
	Если ВидимостьГруппы Тогда
		СсылкаМакета = Справочники.МакетыПечатныхФорм.СсылкаМакета(Параметры.ИдентификаторМакета);
		
		Если ЗначениеЗаполнено(СсылкаМакета) Тогда
			ИменаРеквизитов = "Наименование,ОсновнаяПечатнаяФорма,НаименованиеПечатнойФормы";
			ДанныеМакета = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылкаМакета, ИменаРеквизитов);
			
			НаименованиеМакета        = ДанныеМакета.Наименование;
			ОсновнаяПечатнаяФорма     = ДанныеМакета.ОсновнаяПечатнаяФорма;
			НаименованиеПечатнойФормы = ДанныеМакета.НаименованиеПечатнойФормы;
		Иначе
			НаименованиеМакета = Параметры.НаименованиеМакета;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ПриПодтвержденииЗакрытия", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы,, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьОтображениеФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеМакетаПриИзменении(Элемент)
	ОбновитьОтображениеФормы();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	СохранитьИЗакрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриПодтвержденииЗакрытия(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	СохранитьИЗакрыть();
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ИмяДокумента", НаименованиеМакета);
	СтруктураПараметров.Вставить("ОсновнаяПечатнаяФорма", ОсновнаяПечатнаяФорма);
	СтруктураПараметров.Вставить("НаименованиеПечатнойФормы", НаименованиеПечатнойФормы);
	
	Закрыть(СтруктураПараметров);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображениеФормы()
	Элементы.НаименованиеПечатнойФормы.ПодсказкаВвода = НаименованиеМакета;
КонецПроцедуры
#КонецОбласти
