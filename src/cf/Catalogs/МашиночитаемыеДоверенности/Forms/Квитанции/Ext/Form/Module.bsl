///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Доверенность = Параметры.Доверенность;
	ОбновитьСписокФайлов();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_Файл" Тогда
		ОбновитьСписокФайлов();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПросмотрКвитанцииОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	СтандартнаяОбработка = Ложь;
	ДанныеФайла = ДанныеФайла(Расшифровка, УникальныйИдентификатор);
	РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла, Ложь);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыФайлыДоверенности

&НаКлиенте
Процедура ФайлыДоверенностиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элементы.ФайлыДоверенности.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПоказатьЗначение(, Элементы.ФайлыДоверенности.ТекущиеДанные.Файл);
КонецПроцедуры

&НаКлиенте
Процедура ФайлыДоверенностиПриАктивизацииСтроки(Элемент)
	
	Если Элементы.ФайлыДоверенности.ТекущиеДанные = Неопределено Тогда
		ПросмотрКвитанции = Новый ТабличныйДокумент;
		Элементы.ПросмотрКвитанции.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ТекущийФайл <> Элементы.ФайлыДоверенности.ТекущиеДанные.Файл Тогда
		ТекущийФайл = Элементы.ФайлыДоверенности.ТекущиеДанные.Файл;
		ПодключитьОбработчикОжидания("ФайлыДоверенностиПослеАктивизацииСтроки", 0.1, Истина);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьСписокФайлов();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзРеестра(Команда)

	Ошибка = Неопределено;
	ТекущийФайл = Неопределено;
	ЗагрузитьИзРеестраНаСервере(Ошибка);

	Если Ошибка <> Неопределено Тогда
		КонтекстОшибки = МашиночитаемыеДоверенностиФНССлужебныйКлиент.КонтекстДляОбработкиОшибкиРР();
		КонтекстОшибки.ЗаголовокПредупреждения = Ошибка.ЗаголовокОшибки;
		КонтекстОшибки.Форма = ЭтотОбъект;
		МашиночитаемыеДоверенностиФНССлужебныйКлиент.ОбработатьОшибкуВзаимодействияРР(Ошибка, КонтекстОшибки);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ФайлыДоверенностиПослеАктивизацииСтроки()
	
	Элементы.ПросмотрКвитанции.Видимость = Истина;

	Если Элементы.ФайлыДоверенности.ТекущиеДанные.ЕстьПечатнаяФорма Тогда
		Массив = Новый Массив;
		Массив.Добавить(Элементы.ФайлыДоверенности.ТекущиеДанные.Файл);
		ПросмотрКвитанции = ПечатьКвитанции(Массив);
		Возврат;
	КонецЕсли;

	АдресДанных = Элементы.ФайлыДоверенности.ТекущиеДанные.АдресДанных;

	Если Не ЗначениеЗаполнено(АдресДанных) Тогда
		ПросмотрКвитанции = ОткрытьДляПросмотра(Элементы.ФайлыДоверенности.ТекущиеДанные.Файл);
		Возврат;
	КонецЕсли;

	Если НРег(Элементы.ФайлыДоверенности.ТекущиеДанные.Расширение) = "mxl" Тогда
		ПросмотрКвитанции = ВывестиТабличныйДокумент(Элементы.ФайлыДоверенности.ТекущиеДанные.АдресДанных);
	ИначеЕсли НРег(Элементы.ФайлыДоверенности.ТекущиеДанные.Расширение) = "xml" Или НРег(
		Элементы.ФайлыДоверенности.ТекущиеДанные.Расширение) = "txt" Тогда
		ПросмотрКвитанции = ВывестиТекст(Элементы.ФайлыДоверенности.ТекущиеДанные.АдресДанных);
	Иначе
		ПросмотрКвитанции = ОткрытьДляПросмотра(Элементы.ФайлыДоверенности.ТекущиеДанные.Файл);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокФайлов()

	ФайлыДоверенности.Очистить();

	РезультатЗапроса = МашиночитаемыеДоверенностиФНССлужебный.Квитанции(Доверенность, Ложь);
	Выборка = РезультатЗапроса.Выбрать();

	МассивФайлов = Новый Массив;

	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ФайлыДоверенности.Добавить();
		НоваяСтрока.Файл = Выборка.Ссылка;
		НоваяСтрока.Представление = Выборка.Представление;
		НоваяСтрока.Расширение = Выборка.Расширение;
		НоваяСтрока.ИндексКартинки = Выборка.ИндексКартинки;
		НоваяСтрока.ИмяФайла = Выборка.Наименование + "." + Выборка.Расширение;

		МассивФайлов.Добавить(Выборка.Ссылка);
	КонецЦикла;

	Если МассивФайлов.Количество() > 0 Тогда
		ДанныеФайлов = РаботаСФайлами.ДвоичныеДанныеФайлов(МассивФайлов, Ложь);
		Для Каждого КлючИЗначение Из ДанныеФайлов Цикл

			ДанныеФайла = КлючИЗначение.Значение;
			Строка = ФайлыДоверенности.НайтиСтроки(Новый Структура("Файл", КлючИЗначение.Ключ))[0];
			Строка.АдресДанных = ПоместитьВоВременноеХранилище(ДанныеФайла, УникальныйИдентификатор);
			Если Строка.Расширение = "xml" Тогда
				СвойстваФайла = СвойстваФайла(ДанныеФайла);
				Если ЗначениеЗаполнено(СвойстваФайла.Представление) Тогда
					Строка.Представление = СвойстваФайла.Представление;
				КонецЕсли;
				Строка.ТипФайла = СвойстваФайла.ТипФайла;
				Строка.ЕстьПечатнаяФорма = СвойстваФайла.ЕстьПечатнаяФорма;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СвойстваФайла(ДвоичныеДанные)

	СвойстваФайла = Новый Структура("ЕстьПечатнаяФорма, Представление, ТипФайла", Ложь);

	Поток = ДвоичныеДанные.ОткрытьПотокДляЧтения();

	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьПоток(Поток);

	Попытка
		Пока ЧтениеXML.Прочитать() Цикл
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				Если ЧтениеXML.ЛокальноеИмя = "Подтверждение" Тогда
					ПрочитатьПодтверждение(ЧтениеXML, СвойстваФайла);
				ИначеЕсли ЧтениеXML.ЛокальноеИмя = "Файл" Или ЧтениеXML.ЛокальноеИмя = "Документ" Тогда
					ПрочитатьФайл(ЧтениеXML, СвойстваФайла);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Исключение
		ЧтениеXML.Закрыть();
		Возврат СвойстваФайла;
	КонецПопытки;

	ЧтениеXML.Закрыть();
	Возврат СвойстваФайла;

КонецФункции

&НаСервере
Процедура ПрочитатьПодтверждение(ЧтениеXML, СвойстваФайла)
	ВерсияФормы = ЧтениеXML.ПолучитьАтрибут("ВерсФорм"); // АПК:1036 внешний идентификатор
	СвойстваФайла.ТипФайла = ВерсияФормы;
	Если ВерсияФормы = "PODTVOPER_1" Тогда // АПК:1036 внешний идентификатор

		ОбъектXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
		Если ОбъектXDTO.ТипПодтв = "ПодтвДов" Тогда // АПК:1036 внешний идентификатор
			СвойстваФайла.Представление = ЗаголовокПодтвержденияДоверенности();
		ИначеЕсли ОбъектXDTO.ТипПодтв = "ПодтвОтм" Тогда // АПК:1036 внешний идентификатор
			СвойстваФайла.Представление = ЗаголовокПодтвержденияОтмены();
		КонецЕсли;
		СвойстваФайла.ЕстьПечатнаяФорма = Истина;

	ИначеЕсли ВерсияФормы = "ПодтвФНС003" Тогда // АПК:1036 внешний идентификатор

		ОбъектXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
		Если ОбъектXDTO.ТипПодтв = "ПодтвЗаявлОтмена" Тогда // АПК:1036 внешний идентификатор
			СвойстваФайла.Представление = ЗаголовокПодтвержденияОтменыФНС();
		Иначе
			СвойстваФайла.Представление = ЗаголовокПодтвержденияДоверенностиФНС();
		КонецЕсли;
		СвойстваФайла.ЕстьПечатнаяФорма = Истина;

	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаголовокПодтвержденияДоверенности()
	Возврат НСтр("ru='Подтверждение оператора о факте поступления доверенности'");
КонецФункции

&НаСервереБезКонтекста
Функция ЗаголовокПодтвержденияДоверенностиФНС()
	Возврат НСтр("ru='Подтверждение ЦПРР ФНС о факте поступления доверенности'");
КонецФункции

&НаСервереБезКонтекста
Функция ЗаголовокПодтвержденияОтмены()
	Возврат НСтр("ru='Подтверждение оператора о факте поступления заявления на отмену доверенности'");
КонецФункции

&НаСервереБезКонтекста
Функция ЗаголовокПодтвержденияОтменыФНС()
	Возврат НСтр("ru='Подтверждение ЦПРР ФНС о факте поступления заявления на отмену'");
КонецФункции

&НаСервере
Процедура ПрочитатьФайл(ЧтениеXML, СвойстваФайла)

	ВерсияФормы = ЧтениеXML.ПолучитьАтрибут("ВерсФорм"); // АПК:1036 внешний идентификатор
	ВерсияПрограммы = ЧтениеXML.ПолучитьАтрибут("ВерсПрог"); // АПК:1036 внешний идентификатор
	Если ВерсияФормы = "000" Тогда
		ОбъектXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
		Документ = ОбъектXDTO.Документ;
		НаименованиеДокумента = Документ.СвКвит.НаимВидДок; // АПК:1036 внешний идентификатор
		СвойстваФайла.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Квитанция: %1'"), НаименованиеДокумента);
		СвойстваФайла.ЕстьПечатнаяФорма = Истина;
	ИначеЕсли ВерсияФормы = "001" Тогда
		СвойстваФайла.Представление = НСтр("ru='Заявление на отмену'");
		СвойстваФайла.ЕстьПечатнаяФорма = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеФайла(Файл, УникальныйИдентификатор)
	
	ПараметрыДанныхФайла = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ПараметрыДанныхФайла.ПолучатьСсылкуНаДвоичныеДанные = Истина;
	ПараметрыДанныхФайла.ИдентификаторФормы = УникальныйИдентификатор;
	
	Возврат РаботаСФайлами.ДанныеФайла(Файл, ПараметрыДанныхФайла);
	
КонецФункции

&НаСервере
Процедура ЗагрузитьИзРеестраНаСервере(ОшибкаПриЗагрузке)
	
	МашиночитаемыеДоверенностиФНССлужебный.ЗагрузитьКвитанцииИзРеестра(Доверенность, ОшибкаПриЗагрузке, УникальныйИдентификатор);
	ОбновитьСписокФайлов();

КонецПроцедуры

&НаСервере
Функция ПечатьКвитанции(ФайлыКвитанций)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "МашиночитаемыеДоверенности_Квитанции";
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.МашиночитаемыеДоверенности.ПФ_MXL_Квитанция");

	Для Каждого Ссылка Из ФайлыКвитанций Цикл

		ДанныеФайла = РаботаСФайлами.ДанныеФайла(Ссылка);
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла);

		КвитанцияXDTO = КвитанцияXDTO(ДвоичныеДанные);

		ВывестиКвитанцию(ТабличныйДокумент, Макет, КвитанцияXDTO);

		Штампы = ЭлектроннаяПодпись.ШтампыВизуализацииЭлектронныхПодписей(Ссылка);
		ЭлектроннаяПодпись.ДобавитьШтампыВТабличныйДокумент(ТабличныйДокумент, Штампы);

	КонецЦикла;

	ТабличныйДокумент.ТолькоПросмотр = Истина;

	Возврат ТабличныйДокумент;

КонецФункции

&НаСервере
Функция КвитанцияXDTO(ДвоичныеДанные)

	Поток = ДвоичныеДанные.ОткрытьПотокДляЧтения();
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьПоток(Поток);
	ЧтениеXML.Прочитать();
	КвитанцияXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML);
	ЧтениеXML.Закрыть();

	Возврат КвитанцияXDTO;

КонецФункции

&НаСервереБезКонтекста
Функция ВывестиТабличныйДокумент(АдресДанных)

	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресДанных);
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Поток = ДвоичныеДанные.ОткрытьПотокДляЧтения();
	ТабличныйДокумент.Прочитать(Поток);
	Возврат ТабличныйДокумент;

КонецФункции

&НаСервереБезКонтекста
Функция ВывестиТекст(АдресДанных)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресДанных);
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	Поток = ДвоичныеДанные.ОткрытьПотокДляЧтения();
	ТекстовыйДокумент.Прочитать(Поток);
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.МашиночитаемыеДоверенности.ПФ_MXL_Квитанция");
	ВывестиОбласть(ТабличныйДокумент, Макет, "Текст", Новый Структура("Значение", ТекстовыйДокумент.ПолучитьТекст()));
	Возврат ТабличныйДокумент;

КонецФункции


&НаСервереБезКонтекста
Функция ОткрытьДляПросмотра(Файл)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.МашиночитаемыеДоверенности.ПФ_MXL_Квитанция");
	ВывестиОбласть(ТабличныйДокумент, Макет, "ОткрытьДляПросмотра", Новый Структура("Файл", Файл));
	Возврат ТабличныйДокумент;
	
КонецФункции

&НаСервере
Процедура ВывестиКвитанцию(ТабличныйДокумент, Макет, КвитанцияXDTO)

	ЗаголовкиПолей = ЗаголовкиПолей();
	СписокПолей = ПоследовательностьПолей();

	ЗаполнитьСписокСвойствКвитанции(КвитанцияXDTO, СписокПолей, ЗаголовкиПолей, "");

	Для Каждого Элемент Из СписокПолей Цикл
		Если Не ЗначениеЗаполнено(Элемент.Представление) Тогда
			Продолжить;
		КонецЕсли;
		РеквизитыСтроки = Новый Структура;
		РеквизитыСтроки.Вставить("Значение", Элемент.Представление);

		Если Элемент.Значение = "ВывестиЗаголовок" Тогда
			ВывестиОбласть(ТабличныйДокумент, Макет, "Заголовок", РеквизитыСтроки);
		Иначе
			РеквизитыСтроки.Вставить("Наименование", Элемент.Значение);
			ВывестиОбласть(ТабличныйДокумент, Макет, "Строка", РеквизитыСтроки);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокСвойствКвитанции(ОбъектXDTO, СписокПолей, ЗаголовкиПолей, СвойствоРодитель)
	
	Для Каждого Свойство Из ОбъектXDTO.Свойства() Цикл

		ИмяСвойства = НРег(Свойство.Имя);
		СоставноеИмя = ?(СвойствоРодитель = "", "", СвойствоРодитель + ".") + ИмяСвойства;

		Если ИмяСвойства = НРег("ВерсФорм") Тогда // АПК:1036 внешний идентификатор
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ОбъектXDTO.Получить(Свойство)) = Тип("ОбъектXDTO") Тогда
			ЗаголовокПоля = ЗаголовкиПолей[ИмяСвойства];
			Если ЗаголовокПоля <> Неопределено Тогда
				Найдено = СписокПолей.НайтиПоЗначению(ИмяСвойства);
				Найдено.Значение = "ВывестиЗаголовок";
				Найдено.Представление = ЗаголовокПоля;
			КонецЕсли;
			ЗаполнитьСписокСвойствКвитанции(ОбъектXDTO[Свойство.Имя], СписокПолей, ЗаголовкиПолей, СоставноеИмя);
			Продолжить;
		КонецЕсли;

		Если ИмяСвойства = НРег("ИдФайл") И СвойствоРодитель = НРег("СвДовер") Тогда // АПК:1036 внешний идентификатор
			ИмяСвойства = НРег("ИдФайлД"); // АПК:1036 внешний идентификатор
		КонецЕсли;

		ЗаголовокПоля = ЗаголовкиПолей[ИмяСвойства];
		Если ЗаголовокПоля <> Неопределено Тогда

			ЗначениеПоля = ОбъектXDTO[Свойство.Имя];

			Если ИмяСвойства = НРег("ТипПодтв") Тогда // АПК:1036 внешний идентификатор
				НовоеЗначениеПоля = ЗаголовкиПолей[НРег(ЗначениеПоля)];
				Если НовоеЗначениеПоля <> Неопределено Тогда
					ЗначениеПоля = НовоеЗначениеПоля;
				КонецЕсли;
				ЗаголовокПоля = "ВывестиЗаголовок";
			ИначеЕсли ИмяСвойства = НРег("НаимВидДок") Тогда // АПК:1036 внешний идентификатор
				ЗаголовокПоля = "ВывестиЗаголовок";
			КонецЕсли;

			Найдено = СписокПолей.НайтиПоЗначению(НРег(СоставноеИмя));
			Если Найдено = Неопределено Тогда
				СписокПолей.Добавить(ЗаголовокПоля, ЗначениеПоля);
				Продолжить;
			КонецЕсли;
			Найдено.Значение = ЗаголовокПоля;
			Найдено.Представление = ЗначениеПоля;

		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаголовкиПолей()
	
	// АПК:1036-выкл не проверять текст на орфографию
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(НРег("УрлПродукт"), НСтр(
		"ru='Адрес сайта юридического лица, содержащий сведения о его продукте доверенности'"));
	Соответствие.Вставить(НРег("URLПродуктОпер"), НСтр(
		"ru='Адрес сайта юридического лица, содержащий сведения о его продукте доверенности'"));
	Соответствие.Вставить(НРег("АдрЭлПочт"), НСтр(
		"ru='Адрес электронной почты технической поддержки клиентов юридического лица'"));
	Соответствие.Вставить(НРег("ВерсФорм"), НСтр("ru='Версия формата'"));
	Соответствие.Вставить(НРег("ВнНомДовер"), НСтр("ru='Внутренний номер доверенности'"));
	Соответствие.Вставить(НРег("ВнНомПодтв"), НСтр("ru='Внутренний номер подтверждения'"));
	Соответствие.Вставить(НРег("ДатаПостДок"), НСтр("ru='Дата и время поступления документа'"));
	Соответствие.Вставить(НРег("ДатаПодтв"), НСтр("ru='Дата и время формирования подтверждения'"));
	Соответствие.Вставить(НРег("ДатаВыдДовер"), НСтр("ru='Дата совершения (выдачи) доверенности'"));
	Соответствие.Вставить(НРег("НомДовер"), НСтр("ru='Единый регистрационный номер доверенности'"));
	Соответствие.Вставить(НРег("НомПодтв"), НСтр("ru='Единый регистрационный номер подтверждения'"));
	Соответствие.Вставить(НРег("ИдФайлД"), НСтр("ru='Идентификатор файла доверенности'"));
	Соответствие.Вставить(НРег("ИдФайл"), НСтр("ru='Идентификатор файла'"));
	Соответствие.Вставить(НРег("ИННЮЛ"), НСтр("ru='ИНН юридического лица'"));
	Соответствие.Вставить(НРег("ИнСвед1"), НСтр("ru='Иные сведения'"));
	Соответствие.Вставить(НРег("ИнСвед2"), НСтр("ru='Иные сведения'"));
	Соответствие.Вставить(НРег("КНД"), НСтр("ru='Код формы по КНД'"));
	Соответствие.Вставить(НРег("ВерсПрог"), НСтр(
		"ru='Наименование и версия программы (сервиса), с помощью которой сформировано подтверждение'"));
	Соответствие.Вставить(НРег("НаимЮЛ"), НСтр("ru='Наименование юридического лица'"));
	Соответствие.Вставить(НРег("ОГРН"), НСтр("ru='ОГРН'"));
	Соответствие.Вставить(НРег("ПричОтз"), НСтр("ru='Причина отзыва доверенности'"));
	Соответствие.Вставить(НРег("СрокДейст"), НСтр("ru='Срок действия доверенности'"));
	Соответствие.Вставить(НРег("ТипПодтв"), НСтр("ru='Тип подтверждения'"));
	Соответствие.Вставить(НРег("ТоргНаим"), НСтр("ru='Торговое (публичное) наименование юридического лица'"));
	Соответствие.Вставить(НРег("НаимОрг"), НСтр("ru='Наименование юридического лица'"));
	Соответствие.Вставить(НРег("ИдЮЛ"), НСтр("ru='Уникальный идентификатор юридического лица'"));
	Соответствие.Вставить(НРег("Хэш"), НСтр("ru='Хэш на заявление'"));

	Соответствие.Вставить(НРег("ДатаВремВыдПодтв"), НСтр("ru='Дата и время формирования подтверждения'"));

	Соответствие.Вставить(НРег("НаимВидДок"), НСтр("ru='Наименование документа'"));
	Соответствие.Вставить(НРег("РегНом"), НСтр("ru='Номер документа'"));
	Соответствие.Вставить(НРег("ДатаПост"), НСтр("ru='Дата поступления'"));
	Соответствие.Вставить(НРег("ВремПост"), НСтр("ru='Время поступления'"));
	Соответствие.Вставить(НРег("ИмяПринятФайла"), НСтр("ru='Имя принятого файла'"));
	Соответствие.Вставить(НРег("КПП"), НСтр("ru='КПП юридического лица'"));
	
	Соответствие.Вставить(НРег("СвДовер"), НСтр("ru='Сведения о доверенности'"));
	Соответствие.Вставить(НРег("СвЗаявлОтм"), НСтр("ru='Сведения об отмене'"));
	Соответствие.Вставить(НРег("СвЗаяв"), НСтр("ru='Заявление на отмену'"));
	Соответствие.Вставить(НРег("СвЮЛПодтв"), НСтр("ru='Сведения об операторе, сформировавшем подтверждение'"));
	Соответствие.Вставить(НРег("СведОИС"), НСтр("ru='Сведения об операторе, сформировавшем подтверждение'"));
	
	Соответствие.Вставить(НРег("ПодтвДов"), ЗаголовокПодтвержденияДоверенности());
	Соответствие.Вставить(НРег("ПодтвДовер"), ЗаголовокПодтвержденияДоверенностиФНС());
	Соответствие.Вставить(НРег("ПодтвОтм"), ЗаголовокПодтвержденияОтмены());
	Соответствие.Вставить(НРег("ПодтвЗаявлОтмена"), ЗаголовокПодтвержденияОтменыФНС());
	
	// АПК:1036-вкл не проверять текст на орфографию
	
	Возврат Соответствие;
КонецФункции

&НаСервереБезКонтекста
Функция ПоследовательностьПолей()

	// АПК:1036-выкл не проверять текст на орфографию
	
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(НРег("КНД"));
	СписокЗначений.Добавить(НРег("ТипПодтв"));
	СписокЗначений.Добавить(НРег("НомПодтв"));
	СписокЗначений.Добавить(НРег("ВнНомПодтв"));
	СписокЗначений.Добавить(НРег("ДатаПодтв"));
	СписокЗначений.Добавить(НРег("ДатаВремВыдПодтв"));
	СписокЗначений.Добавить(НРег("ДатаПостДок"));
	СписокЗначений.Добавить(НРег("ИдФайл"));
	СписокЗначений.Добавить(НРег("СвЗаяв"));

	СписокЗначений.Добавить(НРег("СведОИС"));
	СписокЗначений.Добавить(НРег("СведОИС.НаимЮЛ"));
	СписокЗначений.Добавить(НРег("СведОИС.ТоргНаим"));
	СписокЗначений.Добавить(НРег("СведОИС.ИННЮЛ"));
	СписокЗначений.Добавить(НРег("СведОИС.ОГРН"));
	СписокЗначений.Добавить(НРег("СведОИС.АдрЭлПочт"));
	СписокЗначений.Добавить(НРег("СведОИС.ИдЮЛ"));
	СписокЗначений.Добавить(НРег("СведОИС.ВерсПрог"));
	СписокЗначений.Добавить(НРег("СведОИС.УрлПродукт"));

	СписокЗначений.Добавить(НРег("СвЮЛПодтв"));
	СписокЗначений.Добавить(НРег("СвЮЛПодтв.НаимОрг"));
	СписокЗначений.Добавить(НРег("СвЮЛПодтв.ТоргНаим"));
	СписокЗначений.Добавить(НРег("СвЮЛПодтв.ИННЮЛ"));
	СписокЗначений.Добавить(НРег("СвЮЛПодтв.ОГРН"));
	СписокЗначений.Добавить(НРег("СвЮЛПодтв.ИдЮЛ"));
	СписокЗначений.Добавить(НРег("СвЮЛПодтв.ВерсПрог"));
	СписокЗначений.Добавить(НРег("СвЮЛПодтв.URLПродуктОпер"));
	
	// ЦПРР 000
	СписокЗначений.Добавить(НРег("Документ.СвКвит.НаимВидДок"));
	СписокЗначений.Добавить(НРег("Документ.СвКвит.РегНом"));
	СписокЗначений.Добавить(НРег("Документ.СвКвит.ИмяПринятФайла"));
	СписокЗначений.Добавить(НРег("Документ.СвКвит.ДатаПост"));
	СписокЗначений.Добавить(НРег("Документ.СвКвит.ВремПост"));
	СписокЗначений.Добавить(НРег("Документ.СвОтпрДок.СвРосОрг.НаимОрг"));
	СписокЗначений.Добавить(НРег("Документ.СвОтпрДок.СвРосОрг.ИННЮЛ"));
	СписокЗначений.Добавить(НРег("Документ.СвОтпрДок.СвРосОрг.КПП"));

	СписокЗначений.Добавить(НРег("СвДовер"));
	СписокЗначений.Добавить(НРег("СвДовер.НомДовер"));
	СписокЗначений.Добавить(НРег("СвДовер.ДатаВыдДовер"));
	СписокЗначений.Добавить(НРег("СвДовер.ИдФайлД"));
	СписокЗначений.Добавить(НРег("СвДовер.СведСист"));
	СписокЗначений.Добавить(НРег("СвДовер.СрокДейст"));
	СписокЗначений.Добавить(НРег("СвДовер.ВнНомДовер"));

	СписокЗначений.Добавить(НРег("СвЗаявлОтм"));
	СписокЗначений.Добавить(НРег("СвЗаявлОтм.НомДовер"));
	СписокЗначений.Добавить(НРег("СвЗаявлОтм.ПричОтз"));
	СписокЗначений.Добавить(НРег("СвЗаявлОтм.Хэш"));

	СписокЗначений.Добавить(НРег("НаимЮЛ"));
	СписокЗначений.Добавить(НРег("ТоргНаим"));

	СписокЗначений.Добавить(НРег("ОГРН"));
	СписокЗначений.Добавить(НРег("ИдЮЛ"));
	СписокЗначений.Добавить(НРег("ИННЮЛ"));

	СписокЗначений.Добавить(НРег("ПричОтз"));
	СписокЗначений.Добавить(НРег("ИнСвед1"));
	СписокЗначений.Добавить(НРег("ИнСвед2"));
	СписокЗначений.Добавить(НРег("Хэш"));
	
	// АПК:1036-вкл не проверять текст на орфографию
	
	Возврат СписокЗначений;
КонецФункции

&НаСервереБезКонтекста
Процедура ВывестиОбласть(ТабличныйДокумент, Макет, ИмяОбласти, Параметры = Неопределено)

	ОбластьМакета = Макет.ПолучитьОбласть(ИмяОбласти);
	Если ЗначениеЗаполнено(Параметры) Тогда
		ОбластьМакета.Параметры.Заполнить(Параметры);
	КонецЕсли;
	ТабличныйДокумент.Вывести(ОбластьМакета);

КонецПроцедуры

#КонецОбласти