///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка);
	
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	Если Параметры.МножественныйВыбор <> Неопределено Тогда
		Элементы.Список.МножественныйВыбор = Параметры.МножественныйВыбор;
	КонецЕсли;
	Если Параметры.РежимВыбора И Не ЗначениеЗаполнено(Параметры.КлючПользовательскихНастроек) Тогда
		Параметры.КлючПользовательскихНастроек = "РежимВыбора";
		Список.АвтоматическоеСохранениеПользовательскихНастроек = Ложь;
	КонецЕсли;
	
	Заголовок = НСтр("ru='Машиночитаемые доверенности'");
	АвторизованныйПользователь = Пользователи.АвторизованныйПользователь();
	ДобавитьПолеОтбора("Доверитель", "ТипДоверителя");
	ДобавитьПолеОтбора("Представитель", "ТипПредставителя");
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ОтборПоСостоянию, ТолькоДействующие, СертификатПредставителя, СертификатДоверителя");
	ДоверенностиПоРоли = ?(ЗначениеЗаполнено(Параметры.ДоверенностиПоРоли), Параметры.ДоверенностиПоРоли, Элементы.ДоверенностиПоРоли.СписокВыбора[0].Значение);
	Доверитель = ?(ЗначениеЗаполнено(Параметры.Доверитель), Параметры.Доверитель, Доверитель);
	Представитель = ?(ЗначениеЗаполнено(Параметры.Представитель), Параметры.Представитель, Представитель);
	Полномочия = ?(ЗначениеЗаполнено(Параметры.Полномочия), Параметры.Полномочия, Полномочия);
	
	Если ТипЗнч(СертификатПредставителя) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		Сертификат = СертификатПредставителя;
	ИначеЕсли ТипЗнч(СертификатПредставителя) = Тип("ДвоичныеДанные") Или ЭтоАдресВременногоХранилища(СертификатПредставителя) Тогда
		СвойстваСертификата = ЭлектроннаяПодпись.СвойстваСертификата(СертификатПредставителя);
		Сертификат = СвойстваСертификата.Представление;
	Иначе
		Сертификат = Неопределено;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = КоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ДополнительныеКоманды;
	ПараметрыРазмещения.ПрефиксГрупп = "КонтекстноеМеню";
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ЕстьПравоДобавления = ПравоДоступа("Добавление", Метаданные.Справочники.МашиночитаемыеДоверенности);
	
	Элементы.СписокЗагрузитьИзФайла.Видимость = ЕстьПравоДобавления;
	Элементы.СписокЗагрузитьИзРеестраФНС.Видимость = ЕстьПравоДобавления;
	Элементы.СписокПолучитьСтатус.Видимость = ПравоДоступа("Изменение", Метаданные.Справочники.МашиночитаемыеДоверенности);
	
	Настройки = МашиночитаемыеДоверенностиФНССлужебный.НастройкиПодсистемы();
	
	Элементы.ГруппаСтатусВДругомРеестре.Видимость = Настройки.ПоказыватьСтатусВФормеСписка;
	
	Если Настройки.ПоказыватьСтатусВФормеСписка Тогда
		
		Если ЗначениеЗаполнено(Настройки.ЗаголовокКолонкиСтатуса) Тогда
			Элементы.СтатусВДругомРеестре.Заголовок = Настройки.ЗаголовокКолонкиСтатуса;
		Иначе
			Элементы.СтатусВДругомРеестре.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		КонецЕсли;
		Если Настройки.КартинкаКолонкиСтатуса <> Неопределено Тогда
			Элементы.СтатусВДругомРеестре.КартинкаШапки = Настройки.КартинкаКолонкиСтатуса;
		КонецЕсли;
		Если Настройки.КоллекцияКартинокДляСтроки <> Неопределено Тогда
			Элементы.ИндексСтатусаВДругомРеестре.КартинкаЗначений = Настройки.КоллекцияКартинокДляСтроки;
		Иначе
			Элементы.ИндексСтатусаВДругомРеестре.Видимость = Ложь;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "СтатусВДругомРеестре", "");
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "СтатусВДругомРеестре", "НетСтатусов");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьОтбор();
	УстановитьПредставлениеПолномочий();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТолькоДействующиеПриИзменении(Элемент)
	УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура ПредставительПриИзменении(Элемент)
	УстановитьОтбор();
КонецПроцедуры 

&НаКлиенте
Процедура ДоверительПриИзменении(Элемент)
	УстановитьОтбор();
КонецПроцедуры 

&НаКлиенте
Процедура ДоверенностиПоРолиПриИзменении(Элемент)
	УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура ТипДоверителяПриИзменении(Элемент)
	УстановитьТип("Доверитель", "ТипДоверителя");
КонецПроцедуры

&НаКлиенте
Процедура ТипПредставителяПриИзменении(Элемент)
	УстановитьТип("Представитель", "ТипПредставителя");
КонецПроцедуры

&НаКлиенте
Процедура ДоверительОчистка(СтандартнаяОбработка)
	ОчисткаОтбора("Доверитель", "ТипДоверителя");
КонецПроцедуры

&НаКлиенте
Процедура ПредставительОчистка(СтандартнаяОбработка)
	ОчисткаОтбора("Представитель", "ТипПредставителя");
КонецПроцедуры

&НаКлиенте
Процедура ПолномочияПриИзменении(Элемент)
	УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура ПолномочияНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораПолномочий", ЭтотОбъект);
	
	СтруктураДанных = Новый Структура("ПолномочияВТекстовомВиде, ТекстПолномочий, Полномочия", Ложь);
	Если ТипЗнч(Полномочия) = Тип("СписокЗначений") Тогда
		МассивПолномочий = Полномочия.ВыгрузитьЗначения();
	Иначе
		МассивПолномочий = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Полномочия);
	КонецЕсли;
	
	СтруктураДанных.Полномочия = ПоместитьВоВременноеХранилище(МассивПолномочий, УникальныйИдентификатор);
	
	ПараметрыОткрытия = Новый Структура("ТолькоПросмотр, ВыбранныеПолномочия, РежимКлассификатора", Ложь, СтруктураДанных, Истина);

	ОткрытьФорму(
		"Справочник.МашиночитаемыеДоверенности.Форма.ФормаВводаПолномочий",
		ПараметрыОткрытия,
		ЭтотОбъект,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура СертификатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДляШифрованияИРасшифровки", Ложь);
	ПараметрыФормы.Вставить("ВыбранныйСертификат", СертификатПредставителя);
	ПараметрыФормы.Вставить("ВыполнятьНаСервере");
	ПараметрыФормы.Вставить("РежимВыбораСсылки", Истина);
	
	ОбработчикЗавершения = Новый ОписаниеОповещения("СертификатОкончаниеВыбора", ЭтотОбъект);
	МодульЭлектроннаяПодписьСлужебныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ЭлектроннаяПодписьСлужебныйКлиент");
	МодульЭлектроннаяПодписьСлужебныйКлиент.ВыборСертификатаДляПодписанияИлиРасшифровки(ПараметрыФормы, Элемент, ОбработчикЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатОчистка(Элемент, СтандартнаяОбработка)
	СертификатПредставителя = Неопределено;
	УстановитьОтбор();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)

	Если Настройки.ПараметрыДанных.Элементы.Найти("СтатусВДругомРеестре").Значение = "НетСтатусов" Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьКолонкаСтатус = Ложь;
	ЕстьКолонкаИндекс = Ложь;
	
	Для Каждого Строка Из Строки Цикл
		ЕстьКолонкаСтатус = Строка.Значение.Данные.Свойство("СтатусВДругомРеестре");
		ЕстьКолонкаИндекс = Строка.Значение.Данные.Свойство("ИндексСтатусаВДругомРеестре");
		Прервать;
	КонецЦикла;
	
	Если Не ЕстьКолонкаСтатус И Не ЕстьКолонкаИндекс Тогда
		Возврат;
	КонецЕсли;
	
	Доверенности = Новый Соответствие;
	МассивДоверенностей = Строки.ПолучитьКлючи();
	
	МашиночитаемыеДоверенностиФНСПереопределяемый.ПриПолученииСтатусов(МассивДоверенностей, Доверенности);
	
	Если Доверенности.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из Строки Цикл
		
		Статус = Доверенности[Строка.Значение.Данные.Ссылка];
		Если Статус = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ЕстьКолонкаСтатус Тогда
			Строка.Значение.Данные.СтатусВДругомРеестре = Статус.Статус;
		КонецЕсли;
		Если ЕстьКолонкаИндекс Тогда
			Строка.Значение.Данные.ИндексСтатусаВДругомРеестре = Статус.ИндексКартинки;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыгрузитьВФайл(Команда)
	МашиночитаемыеДоверенностиФНССлужебныйКлиент.ВыгрузитьДоверенностиВФайлы(Элементы.Список.ВыделенныеСтроки);
КонецПроцедуры  

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	РезультатАвторизации = АвторизоватьсяНаСервереМЧДРР();
	
	Если ЗначениеЗаполнено(РезультатАвторизации.Ошибка) Тогда
		
		ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ПараметрыВопроса.Картинка = БиблиотекаКартинок.ДиалогВосклицание;
		ПараметрыВопроса.Заголовок = НСтр("ru = 'Загрузка доверенности из файла'");
		ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить("Загрузить",  НСтр("ru = 'Загрузить доверенность'"));
		Кнопки.Добавить("Отменить", НСтр("ru = 'Отменить загрузку'"));
		Кнопки.Добавить("ПоказатьОшибку",      НСтр("ru = 'Подробности...'"));
		
		ТекстОшибки = НСтр("ru = 'Нет возможности проверить доверенность в распределенном реестре, доверенность будет загружена без признака ""Зарегистрирована в реестре ФНС""'");
		
		Оповещение = Новый ОписаниеОповещения("ПослеОтветаНаВопросОЗагрузкеДоверенности", ЭтотОбъект, РезультатАвторизации.Ошибка);
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ТекстОшибки, Кнопки, ПараметрыВопроса);
		
		Возврат;
	КонецЕсли;
	
	ПослеОтветаНаВопросОЗагрузкеДоверенности(Новый Структура("Значение", "Загрузить"), Неопределено);
	
КонецПроцедуры  

&НаСервереБезКонтекста
Функция АвторизоватьсяНаСервереМЧДРР()
	Возврат МашиночитаемыеДоверенностиФНССлужебный.АвторизоватьсяНаСервереМЧДРР();
КонецФункции

&НаКлиенте
Процедура ЗагрузитьИзРеестраФНС(Команда)
	ПослеЗагрузкиДоверенности = Новый ОписаниеОповещения("ПослеЗагрузкиДоверенности", ЭтотОбъект);
	МашиночитаемыеДоверенностиФНССлужебныйКлиент.ПолучитьДоверенностьИзРеестраФНС(ПослеЗагрузкиДоверенности);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусИзРеестраФНС(Команда)
	ДлительнаяОперация = ПолучитьСтатусНаСервере();
	Контекст = Новый Структура("ВыделенныеСтроки", Элементы.Список.ВыделенныеСтроки);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеПолученияСтатуса", ЭтотОбъект, Контекст);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	
	ТекстСообщения = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
		НСтр("ru=';Запрашивается статус для %1 документа;;Запрашивается статус для %1 документов;Запрашивается статус для %1 документов;|Запрашивается статус для %1 документов'"), 
		Контекст.ВыделенныеСтроки.Количество(),, "ЧДЦ=0");
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Обновление статусов из Реестра ФНС'"),
		,
		ТекстСообщения,
		БиблиотекаКартинок.ДиалогИнформация,
		СтатусОповещенияПользователя.Информация);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеОтветаНаВопросОЗагрузкеДоверенности(Результат, Ошибка) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Значение = "ПоказатьОшибку" Тогда
		
		КонтекстОшибки = МашиночитаемыеДоверенностиФНССлужебныйКлиент.КонтекстДляОбработкиОшибкиРР();
		КонтекстОшибки.ЗаголовокПредупреждения = Ошибка.ЗаголовокОшибки;
		КонтекстОшибки.Форма = ЭтотОбъект;
		МашиночитаемыеДоверенностиФНССлужебныйКлиент.ОбработатьОшибкуВзаимодействияРР(Ошибка, КонтекстОшибки);
		
		Возврат;
	
	КонецЕсли;

	Если Результат.Значение <> "Загрузить" Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Загрузка доверенности из файла'");
	ПараметрыЗагрузки.Диалог.Фильтр = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Файлы доверенностей (%1)|%1'"), "*.xml;*.p7s;*.sig;*.zip");
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗагрузкеФайлов", ЭтотОбъект);
	ФайловаяСистемаКлиент.ЗагрузитьФайлы(ОписаниеОповещения, ПараметрыЗагрузки);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(Знач ПараметрыВыполнения)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура УстановитьОтбор()
	
	Если ЗначениеЗаполнено(Параметры.НаДату) Тогда
		ТекущаяДатаСеанса = КонецДня(Параметры.НаДату);
	Иначе
		ТекущаяДатаСеанса = КонецДня(ОбщегоНазначенияКлиент.ДатаСеанса());
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Действует", Истина, ВидСравненияКомпоновкиДанных.Равно,,ТолькоДействующие);
	УстановитьОтборСоСтрокойПоиска(Доверитель, "Доверитель");
	УстановитьОтборСоСтрокойПоиска(Представитель, "Представитель");
	УстановитьОтборСоСтрокойПоиска(Полномочия, "Полномочия");
	УстановитьОтборПоРоли();
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТекущаяДата", ТекущаяДатаСеанса);
	
	Если ОтборПоСостоянию = "СИстекающимСрокомДействия" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ДатаОкончания", ТекущаяДатаСеанса, ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(Список.Отбор, "ДатаОкончания", ВидСравненияКомпоновкиДанных.МеньшеИлиРавно, 
			МашиночитаемыеДоверенностиФНССлужебныйКлиентСервер.ДатаОкончанияПериодаИстекающихДоверенностей(ТекущаяДатаСеанса));
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Статус", ПредопределенноеЗначение("Перечисление.СтатусыМЧД.Действует"), ВидСравненияКомпоновкиДанных.Равно);
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (с истекающим сроком)'"), Заголовок);
	ИначеЕсли ОтборПоСостоянию = "ТребуютВнимания" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ДатаОкончания", ТекущаяДатаСеанса, ВидСравненияКомпоновкиДанных.Больше);
		МассивСтатусов = Новый Массив;
		МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.ТехническиеСтатусыМЧД.ОшибкаРегистрации"));
		МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.ТехническиеСтатусыМЧД.Регистрация"));
		МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.ТехническиеСтатусыМЧД.РегистрацияОтмены"));
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ТехническийСтатус", МассивСтатусов, ВидСравненияКомпоновкиДанных.ВСписке);
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (требуют внимания)'"), Заголовок);
	ИначеЕсли ОтборПоСостоянию = "ОжидаютПодписания" Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ДатаОкончания", ТекущаяДатаСеанса, ВидСравненияКомпоновкиДанных.Больше);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Статус", ПредопределенноеЗначение("Перечисление.СтатусыМЧД.Черновик"), ВидСравненияКомпоновкиДанных.Равно);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Подписана", Ложь, ВидСравненияКомпоновкиДанных.Равно);
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 (ожидают подписания)'"), Заголовок);
	КонецЕсли;
	
	ВсеОтборыПоСертификатам = ВсеОтборыПоСертификатам();
		
	НастроитьОтборыСертификата(ВсеОтборыПоСертификатам, СертификатПредставителя, Истина);
	НастроитьОтборыСертификата(ВсеОтборыПоСертификатам, СертификатДоверителя, Ложь);
		
	Для Каждого ЭлементОтбора Из ВсеОтборыПоСертификатам Цикл
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, ЭлементОтбора.Ключ, ЭлементОтбора.Значение, ЭлементОтбора.Значение <> Неопределено);
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "СрокИстекающихДоверенностей",
		МашиночитаемыеДоверенностиФНССлужебныйКлиентСервер.ДатаОкончанияПериодаИстекающихДоверенностей(ТекущаяДатаСеанса));
КонецПроцедуры    

&НаКлиенте
Процедура УстановитьОтборСоСтрокойПоиска(Значение, ИмяПоля) 
	Отбор = Новый Структура("Значение,СтрокаПоиска");
	Если ЗначениеЗаполнено(Значение) Тогда
		Если ТипЗнч(Значение) = Тип("Строка") Тогда
			Отбор.СтрокаПоиска = "%"+СформироватьСтрокуДляПоискаВЗапросе(Значение)+"%"; 
		Иначе
			Отбор.Значение = Значение;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ЭлементОтбора Из Отбор Цикл
		ИмяПоляОтбора = ?(ЭлементОтбора.Ключ = "Значение", ИмяПоля, ИмяПоля+ЭлементОтбора.Ключ);
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, ИмяПоляОтбора, ЭлементОтбора.Значение, ЗначениеЗаполнено(ЭлементОтбора.Значение));
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоРоли()
	ИспользоватьОтборПоПредставителю = ДоверенностиПоРоли = "Представитель";
	ИспользоватьОтборПоДоверителю = ДоверенностиПоРоли = "Доверитель";
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ПредставительПользователь", АвторизованныйПользователь, ИспользоватьОтборПоПредставителю);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ДоверительПользователь", АвторизованныйПользователь, ИспользоватьОтборПоДоверителю);
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьСтрокуДляПоискаВЗапросе(Знач Значение)
	Возврат ОбщегоНазначения.СформироватьСтрокуДляПоискаВЗапросе(Значение);
КонецФункции  

&НаКлиенте
Процедура ПослеЗагрузкиДоверенности(Результат, Контекст) Экспорт
	
	Если Результат = Неопределено Или Не ЗначениеЗаполнено(Результат.Доверенность) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Список.Обновить();
	Элементы.Список.ВыделенныеСтроки.Очистить();

	Для Каждого Ссылка Из Результат.ЦепочкаДоверенностей Цикл
		Элементы.Список.ВыделенныеСтроки.Добавить(Ссылка);
	КонецЦикла;
	
	Элементы.Список.ТекущаяСтрока = Результат.Доверенность;
	
	Если Результат.ЦепочкаДоверенностей.Количество() = 1 Тогда
		ПоказатьЗначение(Неопределено, Результат.Доверенность);
	Иначе
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Загрузка завершена'"),
			ПолучитьНавигационнуюСсылку(Результат.Доверенность),
			Результат.Доверенность,
			БиблиотекаКартинок.ДиалогИнформация,
			СтатусОповещенияПользователя.Информация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСтатусНаСервере()
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "МашиночитаемыеДоверенностиФНССлужебный.ОбновитьИПрочитатьСтатусДоверенностей", Элементы.Список.ВыделенныеСтроки);
КонецФункции

&НаКлиенте
Процедура УстановитьТип(ИмяПоля, ИмяПоляТипа)
	ЭтотОбъект[ИмяПоля] = ЭтотОбъект[ИмяПоляТипа].ПривестиЗначение(ЭтотОбъект[ИмяПоля]);
КонецПроцедуры

&НаСервере
Процедура ДобавитьПолеОтбора(ИмяПоля, ИмяПоляТипа)
	РасширенноеОписаниеТипаСтороны = Новый ОписаниеТипов(Метаданные.ОпределяемыеТипы.СторонаМЧД.Тип);
	Для Каждого Тип Из РасширенноеОписаниеТипаСтороны.Типы() Цикл
		Элементы[ИмяПоляТипа].СписокВыбора.Добавить(Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Тип)), Строка(Тип));
	КонецЦикла;
	Элементы[ИмяПоляТипа].СписокВыбора.СортироватьПоПредставлению();
	ОписаниеТипа = Элементы[ИмяПоляТипа].СписокВыбора[0].Значение;
	ЭтотОбъект[ИмяПоля] = ОписаниеТипа.ПривестиЗначение(ЭтотОбъект[ИмяПоля]);
	ЭтотОбъект[ИмяПоляТипа] = ОписаниеТипа;
КонецПроцедуры

// Параметры:
//  Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ДополнительныеПараметры - Неопределено
//
&НаКлиенте
Процедура ПослеПолученияСтатуса(Результат, Контекст) Экспорт
	Если Результат.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(
			Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	СостоянияДоверенностей = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	СОшибками = 0;
	
	Для Каждого СостояниеДоверенности Из СостоянияДоверенностей Цикл
		СОшибками = СОшибками + (СостояниеДоверенности.Значение.ДанныеОшибкиЗапросаСтатуса <> Неопределено);
		ОповеститьОбИзменении(СостояниеДоверенности.Ключ);
	КонецЦикла;
	
	Если СОшибками = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Успешно загружен статус для %1 доверенностей'"), СостоянияДоверенностей.Количество());
	Иначе
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Успешно: %1, с ошибками: %2'"), СостоянияДоверенностей.Количество() - СОшибками, СОшибками);
	КонецЕсли;
	Контекст = Новый Структура("Доверенности", СостоянияДоверенностей);
	
	ОткрытьСписокСобытий = Новый ОписаниеОповещения("ОткрытьСписокСобытий", ЭтотОбъект, Контекст);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Получение статуса завершено'"),
		ОткрытьСписокСобытий,
		ТекстСообщения,
		БиблиотекаКартинок.ДиалогИнформация,
		СтатусОповещенияПользователя.Информация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗагрузкеФайлов(ЗагруженныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ЗагруженныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ИмеющиесяДоверенности = НомераСуществующихДоверенностейВИнформационнойБазе(ЗагруженныеФайлы);

	Если ЗначениеЗаполнено(ИмеющиесяДоверенности) Тогда
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить("Пропустить", НСтр("ru = 'Пропустить'"));
		Кнопки.Добавить("Заменить", НСтр("ru = 'Заменить'"));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииДиалогаЗамены", ЭтотОбъект, ЗагруженныеФайлы);
		
		Если ИмеющиесяДоверенности.Количество() = 1 Тогда
			ШаблонВопроса = НСтр("ru = 'Следующая доверенность уже существует:
				|
				|%1'");
		Иначе
			ШаблонВопроса = НСтр("ru = 'Следующие доверенности уже существуют:
				|
				|%1'");
		КонецЕсли;
		
		ПоказатьВопрос(ОписаниеОповещения, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонВопроса, СтрСоединить(ИмеющиесяДоверенности, Символы.ПС + Символы.ПС)),
			Кнопки, , "Пропустить");
		Возврат;
	КонецЕсли; 
	
	ПродолжитьЗаписьДоверенностейВИнформационнуюБазу(ЗагруженныеФайлы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииДиалогаЗамены(ВыбранноеДействие, ЗагруженныеФайлы) Экспорт
	
	Если ВыбранноеДействие = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПродолжитьЗаписьДоверенностейВИнформационнуюБазу(ЗагруженныеФайлы, ВыбранноеДействие = "Заменить");
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗаписьДоверенностейВИнформационнуюБазу(ЗагруженныеФайлы, Замещать = Ложь)
	
	РезультатЗагрузки = ЗагрузитьДоверенностиИзФайлов(ЗагруженныеФайлы, Замещать);
	ЗагруженныеДоверенности = РезультатЗагрузки.Доверенности;
	
	Элементы.Список.Обновить();
	Элементы.Список.ВыделенныеСтроки.Очистить();
	
	Для Каждого Ссылка Из ЗагруженныеДоверенности Цикл
		Элементы.Список.ВыделенныеСтроки.Добавить(Ссылка);
	КонецЦикла;
	
	Если ЗагруженныеДоверенности.Количество() > 0 Тогда
		Элементы.Список.ТекущаяСтрока = ЗагруженныеДоверенности[0];
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РезультатЗагрузки.ТекстОшибки) Тогда
		ВызватьИсключение РезультатЗагрузки.ТекстОшибки;
	КонецЕсли;
	
	Если ЗагруженныеДоверенности.Количество() = 1 Тогда
		ПоказатьЗначение(Неопределено, ЗагруженныеДоверенности[0]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НомераСуществующихДоверенностейВИнформационнойБазе(Знач ЗагруженныеФайлы)
	
	НайденныеДубликаты = Справочники.МашиночитаемыеДоверенности.НайтиДубликатыДоверенностейВИнформационнойБазе(ЗагруженныеФайлы);
	
	Результат = Новый Массив;
	Для Каждого Элемент Из НайденныеДубликаты Цикл
		Результат.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 (№%2)'"),
			Строка(Элемент.Значение),
			Элемент.Ключ));
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ЗагрузитьДоверенностиИзФайлов(Знач ЗагруженныеФайлы, Знач Замещать)
	
	Возврат Справочники.МашиночитаемыеДоверенности.ЗагрузитьДоверенностиВИнформационнуюБазу(ЗагруженныеФайлы, Замещать);
	
КонецФункции

&НаКлиенте
Процедура ОчисткаОтбора(ИмяПоля, ИмяПоляТипа)
	ОписаниеТипа = Элементы[ИмяПоляТипа].СписокВыбора[0].Значение;
	ЭтотОбъект[ИмяПоля] = ОписаниеТипа.ПривестиЗначение(Неопределено);
	ЭтотОбъект[ИмяПоляТипа] = ОписаниеТипа;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПолномочий(Результат, Контекст) Экспорт
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Полномочия = Новый СписокЗначений;
	Для Каждого Полномочие Из Результат.Полномочия Цикл
		Полномочия.Добавить(Полномочие.Код, Полномочие.Наименование);
	КонецЦикла;
	
	УстановитьПредставлениеПолномочий();
	УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокСобытий(Контекст) Экспорт
	ОткрытьФорму("РегистрСведений.МашиночитаемыеДоверенностиСтатусы.Форма.СписокРезультатов",Контекст,ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПредставлениеПолномочий()
	Если ЗначениеЗаполнено(Полномочия) Тогда
		Если ТипЗНЧ(Полномочия) = Тип("Строка") Тогда
			КоличествоПолномочий = 1;
		Иначе
			КоличествоПолномочий = Полномочия.Количество();
		КонецЕсли;         
		ПолномочияПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Отбор по полномочиям (%1)'"), КоличествоПолномочий);
	Иначе
		ПолномочияПредставление = НСтр("ru='Любые полномочия'");
	КонецЕсли;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтборДляДоверенностейПоСертификату(Знач Сертификат, Знач Представитель)
	
	Если Представитель Тогда
		Префикс = "Представитель";
	Иначе
		Префикс = "Доверитель";
	КонецЕсли;
	
	Если ТипЗнч(Сертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		ДанныеСертификата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сертификат, "ДанныеСертификата");
		Возврат МашиночитаемыеДоверенностиФНС.ОтборДляДоверенностейПоСертификату(ДанныеСертификата.Получить(), Префикс);
	Иначе
		Возврат МашиночитаемыеДоверенностиФНС.ОтборДляДоверенностейПоСертификату(Сертификат, Префикс);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ВсеОтборыПоСертификатам()
	Отбор = Новый Структура;
	КорниОтбора = СтрРазделить("Представитель,Доверитель", ",");
	СуффиксыОтбора = СтрРазделить("ОГРН,ИННФЛ,ИНН,СНИЛС", ",");
	Для Каждого КореньОтбора Из КорниОтбора Цикл
		Для Каждого СуффиксОтбора Из СуффиксыОтбора Цикл
			Отбор.Вставить(КореньОтбора + СуффиксОтбора);
		КонецЦикла;
	КонецЦикла;
	
	Возврат Отбор;
КонецФункции

&НаКлиенте
Процедура НастроитьОтборыСертификата(ВсеОтборыПоСертификатам, СертификатОтбора, Представитель)
	Если ЗначениеЗаполнено(СертификатОтбора) Тогда

		Если ТипЗнч(СертификатОтбора) = Тип("Структура") Тогда
			Отборы = Новый Структура;
			ТипСтороны = ?(Представитель, "Представитель", "Доверитель");
			Для Каждого ЭлементОтбора Из СертификатОтбора Цикл
				Отборы.Вставить(ТипСтороны + ЭлементОтбора.Ключ, ЭлементОтбора.Значение);
			КонецЦикла;
		Иначе
			Отборы = ОтборДляДоверенностейПоСертификату(СертификатОтбора, Представитель);
		КонецЕсли;
		
		Для Каждого ЭлементОтбора Из Отборы Цикл
			Если Не ВсеОтборыПоСертификатам.Свойство(ЭлементОтбора.Ключ)Тогда
				Продолжить;
			КонецЕсли;
			
			ВсеОтборыПоСертификатам[ЭлементОтбора.Ключ] = ?(ЭлементОтбора.Значение = Неопределено, "", ЭлементОтбора.Значение);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура СертификатОкончаниеВыбора(Результат, Контекст) Экспорт
	Если ТипЗнч(Результат) = Тип("ДвоичныеДанные") Тогда
		СвойстваСертификата = Ждать ЭлектроннаяПодписьСлужебныйКлиент.СвойстваСертификата(Новый СертификатКриптографии(Результат));
		Сертификат = СвойстваСертификата.Представление;
		СертификатПредставителя = Результат;
	Иначе
		Сертификат = Неопределено;
		СертификатПредставителя = Неопределено;
	КонецЕсли;
	УстановитьОтбор();
КонецПроцедуры

#КонецОбласти