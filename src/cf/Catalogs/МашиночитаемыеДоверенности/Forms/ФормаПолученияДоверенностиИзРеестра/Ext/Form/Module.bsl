///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НомерДоверенности = Параметры.НомерДоверенности;
	ИННДоверителя = Параметры.ИННДоверителя;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Получить(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	НомерДоверенности = СокрЛП(НомерДоверенности);
	ИННДоверителя = СокрЛП(ИННДоверителя);
	Доверенность = НайтиДоверенность(НомерДоверенности);
	
	Если Доверенность <> Неопределено Тогда
		ШаблонВопроса = НСтр("ru = 'Следующая доверенность уже есть существует:
			|
			|%1'");
		
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонВопроса, Доверенность);
			
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить("НеЗагружать", НСтр("ru = 'Не загружать'"));
		Кнопки.Добавить("Заменить", НСтр("ru = 'Загрузить и заменить'"));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииДиалогаЗамены", ЭтотОбъект);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , "НеЗагружать");
		Возврат;
	КонецЕсли;
	
	ПродолжитьЗагрузкуДоверенностиИзРеестра();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииДиалогаЗамены(ВыбранноеДействие, ЗагруженныеФайлы) Экспорт
	
	Если ВыбранноеДействие = Неопределено Или ВыбранноеДействие = "НеЗагружать" Тогда
		Возврат;
	КонецЕсли;
	
	ПродолжитьЗагрузкуДоверенностиИзРеестра();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗагрузкуДоверенностиИзРеестра()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриПолученииФайлов", ЭтотОбъект);
	МашиночитаемыеДоверенностиФНССлужебныйКлиент.ПолучитьДоверенностьИзРеестра(
		ОписаниеОповещения, НомерДоверенности, ИННДоверителя);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриПолученииФайлов(Результат, ДополнительныеДанные) Экспорт
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиДоверенность(НомерДоверенности)
	
	Возврат Справочники.МашиночитаемыеДоверенности.НайтиДоверенность(НомерДоверенности);
	
КонецФункции

#КонецОбласти


