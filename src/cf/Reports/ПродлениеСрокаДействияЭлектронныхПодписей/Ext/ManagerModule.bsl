///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.
Процедура ПередДобавлениемКомандОтчетов(КомандыОтчетов, Параметры, СтандартнаяОбработка) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Отчеты.ПродлениеСрокаДействияЭлектронныхПодписей) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЭлектроннаяПодписьСлужебныйПовтИсп.ДоступнаУсовершенствованнаяПодпись() Тогда
		Возврат;
	КонецЕсли;
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Менеджер = "Отчет.ПродлениеСрокаДействияЭлектронныхПодписей";
	Команда.КлючВарианта = "НеобработанныеПодписи";
	Команда.Представление = НСтр("ru = 'Необработанные подписи.'");
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Менеджер = "Отчет.ПродлениеСрокаДействияЭлектронныхПодписей";
	Команда.КлючВарианта = "ТребуетсяУсовершенствоватьПодписи";
	Команда.Представление = НСтр("ru = 'Требуется усовершенствовать подписи.'");
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Менеджер = "Отчет.ПродлениеСрокаДействияЭлектронныхПодписей";
	Команда.КлючВарианта = "ТребуетсяДобавитьАрхивныеМетки";
	Команда.Представление = НСтр("ru = 'Требуется добавить архивные метки в подписи.'");
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Менеджер = "Отчет.ПродлениеСрокаДействияЭлектронныхПодписей";
	Команда.КлючВарианта = "ОшибкиПриАвтоматическомПродлении";
	Команда.Представление = НСтр("ru = 'Ошибки при автоматическом продлении подписей.'");
	
	
КонецПроцедуры

// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   НастройкиОтчета - см. ВариантыОтчетов.ОписаниеОтчета.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	Иначе
		Возврат;
	КонецЕсли;
	
	ДоступнаУсовершенствованнаяПодпись = ЭлектроннаяПодписьСлужебныйПовтИсп.ДоступнаУсовершенствованнаяПодпись();
	
	Если Не ОбщегоНазначения.РазделениеВключено()
		Или ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
		УсовершенствоватьПодписиАвтоматически = Константы.УсовершенствоватьПодписиАвтоматически.Получить();
		ДобавлятьМеткиВремениАвтоматически = Константы.ДобавлятьМеткиВремениАвтоматически.Получить();
	Иначе
		УсовершенствоватьПодписиАвтоматически = 0;
		ДобавлятьМеткиВремениАвтоматически = Ложь;
	КонецЕсли;
	
	МодульВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Ложь);
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	НастройкиОтчета.Описание = НСтр("ru = 'Подписи, срок действия которых должен быть продлен.'");
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "НеобработанныеПодписи");
	НастройкиВарианта.Описание = НСтр("ru = 'Необработанные подписи.'");
	НастройкиВарианта.Включен = ДоступнаУсовершенствованнаяПодпись И УсовершенствоватьПодписиАвтоматически <> 0;

	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ТребуетсяУсовершенствоватьПодписи");
	НастройкиВарианта.Описание = НСтр("ru = 'Требуется усовершенствовать подписи.'");
	НастройкиВарианта.Включен = ДоступнаУсовершенствованнаяПодпись И УсовершенствоватьПодписиАвтоматически <> 0;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ТребуетсяДобавитьАрхивныеМетки");
	НастройкиВарианта.Описание = НСтр("ru = 'Требуется добавить архивные метки в подписи.'");
	НастройкиВарианта.Включен = ДоступнаУсовершенствованнаяПодпись И ДобавлятьМеткиВремениАвтоматически;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ОшибкиПриАвтоматическомПродлении");
	НастройкиВарианта.Описание = НСтр("ru = 'Ошибки при автоматическом продлении подписей.'");
	НастройкиВарианта.Включен = ДоступнаУсовершенствованнаяПодпись
	 И (ДобавлятьМеткиВремениАвтоматически Или УсовершенствоватьПодписиАвтоматически = 1);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти

#КонецЕсли